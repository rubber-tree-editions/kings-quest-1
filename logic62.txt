#include "vars.txt"

LOGIC__HEADER

#define rfFALLING_SOUND_PLAYED rf0
#define rfHIDE_EGO rf1

#define rvTEMP_PRIORITY rv0

#if RUBBER_TREE
#define rvRESTORE_EGO_X rv1
#define rvRESTORE_EGO_Y rv2
#define rvRESTORE_EGO_DIR rv3
#endif

#if RUBBER_TREE
if (fUNDO && fGAME_OVER) {
  reset(fSCRIPTED_MOTION);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, rvRESTORE_EGO_X, rvRESTORE_EGO_Y);
  reset(fCYCLE_EGO_ANIMATION);
  stop.cycling(oEGO);
  set.view(oEGO, 0);
  set.dir(oEGO, rvRESTORE_EGO_DIR);
  release.priority(oEGO);
  draw(oEGO);
  reset(fUNDO);
  reset(fGAME_OVER);
  reset(fFALLING);
  player.control();
  return;
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  #if VERSION_S
    load.sound(23);
  #endif
  set.horizon(45);
  load.sound(SND_FALLING);
  draw(oEGO);
  show.pic();
}
if (said("examine", "room") || said("examine", "clouds")) {
  print("You are in the Land of the Clouds.  It is rumored that a giant lives up here.");
}
if (said("examine", "tree")) {
  if (posn(oEGO, 86, 101, 159, 167)) {
    print("Its trunk has a large hole in the base.");
  }
  else {
    print("These are strange looking trees.");
  }
}
if (said("examine", "hole") || said("look inside", "hole")) {
  if (posn(oEGO, 117, 100, 144, 117)) {
    if (!fSLINGSHOT_TAKEN) {
      print("You notice a sling in the hole in the tree.");
    }
    else {
      print("It is empty.");
    }
  }
  else {
    print("You can't see into the hole from here.");
  }
}
if (said("examine", "slingshot")) {
  if (fCARRYING_SLINGSHOT) {
    print("This sling is made of very stretchy leather.");
  }
  else {
    print("Why?  What sling?");
  }
}
if (fSLINGSHOT_TAKEN) {
  if (said("get", "slingshot")) {
    if (posn(oEGO, 117, 100, 144, 117)) {
      print("Why?  What sling?");
    }
    else {
      print("You need to be closer to try.");
    }
  }
}
if (!fSLINGSHOT_TAKEN) {
  if (said("get", "slingshot")) {
    if (posn(oEGO, 117, 100, 144, 117)) {
      #if VERSION_S
        sound(23, fMISC_END_MARKER);
      #endif
      print("You reach into the hole and pull out the leather sling.");
      set(fSLINGSHOT_TAKEN);
      set(fCARRYING_SLINGSHOT);
      vSCORE += 2;
      get("leather slingshot");
    }
    else {
      print("You need to be closer to try.");
    }
  }
}
if (fEGO_TOUCHED_TRIGGER_LINE) {
  #if RUBBER_TREE
    get.posn(oEGO, rvRESTORE_EGO_X, rvRESTORE_EGO_Y);
    if (rvRESTORE_EGO_X < 148) {
      rvRESTORE_EGO_Y -= 5;
    }
    else {
      rvRESTORE_EGO_X -= 5;
    }
    rvRESTORE_EGO_DIR = vEGO_DIR;
  #endif
  #if VERSION_S
    prevent.input();
    set(f215);
    set(fFALLING);
  #endif
  set(fSCRIPTED_MOTION);
  program.control();
  stop.cycling(oEGO);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvTEMP_PRIORITY);
  set.priority.v(oEGO, rvTEMP_PRIORITY);
  vTEMP_Y += 6;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfHIDE_EGO);
  sound(SND_FALLING, fSOUND_END_MARKER);
}
if (rfHIDE_EGO) {
  reset(rfHIDE_EGO);
  erase(oEGO);
}
if (fSOUND_END_MARKER) {
  reset(fSOUND_END_MARKER);
  set(rfFALLING_SOUND_PLAYED);
}
if (rfFALLING_SOUND_PLAYED) {
  reset(rfFALLING_SOUND_PLAYED);
  print("You've fallen through the clouds!  Cloud walking is for the birds.");
  erase(oEGO);
  set(fGAME_OVER);
  #if VERSION_S
    accept.input();
  #endif
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM59_CLOUDS_MOUNTAIN_CAVE);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM61_CLOUDLANDS_SOUTH);
}
#if VERSION_S
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
if (vTEMP_Y > 150 || vTEMP_X > 140 || posn(oEGO, 118, 120, 154, 167)) {
  set(f216);
}
else {
  reset(f216);
}
#endif
return;

LOGIC__FOOTER

#ifndef LOGIC_AS_C
#message 1 "Its trunk has a large hole in the base."
#message 2 "You notice a sling in the hole in the tree."
#message 3 "It is empty."
#message 4 "This sling is made of very stretchy leather."
#message 5 "You reach into the hole and pull out the leather sling."
#message 6 "You can't see into the hole from here."
#message 7 "Why?  What sling?"
#message 8 "You are in the Land of the Clouds.  It is rumored that a giant lives up here."
#message 9 "You've fallen through the clouds!  Cloud walking is for the birds."
#message 10 "These are strange looking trees."
#message 11 "You need to be closer to try."
#endif
