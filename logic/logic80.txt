#include "vars.txt"

#define PT_WITCH_START 1, 60
#define PT_WITCH_END 130, 60
#define PT_BIRD_START 1, 60
#define PT_BIRD_END 135, 60

#define rfTO_WITCHS_HOUSE rf0
#define rfTO_LEPRECHAUN_HOLE rf1
#define rfPLAYED_SND_BIRD rf2

#define rvCOUNTDOWN_TO_BIRD_SOUND rv0
#define rvCOUNTDOWN_TO_WITCH_SOUND rv1

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (fCARRIED_BY_WITCH) {
    if (BUILD_TARGET == APPLE_IIGS) {
      load.sound(SND_CARRIED_BY_WITCH);
    }
    load.view(91);
    set.view(oEGO, 91);
    position(oEGO, PT_WITCH_START);
    move.obj(oEGO, PT_WITCH_END, 0, rfTO_WITCHS_HOUSE);
    if (BUILD_TARGET == APPLE_IIGS) {
      rvCOUNTDOWN_TO_WITCH_SOUND = 35;
    }
  }
  else {
    load.view(89);
    set.view(oEGO, 89);
    position(oEGO, PT_BIRD_START);
    move.obj(oEGO, PT_BIRD_END, 0, rfTO_LEPRECHAUN_HOLE);
    if (BUILD_TARGET == APPLE_IIGS) {
      rvCOUNTDOWN_TO_BIRD_SOUND = 12;
      set(rfPLAYED_SND_BIRD);
      load.sound(SND_BIRD);
    }
  }
  draw(oEGO);
  show.pic();
}
if (BUILD_TARGET == APPLE_IIGS) {
  if (!fCARRIED_BY_WITCH && rfPLAYED_SND_BIRD) {
    rvCOUNTDOWN_TO_BIRD_SOUND--;
    if (rvCOUNTDOWN_TO_BIRD_SOUND == 1) {
      rvCOUNTDOWN_TO_BIRD_SOUND = 35;
      sound(SND_BIRD, rfPLAYED_SND_BIRD);
    }
  }
  else {
    rvCOUNTDOWN_TO_WITCH_SOUND--;
    if (rvCOUNTDOWN_TO_WITCH_SOUND == 1) {
      rvCOUNTDOWN_TO_WITCH_SOUND = 35;
      sound(SND_CARRIED_BY_WITCH, fMISC_END_MARKER);
    }
  }
}
if (said("examine", "view") || said("examine", "clouds") || said("examine", "room")) {
  print("The view is magnificent.");
}
if (said("anyword", "witch")) {
  print("Now is not the time to irritate the witch.");
}
if (said("anyword", "bird")) {
  print("Now is not a good time to bother the bird.");
}
if (rfTO_WITCHS_HOUSE) {
  new.room(ROOM65_IN_GINGERBREAD_HOUSE);
}
if (rfTO_LEPRECHAUN_HOLE) {
  new.room(ROOM48_LEPRECHAUN_HOLE);
}
return;

#message 1 "The view is magnificent."
#message 2 "Now is not the time to irritate the witch."
#message 3 "Now is not a good time to bother the bird."
