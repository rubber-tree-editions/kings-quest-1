#include "vars.txt"

#define PT_DEAD_GOAT 26, 64

#define BX_NEAR_DOORWAY 100, 111, 133, 136
#define BX_NEAR_AXE 61, 80, 88, 118
#define BX_NEAR_PUMP 24, 115, 61, 149
#define BX_COTTAGE_ENTRY_ZONE 126, 119, 128, 127

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  set.horizon(41);
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  if (vPREV_ROOM_NO == ROOM37_SWAMP && fEGO_ON_WATER) {
    set.view(oEGO, 0);
    reset(fCYCLE_EGO_ANIMATION);
    vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
  }
  if (vPREV_ROOM_NO == ROOM79_IN_WOODCUTTER_COTTAGE) {
    position(oEGO, 118, 123);
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "pump")) {
  print("It is an ancient, rusty water pump.  It probably doesn't even work.");
}
if (said("examine", "axe")) {
  print("The handle has been broken and repaired.  The head shows the signs of many sharpenings.");
}
if (said("examine", "building")) {
  print("It was made from rough, hand-hewn logs long ago.  Lately, the upkeep has faltered.  There are even holes in the  roof.");
}
if (said("examine", "stumps")) {
  print("The trees were cut from these stumps years ago.");
}
if (said("use", "pump") || said("pump", "water")) {
  if (posn(oEGO, BX_NEAR_PUMP)) {
    print("The rusty pump doesn't work.");
  }
  else {
    print("You are not close enough.");
  }
}
if (said("get", "axe")) {
  if (posn(oEGO, BX_NEAR_AXE)) {
    print("The old axe is tightly wedged in the tree stump.  You cannot remove it no matter what you do.");
  }
  else {
    print("You are not close enough.");
  }
}
if (said("use", "axe")) {
  if (posn(oEGO, BX_NEAR_AXE)) {
    print("How are you going to use the ax when you can't even get it out of this stump?");
  }
  else {
    print("You are not close enough.");
  }
}
if (said("open", "doors") || said("close", "doors")) {
  if (posn(oEGO, BX_NEAR_DOORWAY)) {
    print("Upon closer inspection you realize this poor cabin doesn't even have a door.");
  }
  else {
    print("You are not close enough.");
  }
}
if (said("examine", "doors") || said("examine", "doorway") || said("look inside", "doors")) {
  if (posn(oEGO, BX_NEAR_DOORWAY)) {
    print("When you look in the doorway, you see a poor, starving woodcutter and his wife.");
  }
  else {
    print("You are not close enough.");
  }
}
if (VERSION_MAJOR >= 2 && (said("examine", "woodcutter") || said("examine", "wife"))) {
  if (posn(oEGO, BX_NEAR_DOORWAY)) {
    print("Perhaps you should go inside the house for a better look.");
  }
  else {
    print("You are not close enough.");
  }
}
if (posn(oEGO, BX_COTTAGE_ENTRY_ZONE)) {
  new.room(ROOM79_IN_WOODCUTTER_COTTAGE);
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM5_SERENE_LAKE_WEST);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM45_OUTSIDE_WOODCUTTER_COTTAGE_BACK);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM43_CLEAR_BLUE_LAKE);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM37_SWAMP);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
return;

#message 1 "It is an ancient, rusty water pump.  It probably doesn't even work."
#message 2 "The rusty pump doesn't work."
#message 3 "The handle has been broken and repaired.  The head shows the signs of many sharpenings."
#message 4 "It was made from rough, hand-hewn logs long ago.  Lately, the upkeep has faltered.  There are even holes in the  roof."
#message 5 "The old axe is tightly wedged in the tree stump.  You cannot remove it no matter what you do."
#message 6 "Upon closer inspection you realize this poor cabin doesn't even have a door."
#message 7 "The trees were cut from these stumps years ago."
#message 8 "When you look in the doorway, you see a poor, starving woodcutter and his wife."
#message 9 "You are not close enough."
#message 10 "How are you going to use the ax when you can't even get it out of this stump?"
#if VERSION_MAJOR >= 2
  #message 11 "Perhaps you should go inside the house for a better look."
#endif
