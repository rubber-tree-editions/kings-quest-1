#include "vars.txt"

#define PT_DEAD_GOAT 110, 110

#define BX_NEAR_STUMP 66, 134, 159, 167
#define BX_NEAR_RIVER 0, 27, 95, 95

#define rfPULLED_UNDERWATER rf0
#define rfPLAYED_SND_RUSHING_WATER rf1

#if RUBBER_TREE
if (fUNDO && rfPULLED_UNDERWATER) {
  reset(fUNDO);
  reset(rfPULLED_UNDERWATER);
  vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
  vDROWN_GAME_OVER_COUNTDOWN = 0;
  stop.sound();
  erase(oEGO);
  set.view(oEGO, 0);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  draw(oEGO);
  reset(fCYCLE_EGO_ANIMATION);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  set.horizon(46);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_RUSHING_WATER);
  }
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  load.logics(LOGIC101_SWIMMING);
  call(LOGIC101_SWIMMING);
  set(fSWIMMING_IMPOSSIBLE_HERE);
  if (vPREV_ROOM_NO == ROOM23_MIRRORLIKE_LAKE) {
    if (fEGO_ON_WATER) {
      set.view(oEGO, 0);
      reset(fCYCLE_EGO_ANIMATION);
      vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
    }
  }
  ignore.blocks(oEGO);
  draw(oEGO);
  show.pic();
  if (BUILD_TARGET == APPLE_IIGS) {
    set(rfPLAYED_SND_RUSHING_WATER);
  }
}
if (BUILD_TARGET == APPLE_IIGS) {
  if (!rfPULLED_UNDERWATER && rfPLAYED_SND_RUSHING_WATER && !fEGO_PLAYING_FIDDLE) {
    sound(SND_RUSHING_WATER, rfPLAYED_SND_RUSHING_WATER);
  }
}
if (said("examine", "hole") || said("look inside", "hole") || said("look inside", "stumps")) {
  if (posn(oEGO, BX_NEAR_STUMP)) {
    print("It is just a hole in an empty, old stump.");
  }
  else {
    print("You are not close enough.");
  }
}
if (said("examine", "stumps")) {
  print("It is old and rotted.");
}
if (said("examine", "stream")) {
  print("The stream appears placid enough, but appearances can be deceiving.");
}
if (said("examine", "anyword")) {
  print("There is an attractive meadow dotted with trees.");
}
if (fCARRYING_BUCKET && !fBUCKET_FILLED) {
  if (said("get", "water") || said("drink", "water")) {
    if (posn(oEGO, BX_NEAR_RIVER)) {
      print("The bank is too steep to allow access to the river's edge.");
    }
    else {
      print("You are not close enough to the river.");
    }
  }
}
if (fEGO_ON_WATER && !rfPULLED_UNDERWATER) {
  set(rfPULLED_UNDERWATER);
  print("This placid looking stream is actually quite dangerous.... the current is stronger than it looks.  You can't keep your head above water.");
}
if (vEGO_EDGE_CODE != EDGE_NONE) {
  reset(fSWIMMING_IMPOSSIBLE_HERE);
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM39_WEATHER_BEATEN_BRIDGE);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM27_BIG_FOREGROUND_TREE);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM25_RICKETY_BRIDGE);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM23_MIRRORLIKE_LAKE);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
call(LOGIC101_SWIMMING);
#if RUBBER_TREE
  if (!fEGO_ON_WATER && !rfPULLED_UNDERWATER) {
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    if (vRESTORE_Y >= 61) {
      vRESTORE_Y += 5;
    }
    else if (vRESTORE_X < 25) {
      vRESTORE_Y -= 4;
    }
    else if (vRESTORE_X >= 72) {
      vRESTORE_X += 5;
    }
    else if (vRESTORE_X < 55) {
      vRESTORE_X -= 2;
      vRESTORE_Y -= 2;
    }
    vRESTORE_DIR = vEGO_DIR;
  }
#endif
return;

#message 1 "This placid looking stream is actually quite dangerous.... the current is stronger than it looks.  You can't keep your head above water."
#message 2 "The bank is too steep to allow access to the river's edge."
#message 3 "You are not close enough to the river."
#message 4 "The stream appears placid enough, but appearances can be deceiving."
#message 5 "It is old and rotted."
#message 6 "There is an attractive meadow dotted with trees."
#message 7 "It is just a hole in an empty, old stump."
#message 8 "You are not close enough."
