#include "vars.txt"

#define rfPLAYED_SND_FALLING rf0
#define rfFALLING rf1
#define rfFELL rf2

#define rvLOCK_PRIORITY rv0

#if BUILD_TARGET == APPLE_IIGS
  #define _fFALLING fFALLING
#else
  #define _fFALLING rfFALLING
#endif

#if RUBBER_TREE
if (fUNDO && (_fFALLING || fGAME_OVER)) {
  reset(_fFALLING);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  reset(fCYCLE_EGO_ANIMATION);
  stop.cycling(oEGO);
  stop.motion(oEGO);
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  set.view(oEGO, 0);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  release.priority(oEGO);
  draw(oEGO);
  reset(fUNDO);
  reset(_fFALLING);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  ignore.horizon(oEGO);
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.sound(SND_FALLING);
  if (vPREV_ROOM_NO == ROOM56_CLOUDS_GRASS_EDGE) {
    position(oEGO, 145, 109);
  }
  draw(oEGO);
  show.pic();
}
if (fEGO_TOUCHED_TRIGGER_LINE && !_fFALLING) {
  #if RUBBER_TREE
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    if (vRESTORE_X < 54) {
      vRESTORE_X += 5;
    }
    else if (vRESTORE_Y < 105) {
      vRESTORE_Y += 3;
    }
    else {
      vRESTORE_Y -= 3;
    }
    vRESTORE_DIR = vEGO_DIR;
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    prevent.input();
    set(fEGO_PREOCCUPIED);
  }
  set(_fFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvLOCK_PRIORITY);
  set.priority.v(oEGO, rvLOCK_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 6;
  sound(SND_FALLING, fSOUND_END_MARKER);
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL);
}
if (rfFELL) {
  reset(rfFELL);
  #if RUBBER_TREE
  if (_fFALLING)
  #endif
  {
    erase(oEGO);
  }
}
if (fSOUND_END_MARKER) {
  reset(fSOUND_END_MARKER);
  set(rfPLAYED_SND_FALLING);
}
if (rfPLAYED_SND_FALLING) {
  reset(rfPLAYED_SND_FALLING);
  #if RUBBER_TREE
  if (_fFALLING)
  #endif
  {
    print("So you're walking on clouds, huh?  Unfortunately, that's an impossible task that cost you your life!");
    erase(oEGO);
    if (BUILD_TARGET == APPLE_IIGS) {
      accept.input();
    }
    set(fGAME_OVER);
  }
}
if (said("examine", "anyword")) {
  print("You are in the Land of the Clouds.  It is rumored that a giant lives up here.");
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM56_CLOUDS_GRASS_EDGE);
}
if (BUILD_TARGET == APPLE_IIGS) {
  set(fFALL_DANGER);
}
return;

#message 1 "So you're walking on clouds, huh?  Unfortunately, that's an impossible task that cost you your life!"
#message 2 "You are in the Land of the Clouds.  It is rumored that a giant lives up here."
