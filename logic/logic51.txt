#include "vars.txt"

#define PT_DEAD_DRAGON 40, 152

#define BX_NEAR_MIRROR 26, 104, 59, 127
#define BX_PAST_BOULDER 0, 0, 1, 167

#define rfDRAGON_GOT_YOU rf0
#define rfDRAGON_AT_BOULDER rf1
#define rfBOULDER_MOVED rf2
#define rfDRAGON_LEFT rf3
#define rfPLAYING_SND_VILLAIN rf4
#define rfPLAYED_SND_VILLAIN rf5

#define rvDRAGON_STEP_SIZE rv0
#define rvDRAGON_CURRENT_LOOP rv1
#define rvTEMP_CEL rv2

#define oMAGIC_MIRROR o1
#define oBOULDER o2
#define oDRAGON o3

#if RUBBER_TREE
if (fUNDO) {
  if (fGAME_OVER) {
    set(fRESTORE_STATE);
    stop.sound();
    reset(fUNDO);
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    erase(oEGO);
    erase(oDRAGON);
    set.view(oEGO, 0);
    position(oEGO, 133, 136);
    vEGO_DIR = DIR_WEST;
    set.view(oDRAGON, 33);
    position(oDRAGON, 40, 120);
    draw(oEGO);
    draw(oDRAGON);
    reset(rfPLAYING_SND_VILLAIN);
    follow.ego(oDRAGON, DRAGON_REACH_DISTANCE, rfDRAGON_GOT_YOU);
    player.control();
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
    set(fSTORE_STATE);
  #endif
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (!fTAKEN_MIRROR) {
    if (BUILD_TARGET == APPLE_IIGS) {
      load.sound(SND_TREASURE);
    }
    load.view(34);
  }
  if (vDRAGON_STATE == DRAGONSTATE_DEAD) {
    load.view(61);
  }
  if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    load.view(33);
    load.view(61);
    load.view(82);
    if (BUILD_TARGET == APPLE_IIGS) {
      load.sound(SND_DRAGON2);
      load.sound(SND_DRAGON);
      load.sound(SND_MOVING_ROCK);
      load.sound(SND_GIFT);
    }
    else {
      load.sound(SND_VILLAIN);
      load.sound(SND_DRAGON);
    }
  }
  load.view(35);
  if (vPREV_ROOM_NO == ROOM52_IN_WELL_UNDERWATER) {
    set.view(oEGO, 0);
    reset(fCYCLE_EGO_ANIMATION);
    vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
    position(oEGO, 133, 136);
    if (!fPOINTS_FOR_FINDING_DRAGON) {
      set(fPOINTS_FOR_FINDING_DRAGON);
      vSCORE += 1;
    }
  }
  if (vPREV_ROOM_NO == ROOM50_CAVE_ADJOINING_DRAGONS_LAIR) {
    position(oEGO, 9, 138);
  }
  if (!fTAKEN_MIRROR) {
    animate.obj(oMAGIC_MIRROR);
    set.view(oMAGIC_MIRROR, 34);
    position(oMAGIC_MIRROR, 38, 108);
    draw(oMAGIC_MIRROR);
    stop.update(oMAGIC_MIRROR);
  }
  if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    animate.obj(oBOULDER);
    set.view(oBOULDER, 35);
    ignore.blocks(oBOULDER);
    position(oBOULDER, 8, 127);
    draw(oBOULDER);
    stop.cycling(oBOULDER);
  }
  if (vDRAGON_STATE != DRAGONSTATE_ABSENT) {
    animate.obj(oBOULDER);
    set.view(oBOULDER, 35);
    ignore.blocks(oBOULDER);
    position(oBOULDER, 3, 127);
    draw(oBOULDER);
    stop.cycling(oBOULDER);
    animate.obj(oDRAGON);
    if (vDRAGON_STATE == DRAGONSTATE_DEAD) {
      add.to.pic(61, 0, 0, PT_DEAD_DRAGON, 0, 0);
    }
    if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
      set.view(oDRAGON, 33);
      position(oDRAGON, 40, 120);
      rvDRAGON_STEP_SIZE = DRAGON_STEP_SIZE;
      step.size(oDRAGON, rvDRAGON_STEP_SIZE);
      draw(oDRAGON);
      observe.blocks(oDRAGON);
      follow.ego(oDRAGON, DRAGON_REACH_DISTANCE, rfDRAGON_GOT_YOU);
    }
  }
  draw(oEGO);
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fMISC_END_MARKER);
  }
  show.pic();
}
get.posn(oEGO, vTEMP_X, vTEMP_Y);
if (vTEMP_X > 15) {
  ignore.blocks(oEGO);
}
if (vTEMP_X < 16) {
  if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    ignore.blocks(oEGO);
  }
  else {
    observe.blocks(oEGO);
  }
}
if (vDRAGON_STATE == DRAGONSTATE_NORMAL && !rfPLAYING_SND_VILLAIN) {
  set(rfPLAYING_SND_VILLAIN);
  if (BUILD_TARGET == APPLE_IIGS) {
    sound(SND_DRAGON2, rfPLAYED_SND_VILLAIN);
  }
  else {
    sound(SND_VILLAIN, rfPLAYED_SND_VILLAIN);
  }
}
if (vDRAGON_STATE == DRAGONSTATE_NORMAL && rfPLAYED_SND_VILLAIN) {
  current.loop(oDRAGON, rvDRAGON_CURRENT_LOOP);
  if (BUILD_TARGET == APPLE_IIGS) {
    current.cel(oDRAGON, rvTEMP_CEL);
  }
  if (rvDRAGON_CURRENT_LOOP == 0
    #if BUILD_TARGET == APPLE_IIGS
      && rvTEMP_CEL == 1 && fMISC_END_MARKER
    #endif
  ) {
    sound(SND_DRAGON, fMISC_END_MARKER);
  }
  #if BUILD_TARGET != APPLE_IIGS
  else {
    stop.sound();
  }
  #endif
}
if (rfDRAGON_GOT_YOU && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
  reset(rfDRAGON_GOT_YOU);
  set.view(oEGO, 82);
  if (BUILD_TARGET == APPLE_IIGS) {
    wander(oDRAGON);
  }
  if (fCARRYING_SHIELD) {
    print("Your shield melted from the intensity of the dragon's flames!  You should know about these fire-breathing dragons!  Maybe next time you will be a little more careful!");
  }
  else {
    print("You should know about these fire-breathing dragons!  Maybe next time you will be a little more careful!");
  }
  set(fGAME_OVER);
}
if (said("examine", "room")) {
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    print("The ferocious fire-breathing dragon is protecting the magic mirror.");
  }
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("There is a dead dragon, a large boulder, and a seemingly ordinary mirror in the cave.");
  }
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("There is a large granite boulder and a magic mirror!! in the cave.");
  }
}
if (said("examine", "cave")) {
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    print("A green, scaly dragon breathing hot fire and smoke is protecting the mirror.");
  }
  if (fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("In this large cavern, there is a slimy dead dragon, an opening on one side, and a large boulder in the other.");
  }
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("There is a dead dragon, a large boulder, and a seemingly ordinary mirror in the cave.");
  }
  if (!fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("There is a large granite boulder and a magic mirror!! in the cave.");
  }
  if (fTAKEN_MIRROR && vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("The cavern is empty, save for a couple of openings on either side.");
  }
}
if (said("examine", "dragon")) {
  if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("There is no dragon here.");
  }
  if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    print("This ferocious, fire-breathing dragon with sharp, green scales and a long tail is protecting the magic mirror. Watch out for the flames!");
  }
  if (vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("You see the once powerful dragon lying lifeless before you.");
  }
}
if (said("find", "dragon")) {
  if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("There is no dragon here.");
  }
  if (vDRAGON_STATE == DRAGONSTATE_NORMAL || vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("I think you found the dragon!");
  }
}
if (said("examine", "stalactites") || said("examine", "stalactites")) {
  print("It took millenniums for these stalagmites and stalactites to form.");
}
if (said("examine", "rocks")) {
  print("This boulder is a huge chunk of granite.");
}
if (said("shove", "rocks") || said("shove", "rocks")) {
  print("This boulder is far too heavy to move.");
}
if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
  if (said("examine", "hole") || said("look inside", "hole")) {
    print("A strange watery light issues from the hole.");
  }
  if (said("talk", "dragon") || said("hello") || said("say", "hello")) {
    print("This dragon does not respond to reason.");
  }
}
if (vDRAGON_STATE != DRAGONSTATE_NORMAL) {
  if (said("examine", "hole") || said("look inside", "hole")) {
    if (posn(oEGO, 1, 1, 75, 166)) {
      print("The hole seems to lead further into the cave.");
    }
    else {
      print("A strange watery light issues from the hole.");
    }
  }
}
if (vDRAGON_STATE == DRAGONSTATE_DEAD) {
  if (said("get", "dagger")) {
    print("The mere thought of retrieving the dagger repulses you.");
  }
}
if (fCARRYING_DAGGER) {
  if (said("use", "dagger")) {
    print("How?");
  }
}
if (said("murder", "dragon")) {
  if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
    print("How?");
  }
  if (vDRAGON_STATE == DRAGONSTATE_DEAD) {
    print("This dragon had only one life and you have taken it.");
  }
  if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    if (posn(oEGO, 105, 96, 145, 166)) {
      print("You can't do that from here.!");
    }
    else {
      print("How do you wish to accomplish that?");
    }
  }
}
if (fCARRYING_BUCKET) {
  if (said("throw", "bucket")) {
    print("You throw your bucket on the cavern floor.");
    reset(fCARRYING_BUCKET);
    drop("water bucket");
    vSCORE -= 2;
    if (fBUCKET_FILLED) {
      reset(fBUCKET_FILLED);
      drop("water");
      reset(fFILL_BUCKET_POINTS);
      vSCORE -= 2;
    }
  }
}
if (!fCARRYING_MIRROR && fTAKEN_MIRROR) {
  if (said("anyword", "mirror")) {
    print("There is no mirror here.");
  }
}
if (!fCARRYING_MIRROR && !fTAKEN_MIRROR) {
  if (said("steal", "mirror") && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
    print("With that dragon here?  No way!");
  }
  if (posn(oEGO, BX_NEAR_MIRROR)) {
    if (said("examine", "mirror")) {
      if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
        print("The dragon is protecting the magic mirror.");
      }
      if (vDRAGON_STATE == DRAGONSTATE_ABSENT || vDRAGON_STATE == DRAGONSTATE_DEAD) {
        print("The magic mirror is framed with ornate mahogany wood, polished to a high sheen.");
      }
    }
    if (said("get", "mirror")) {
      if (vDRAGON_STATE == DRAGONSTATE_NORMAL) {
        print("I do not think that you can get close enough to the magic mirror with the dragon here.");
      }
      if (vDRAGON_STATE == DRAGONSTATE_ABSENT || vDRAGON_STATE == DRAGONSTATE_DEAD) {
        start.update(oMAGIC_MIRROR);
        erase(oMAGIC_MIRROR);
        set(fCARRYING_MIRROR);
        set(fTAKEN_MIRROR);
        get("magic mirror");
        if (BUILD_TARGET == APPLE_IIGS) {
          sound(SND_TREASURE, fMISC_END_MARKER);
        }
        print("Carefully, you pick up the magic mirror.");
        vTREASURE_COUNT++;
        vSCORE += 8;
      }
    }
  }
  if (!posn(oEGO, BX_NEAR_MIRROR)) {
    if (said("examine", "mirror")) {
      print("You could get a much better view if you were closer.");
    }
    if (said("get", "mirror")) {
      print("You are not close enough to reach it.");
    }
  }
}
if (!fBUCKET_FILLED) {
  if (said("douse", "dragon") || said("throw", "water") || said("throw", "water", "dragon")) {
    if (vDRAGON_STATE != DRAGONSTATE_ABSENT) {
      print("You have nothing with which to douse a dragon.");
    }
    if (vDRAGON_STATE == DRAGONSTATE_ABSENT) {
      print("There is no dragon here.");
    }
  }
}
if (posn(oEGO, 105, 96, 145, 166) && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
#if VERSION_MAJOR >= 2
  if (fCARRYING_DAGGER && (said("throw", "dagger") || said("throw", "dagger", "dragon"))) {
#else
  if (fCARRYING_DAGGER && said("throw", "dagger")) {
#endif
    reset(fCARRYING_DAGGER);
    print("The dagger sings through the air, but falls short of the dragon.  You should have been closer.");
    drop("dagger");
    vSCORE -= 5;
  }
  if (fBUCKET_FILLED && (
    said("throw", "water")
    || said("throw", "water", "dragon")
    || said("douse", "dragon")
  )) {
    reset(fBUCKET_FILLED);
    print("You throw your water, but to no avail.  It lands on the floor and is quickly absorbed.");
    drop("water");
    vSCORE -= 2;
    reset(fFILL_BUCKET_POINTS);
  }
}
if (!posn(oEGO, 105, 96, 145, 166) && vDRAGON_STATE == DRAGONSTATE_NORMAL) {
  if (fBUCKET_FILLED && (
    said("throw", "water")
    || said("throw", "water", "dragon")
    || said("douse", "dragon")
  )) {
    if (BUILD_TARGET == APPLE_IIGS) {
      sound(SND_GIFT, fMISC_END_MARKER);
    }
    reset(fBUCKET_FILLED);
    print("The water hits the dragon squarely in the mouth, extinguishing its roaring flames.  No fire!  How mortified and embarrassed the dragon is!  Moving a huge boulder in leaving, the dragon creeps off to sulk.");
    drop("water");
    vSCORE -= 2;
    vSCORE += 7;
    if (VERSION_MAJOR >= 2) {
      set.loop(oDRAGON, 1);
    }
    reset(fFILL_BUCKET_POINTS);
    move.obj(oDRAGON, 40, 137, 0, rfDRAGON_AT_BOULDER);
    if (BUILD_TARGET == APPLE_IIGS) {
      ignore.objs(oDRAGON);
    }
    vDRAGON_STATE = DRAGONSTATE_ABSENT;
  }
#if VERSION_MAJOR >= 2
  if (fCARRYING_DAGGER && (said("throw", "dagger") || said("throw", "dagger", "dragon"))) {
#else
  if (fCARRYING_DAGGER && said("throw", "dagger")) {
#endif
    if (BUILD_TARGET == APPLE_IIGS) {
      stop.sound();
      sound(SND_GIFT, fMISC_END_MARKER);
    }
    ignore.blocks(oDRAGON);
    reset(fCARRYING_DAGGER);
    print("Nervously, you throw the dagger at the dragon and strike it in the heart.  Death for the dragon is instant.");
    drop("dagger");
    vSCORE -= 5;
    vSCORE += 3;
    erase(oDRAGON);
    set.view(oDRAGON, 61);
    rvDRAGON_STEP_SIZE = 0;
    step.size(oDRAGON, rvDRAGON_STEP_SIZE);
    draw(oDRAGON);
    stop.motion(oDRAGON);
    stop.cycling(oDRAGON);
    vDRAGON_STATE = DRAGONSTATE_DEAD;
  }
}
if (rfDRAGON_AT_BOULDER) {
  reset(rfDRAGON_AT_BOULDER);
  start.cycling(oBOULDER);
  if (BUILD_TARGET == APPLE_IIGS) {
    sound(SND_MOVING_ROCK, fMISC_END_MARKER);
  }
  end.of.loop(oBOULDER, rfBOULDER_MOVED);
}
if (rfBOULDER_MOVED) {
  ignore.blocks(oDRAGON);
  reset(rfBOULDER_MOVED);
  stop.cycling(oBOULDER);
  move.obj(oDRAGON, 4, 137, 0, rfDRAGON_LEFT);
}
if (rfDRAGON_LEFT) {
  reset(rfDRAGON_LEFT);
  erase(oDRAGON);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM52_IN_WELL_UNDERWATER);
}
if (posn(oEGO, BX_PAST_BOULDER)) {
  new.room(ROOM50_CAVE_ADJOINING_DRAGONS_LAIR);
}
return;

#message 1 "The ferocious fire-breathing dragon is protecting the magic mirror."
#message 2 "There is a dead dragon, a large boulder, and a seemingly ordinary mirror in the cave."
#message 3 "There is a large granite boulder and a magic mirror!! in the cave."
#message 4 "A green, scaly dragon breathing hot fire and smoke is protecting the mirror."
#message 5 "In this large cavern, there is a slimy dead dragon, an opening on one side, and a large boulder in the other."
#message 6 "The cavern is empty, save for a couple of openings on either side."
#message 7 "There is no dragon here."
#message 8 "This ferocious, fire-breathing dragon with sharp, green scales and a long tail is protecting the magic mirror. Watch out for the flames!"
#message 9 "You see the once powerful dragon lying lifeless before you."
#message 10 "It took millenniums for these stalagmites and stalactites to form."
#message 11 "This boulder is a huge chunk of granite."
#message 12 "This boulder is far too heavy to move."
#message 13 "A strange watery light issues from the hole."
#message 14 "The mere thought of retrieving the dagger repulses you."
#message 15 "The hole seems to lead further into the cave."
#message 16 "How?"
#message 17 "This dragon had only one life and you have taken it."
#message 18 "With that dragon here?  No way!"
#message 19 "This dragon does not respond to reason."
#message 20 "You throw your bucket on the cavern floor."
#message 21 "The dragon is protecting the magic mirror."
#message 22 "The magic mirror is framed with ornate mahogany wood, polished to a high sheen."
#message 23 "There is no mirror here."
#message 24 "Carefully, you pick up the magic mirror."
#message 25 "I do not think that you can get close enough to the magic mirror with the dragon here."
#message 26 "You could get a much better view if you were closer."
#message 27 "You are not close enough to reach it."
#message 28 "You don't have a dagger."
#message 29 "You don't have any water."
#message 30 "You have nothing with which to douse a dragon."
#message 31 "You can't do that from here.!"
#message 32 "The dagger sings through the air, but falls short of the dragon.  You should have been closer."
#message 33 "You throw your water, but to no avail.  It lands on the floor and is quickly absorbed."
#message 34 "The water hits the dragon squarely in the mouth, extinguishing its roaring flames.  No fire!  How mortified and embarrassed the dragon is!  Moving a huge boulder in leaving, the dragon creeps off to sulk."
#message 35 "Nervously, you throw the dagger at the dragon and strike it in the heart.  Death for the dragon is instant."
#message 36 "How do you wish to accomplish that?"
#message 37 "You are too busy looking around to even think about that."
#message 38 "You should know about these fire-breathing dragons!  Maybe next time you will be a little more careful!"
#message 39 "Your shield melted from the intensity of the dragon's flames!  You should know about these fire-breathing dragons!  Maybe next time you will be a little more careful!"
#message 40 "I think you found the dragon!"
