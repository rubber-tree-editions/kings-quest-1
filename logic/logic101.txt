#include "vars.txt"
#include "swimming-defs.txt"

if (fROOM_SCRIPT_EXECUTED) {
  load.view(93);
  if (BUILD_TARGET == APPLE_IIGS) {
    if (vROOM_NO > 2) {
      load.view(70);
    }
    if (vROOM_NO != ROOM49_INSIDE_WELL && vROOM_NO > 2) {
      load.sound(SND_SPLASHING);
    }
    if (vROOM_NO < 3) {
      load.sound(SND_MOAT);
    }
  }
  else {
    load.view(70);
    load.sound(SND_SPLASHING);
  }
  set(fSWIMMING_LOGIC_LOADED);
  reset(fSWIMMING);
  if (BUILD_TARGET == APPLE_IIGS) {
    reset(fPLAYING_SND_SPLASHING);
  }
}
if (BUILD_TARGET == APPLE_IIGS && fEATEN_BY_GATORS) {
  load.sound(SND_MOAT);
}
if (f174_UNKNOWN || fGAME_OVER) {
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fEGO_PREOCCUPIED);
  }
  goto RETURN;
}
if (fSWIMMING_IMPOSSIBLE_HERE && vAQUATIC_ACTIVITY == AQUATICACTIVITY_DROWNING) {
  set(rfDROWNING);
  if (said("swim") || said("go", "swim") || said("swim", "anyword") || controller(cSWIM)) {
    if (vROOM_NO == ROOM1_CASTLE_EAST || vROOM_NO == ROOM2_CASTLE_WEST) {
      #if BUILD_TARGET == APPLE_IIGS
        print("Unfortunately, with the moat's population of alligators, swimming is not possible.");
      #else
        print("Unfortunately, with the moat population swimming is not possible.");
      #endif
    }
    else {
      print("The swift current does not allow for swimming.");
    }
  }
}
if (vAQUATIC_ACTIVITY == AQUATICACTIVITY_DROWNING) {
  if (!fPLAYING_SND_SPLASHING) {
    set(fPLAYING_SND_SPLASHING);
    if (BUILD_TARGET == APPLE_IIGS) {
      if (vROOM_NO != ROOM49_INSIDE_WELL) {
        if (!fEATEN_BY_GATORS && vROOM_NO > 2) {
          sound(SND_SPLASHING, fPLAYED_SND_SPLASHING);
        }
        else {
          sound(SND_MOAT, fPLAYED_SND_SPLASHING);
        }
      }
    }
    else {
      sound(SND_SPLASHING, fPLAYED_SND_SPLASHING);
    }
  }
  if (fPLAYED_SND_SPLASHING) {
    reset(fPLAYED_SND_SPLASHING);
    reset(fPLAYING_SND_SPLASHING);
  }
}
if (fEGO_ON_WATER) {
  if (vAQUATIC_ACTIVITY == AQUATICACTIVITY_NONE) {
    set(fCYCLE_EGO_ANIMATION);
    stop.motion(oEGO);
    vDROWN_MESSAGE_COUNTDOWN = 250;
    #if RUBBER_TREE
      if (fEGO_INVISIBLE) {
        vDROWN_INVISCD_HI = vINVISIBLE_COUNTDOWN_HI;
        vDROWN_INVISCD_LO = vINVISIBLE_COUNTDOWN_LO;
      }
      else if (fWEARING_RING) {
        vDROWN_INVISCD_HI = 102;
      }
      else if (fCARRYING_RING) {
        vDROWN_INVISCD_HI = 103;
      }
      else {
        vDROWN_INVISCD_HI = 101;
      }
      if (fPROTECTED_BY_SPELL) {
        vDROWN_PROTECTCD_HI = vSPELL_COUNTDOWN_HI;
        vDROWN_PROTECTCD_LO = vSPELL_COUNTDOWN_LO;
      }
      else {
        vDROWN_PROTECTCD_HI = 101;
      }
      if (fGOAT_COMPANION) {
        get.posn(o13, vDROWN_GOAT_X, vDROWN_GOAT_Y);
        get.dir(o13, vDROWN_GOAT_DIR);
      }
    #endif
    vAQUATIC_ACTIVITY = AQUATICACTIVITY_DROWNING;
    if (fMANUAL_EGO_WATER_POS) {
      reposition(oEGO, vEGO_WATER_X, vEGO_WATER_Y);
    }
    else {
      object.on.water(oEGO);
    }
    get.posn(oEGO, vEGO_WATER_X, vEGO_WATER_Y);
    if (vEGO_WATER_X > 139) {
      erase(oEGO);
      set.view(oEGO, 93);
      draw(oEGO);
      if (fEGO_TINY) {
        reset(fEGO_TINY);
        vTINY_COUNTDOWN = 0;
        print("It appears you have become normal size again.");
      }
      if (fEGO_INVISIBLE) {
        vINVISIBLE_COUNTDOWN_LO = 0;
        vINVISIBLE_COUNTDOWN_HI = 0;
        reset(fEGO_INVISIBLE);
        reset(fCARRYING_RING);
        reset(fWEARING_RING);
        set(fLOST_RING);
        drop(iRING);
        vSCORE -= 3;
        print("Thrashing around in the water has caused you to lose the magic ring and become visible again.");
      }
    }
    else {
      set.view(oEGO, 93);
      if (fEGO_TINY) {
        reset(fEGO_TINY);
        vTINY_COUNTDOWN = 0;
        print("It appears you have become normal size again.");
      }
      if (fEGO_INVISIBLE) {
        vINVISIBLE_COUNTDOWN_LO = 0;
        vINVISIBLE_COUNTDOWN_HI = 0;
        reset(fEGO_INVISIBLE);
        reset(fCARRYING_RING);
        reset(fWEARING_RING);
        set(fLOST_RING);
        drop(iRING);
        vSCORE -= 3;
        print("Thrashing around in the water has caused you to lose the magic ring and become visible again.");
      }
    }
    if (fSWIMMING_IMPOSSIBLE_HERE) {
      vDROWN_GAME_OVER_COUNTDOWN = 50;
    }
  }
  else if (!fSWIMMING_IMPOSSIBLE_HERE && vAQUATIC_ACTIVITY != AQUATICACTIVITY_SWIMMING && !rfDROWNING) {
    if (said("swim") || said("go", "swim") || said("swim", "anyword") || controller(cSWIM)) {
      set.view(oEGO, 70);
      start.motion(oEGO);
      stop.sound();
      vAQUATIC_ACTIVITY = AQUATICACTIVITY_SWIMMING;
      goto RETURN;
    }
  }
}
if (!fSWIMMING && !rfDROWNING) {
  if (said("swim") || said("go", "swim") || said("swim", "anyword") || controller(cSWIM)) {
    set(fSWIMMING);
    if (!fEGO_ON_WATER) {
      print("You need to be in the water in order to swim.");
    }
    else if (fEGO_ON_WATER && vAQUATIC_ACTIVITY == AQUATICACTIVITY_SWIMMING) {
      print("You are already swimming!");
    }
  }
}
else if (!controller(cSWIM)) {
  reset(fSWIMMING);
}
if (vAQUATIC_ACTIVITY == AQUATICACTIVITY_DROWNING && vDROWN_GAME_OVER_COUNTDOWN <= 0) {
  if (vDROWN_MESSAGE_COUNTDOWN == 0) {
    set(rfDROWNING);
    print("You splash around in the water for a while but seem  to have forgotten how to swim. Unfortunately, you drown.");
    vDROWN_GAME_OVER_COUNTDOWN = 50;
  }
  else {
    vDROWN_MESSAGE_COUNTDOWN--;
  }
}
if (!fEGO_ON_WATER && vAQUATIC_ACTIVITY == AQUATICACTIVITY_SWIMMING) {
  reset(fCYCLE_EGO_ANIMATION);
  vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
  set.view(oEGO, 0);
  if (f136_UNKNOWN) {
    reposition(oEGO, vEGO_WATER_X, vEGO_WATER_Y);
  }
  else {
    object.on.land(oEGO);
  }
}
reset(fMANUAL_EGO_WATER_POS);
reset(f136_UNKNOWN);
vEGO_WATER_X = 0;
vEGO_WATER_Y = 0;
vDROWN_GAME_OVER_COUNTDOWN--;
if (vDROWN_GAME_OVER_COUNTDOWN == 1) {
  erase(oEGO);
  stop.sound();
  set(fGAME_OVER);
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fEGO_PREOCCUPIED);
    if (vROOM_NO == ROOM2_CASTLE_WEST && !fEATEN_BY_GATORS) {
      print("You splash around in the water for a while but seem  to have forgotten how to swim. Unfortunately, you drown.");
    }
  }
}
RETURN:
  return;

#message 1 "You splash around in the water for a while but seem  to have forgotten how to swim. Unfortunately, you drown."
#if BUILD_TARGET == APPLE_IIGS
  #message 2 "Unfortunately, with the moat's population of alligators, swimming is not possible."
#else
  #message 2 "Unfortunately, with the moat population swimming is not possible."
#endif
#message 3 "You need to be in the water in order to swim."
#message 4 "You are already swimming!"
#message 5 "The swift current does not allow for swimming."
#message 6 "Thrashing around in the water has caused you to lose the magic ring and become visible again."
#message 7 "It appears you have become normal size again."
