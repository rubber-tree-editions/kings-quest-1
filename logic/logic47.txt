#include "vars.txt"
#include "swimming-defs.txt"

#define PT_DEAD_GOAT 14, 144

#define rfSHOW_MUSHROOM rf0
#define rfPULLED_UNDERWATER rf1
#define rfPLAYED_SND_RUSHING_WATER rf2

#define oMUSHROOM o1

#if RUBBER_TREE
if (fUNDO) {
  if (fEGO_ON_WATER || rfPULLED_UNDERWATER || fGAME_OVER) {
    stop.sound();
    erase(oEGO);
    reset(fUNDO);
    reset(fEGO_PREOCCUPIED);
    reset(rfPULLED_UNDERWATER);
    reset(rfDROWNING);
    object.on.anything(oEGO);
    vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
    if (vRESTORE_DIR == DIR_NORTH || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_NORTHWEST) {
      vRESTORE_Y += 3;
    }
    else if (vRESTORE_DIR == DIR_SOUTH || vRESTORE_DIR == DIR_SOUTHEAST || vRESTORE_DIR == DIR_SOUTHWEST) {
      vRESTORE_Y -= 3;
    }
    if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
      vRESTORE_X -= 3;
    }
    else if (vRESTORE_DIR == DIR_WEST || vRESTORE_DIR == DIR_NORTHWEST || vRESTORE_DIR == DIR_SOUTHWEST) {
      vRESTORE_X += 3;
    }
    reposition.to.v(oEGO, vRESTORE_X, vRESTORE_Y);
    object.on.land(oEGO);
    reset(fEGO_ON_WATER);
    reset(fCYCLE_EGO_ANIMATION);
    set.view(oEGO, 0);
    if (vRESTORE_DIR == DIR_SOUTH) {
      set.loop(oEGO, 2);
    }
    else if (vRESTORE_DIR == DIR_NORTH) {
      set.loop(oEGO, 3);
    }
    else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
      set.loop(oEGO, 0);
    }
    else {
      set.loop(oEGO, 1);
    }
    vDROWN_GAME_OVER_COUNTDOWN = 0;
    draw(oEGO);
    player.control();

    if (fGAME_OVER) {
      reset(fGAME_OVER);
      reset(fGAME_OVER_PREPARATION);
      //return;
    }
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_RUSHING_WATER);
    load.sound(SND_POINTS);
  }
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
    if (BUILD_TARGET == APPLE_IIGS) {
      get.posn(oEGO, vTEMP_X, vTEMP_Y);
      if (vTEMP_Y < 100) {
        vTEMP_Y = 101;
        position.v(oEGO, vTEMP_X, vTEMP_Y);
      }
    }
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  load.logics(LOGIC101_SWIMMING);
  call(LOGIC101_SWIMMING);
  if (!fMUSHROOM_TAKEN) {
    load.view(60);
    load.view(84);
    animate.obj(oMUSHROOM);
    set.view(oMUSHROOM, 84);
    position(oMUSHROOM, 84, 80);
    set(rfSHOW_MUSHROOM);
  }
  if (vPREV_ROOM_NO == ROOM34_PEBBLY_RIVER_BANK) {
    if (posn(oEGO, 77, 165, 112, 167)) {
      position(oEGO, 71, 164);
    }
    if (posn(oEGO, 113, 165, 159, 167)) {
      position(oEGO, 142, 164);
    }
  }
  if (rfSHOW_MUSHROOM) {
    reset(rfSHOW_MUSHROOM);
    draw(oMUSHROOM);
  }
  set(fSWIMMING_IMPOSSIBLE_HERE);
  ignore.blocks(oEGO);
  draw(oEGO);
  show.pic();
  if (BUILD_TARGET == APPLE_IIGS) {
    set(rfPLAYED_SND_RUSHING_WATER);
  }
}
if (BUILD_TARGET == APPLE_IIGS && !rfPULLED_UNDERWATER && rfPLAYED_SND_RUSHING_WATER && !fEGO_PLAYING_FIDDLE) {
  sound(SND_RUSHING_WATER, rfPLAYED_SND_RUSHING_WATER);
}
if (fEGO_ON_WATER && !rfPULLED_UNDERWATER) {
  set(rfPULLED_UNDERWATER);
  print("This is sad.  You are not the first to fail your quest in the Raging River... I hope you may be the last.");
}
if (said("examine", "water")) {
  print("This is the wild Raging River filled with boulders and rapids.");
}
if (said("examine", "rocks")) {
  print("It would be dangerous to try and jump across the river on the boulders.");
}
if (said("examine", "across")) {
  if (posn(oEGO, 0, 0, 36, 111) || posn(oEGO, 0, 112, 97, 167)) {
    print("The land across the river looks enchanting.  You wonder how you can get there.");
  }
  else {
    print("You see the familiar side of the Raging River.");
  }
}
if (!fCARRYING_MUSHROOM && fMUSHROOM_TAKEN) {
  if (said("examine", "mushroom")) {
    print("There is no mushroom here.");
  }
}
if (fMUSHROOM_TAKEN) {
  if (said("examine", "ground")) {
    print("There is nothing of interest around you.");
  }
  if (said("get", "mushroom")) {
    if (fCARRYING_MUSHROOM) {
      print("You already have it.");
    }
    else {
      print("There is no mushroom here.");
    }
  }
}
if (!fMUSHROOM_TAKEN) {
  if (said("examine", "mushroom")) {
    if (posn(oEGO, 0, 0, 36, 111) || posn(oEGO, 0, 112, 97, 167)) {
      print("The mushroom is on the far side of the river.");
    }
    else if (posn(oEGO, 64, 61, 114, 101)) {
      print("This mushroom is large and aromatic.");
    }
    else {
      print("The colorful mushroom is in the distance.");
    }
  }
  if (said("examine", "ground")) {
    if (posn(oEGO, 0, 0, 36, 111) || posn(oEGO, 0, 112, 97, 167)) {
      print("There is nothing of interest around you.");
    }
    else if (posn(oEGO, 64, 61, 114, 101)) {
      print("There is a large mushroom growing in the ground close by.");
    }
    else {
      print("There is nothing of interest around you.");
    }
  }
  if (said("get", "mushroom")) {
    if (posn(oEGO, 0, 0, 36, 111) || posn(oEGO, 0, 112, 97, 167)) {
      print("The mushroom is on the far side of the river.");
    }
    else if (posn(oEGO, 74, 70, 94, 90)) {
      erase(oMUSHROOM);
      if (BUILD_TARGET == APPLE_IIGS) {
        sound(SND_POINTS, fMISC_END_MARKER);
      }
      print("You reach down and pick a fat mushroom.");
      set(fMUSHROOM_TAKEN);
      set(fCARRYING_MUSHROOM);
      vSCORE += 1;
      get("mushroom");
    }
    else {
      print("You need to get closer to do that.");
    }
  }
}
if (said("examine", "building")) {
  print("You see King Edward's castle on the horizon.");
}
if (said("examine", "room")) {
  if (fMUSHROOM_TAKEN) {
    print("This lovely meadow is split by the Raging River.");
  }
  else {
    print("There is a large mushroom in the meadow next to the river.");
  }
}
if (vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE && fEGO_ON_WATER) {
  vEGO_WATER_X = 255;
  if (posn(oEGO, 7, 40, 62, 81)) {
    vEGO_WATER_X = 23;
    vEGO_WATER_Y = 81;
  }
  if (posn(oEGO, 10, 82, 37, 116)) {
    vEGO_WATER_X = 28;
    vEGO_WATER_Y = 95;
  }
  if (posn(oEGO, 37, 82, 74, 90)) {
    vEGO_WATER_X = 60;
    vEGO_WATER_Y = 104;
  }
  if (posn(oEGO, 64, 91, 104, 116)) {
    vEGO_WATER_X = 74;
    vEGO_WATER_Y = 116;
  }
  if (posn(oEGO, 37, 117, 100, 145)) {
    vEGO_WATER_X = 70;
    vEGO_WATER_Y = 117;
  }
  if (posn(oEGO, 68, 146, 111, 167) || posn(oEGO, 101, 117, 143, 167)) {
    vEGO_WATER_X = 102;
    vEGO_WATER_Y = 155;
  }
  if (vEGO_WATER_X == 255) {
    vEGO_WATER_X = 0;
  }
  else {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vEGO_WATER_X -= vTEMP_X;
    vEGO_WATER_Y -= vTEMP_Y;
    set(fMANUAL_EGO_WATER_POS);
  }
}
#if RUBBER_TREE
if (posn(oEGO, 86, 166, 125, 168)) {
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  vTEMP_Y = 165;
  reposition.to.v(oEGO, vTEMP_X, vTEMP_Y);
}
#endif
if (!fBUCKET_FILLED && !fEGO_ON_WATER && !fDRANK_WATER) {
  if (said("drink", "water") || said("get", "drink")) {
    if (posn(oEGO, 0, 40, 32, 111)
      || posn(oEGO, 14, 112, 88, 135)
      || posn(oEGO, 39, 136, 99, 167)
      || posn(oEGO, 36, 40, 74, 76)
      || posn(oEGO, 46, 77, 101, 88)
      || posn(oEGO, 68, 89, 151, 167)
    ) {
      print("The water from this swift river is cold and sweet.");
    }
    else {
      print("You need to get closer to do that.");
    }
  }
}
if (!fBUCKET_FILLED && fCARRYING_BUCKET && !fEGO_ON_WATER && said("get", "water")) {
  if (posn(oEGO, 0, 40, 32, 111)
    || posn(oEGO, 14, 112, 88, 135)
    || posn(oEGO, 39, 136, 99, 167)
    || posn(oEGO, 36, 40, 74, 76)
    || posn(oEGO, 46, 77, 101, 88)
    || posn(oEGO, 68, 89, 151, 167)
  ) {
    reset(fDRANK_WATER);
    print("The river is so swift it is a battle to fill the bucket.");
    get("water");
    set(fBUCKET_FILLED);
    if (!fFILL_BUCKET_POINTS) {
      vSCORE += 2;
    }
    set(fFILL_BUCKET_POINTS);
  }
  else {
    print("You need to get closer to do that.");
  }
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM48_LEPRECHAUN_HOLE);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM46_OGRE_STOMPING_GROUND);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM34_PEBBLY_RIVER_BANK);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
#if RUBBER_TREE
if (!fEGO_ON_WATER) {
  get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
  vRESTORE_DIR = vEGO_DIR;
}
#endif
call(LOGIC101_SWIMMING);
return;

#message 1 "This is sad.  You are not the first to fail your quest in the Raging River... I hope you may be the last."
#message 2 "This is the wild Raging River filled with boulders and rapids."
#message 3 "It would be dangerous to try and jump across the river on the boulders."
#message 5 "The land across the river looks enchanting.  You wonder how you can get there."
#message 6 "The mushroom is on the far side of the river."
#message 7 "This mushroom is large and aromatic."
#message 8 "There is a large mushroom growing in the ground close by."
#message 9 "You reach down and pick a fat mushroom."
#message 10 "You see the familiar side of the Raging River."
#message 11 "The colorful mushroom is in the distance."
#message 12 "You need to get closer to do that."
#message 13 "The river is so swift it is a battle to fill the bucket."
#message 14 "The water from this swift river is cold and sweet."
#message 15 "You see King Edward's castle on the horizon."
#message 16 "There is a large mushroom in the meadow next to the river."
#message 17 "There is nothing of interest around you."
#message 18 "This lovely meadow is split by the Raging River."
#message 19 "There is no mushroom here."
#message 20 "You already have it."
