#include "vars.txt"

#define PRI_GOLDEN_EGG 6

#define rfSHOW_GOLDEN_EGG rf0
#define rfFALLING_OUT_OF_THE_TREE rf1

#define oGOLDEN_EGG o1

#if RUBBER_TREE
if (fUNDO && rfFALLING_OUT_OF_THE_TREE) {
  erase(oEGO);
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  set.priority(oEGO, 6);
  reset(fUNDO);
  reset(rfFALLING_OUT_OF_THE_TREE);
  reset(fFALLING_OUT_OF_TREE);
  reset(fCYCLE_EGO_ANIMATION);
  observe.blocks(oEGO);
  stop.motion(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  set.view(oEGO, 0);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  draw(oEGO);
  player.control();
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
  if (fREPLENISH_MINOR_TREASURE) {
    reset(fTAKEN_GOLDEN_EGG);
    reset(fDWARF_OR_TROLL_HAS_EGG);
    reset(fREPLENISH_MINOR_TREASURE);
  }
  #endif
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_POINTS);
  }
  load.sound(SND_FALLING);
  load.view(105);
  if (!fPOINTS_FOR_CLIMBING_TREE) {
    set(fPOINTS_FOR_CLIMBING_TREE);
    vSCORE += 2;
  }
  if (!fTAKEN_GOLDEN_EGG) {
    set(rfSHOW_GOLDEN_EGG);
    load.view(39);
    animate.obj(oGOLDEN_EGG);
    set.view(oGOLDEN_EGG, 39);
    position(oGOLDEN_EGG, 70, 85);
    set.priority(oGOLDEN_EGG, PRI_GOLDEN_EGG);
  }
  set.priority(oEGO, 6);
  #if RUBBER_TREE
    if (fPLAYER_NOT_IN_CONTROL) {
      position.v(oEGO, vRESTORE_X, vRESTORE_Y);
      reset(fPLAYER_NOT_IN_CONTROL);
      reset(fFALLING_OUT_OF_TREE);
      reset(fCYCLE_EGO_ANIMATION);
      vEGO_DIR = 0;
      set.view(oEGO, 0);
      if (vRESTORE_DIR == DIR_SOUTH) {
        set.loop(oEGO, 2);
      }
      else if (vRESTORE_DIR == DIR_NORTH) {
        set.loop(oEGO, 3);
      }
      else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
        set.loop(oEGO, 0);
      }
      else {
        set.loop(oEGO, 1);
      }
    }
    else {
      position(oEGO, 35, 164);
      vRESTORE_X = 35;
      vRESTORE_Y = 164;
      vRESTORE_DIR = vEGO_DIR;
      if (VERSION_MAJOR >= 2) {
        vEGO_DIR = 0;
      }
    }
    observe.blocks(oEGO);
  #else
    position(oEGO, 35, 164);
    observe.blocks(oEGO);
    if (VERSION_MAJOR >= 2) {
      vEGO_DIR = 0;
    }
  #endif
  if (rfSHOW_GOLDEN_EGG) {
    draw(oGOLDEN_EGG);
    stop.update(oGOLDEN_EGG);
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "tree")) {
  print("There is a rather large bird's nest here.");
}
if (fTAKEN_GOLDEN_EGG) {
  if (said("examine", "birds nest") || said("look inside", "birds nest")) {
    print("The nest is empty.");
  }
  if (fCARRYING_GOLDEN_EGG && said("get", "golden egg")) {
    print("You already have the golden egg from the nest.");
  }
  if (!fCARRYING_GOLDEN_EGG && said("get", "golden egg")) {
    print("There is no egg here to get.");
  }
}
distance(oEGO, oGOLDEN_EGG, vTEMP_RESULT);
if (!fTAKEN_GOLDEN_EGG) {
  if (said("examine", "birds nest") || said("look inside", "birds nest")) {
    print("There is a pure golden egg lying in the nest!");
  }
  if (said("examine", "golden egg")) {
    print("The golden egg is smooth and shiny.");
  }
  if (said("get", "golden egg")) {
    if (vTEMP_RESULT < 24) {
      start.update(oGOLDEN_EGG);
      erase(oGOLDEN_EGG);
      set(fCARRYING_GOLDEN_EGG);
      get("gold egg");
      set(fTAKEN_GOLDEN_EGG);
      vSCORE += 6;
      if (BUILD_TARGET == APPLE_IIGS) {
        sound(SND_POINTS, fMISC_END_MARKER);
      }
      print("The golden egg is so heavy you almost drop it.");
    }
    else {
      print("You can't get the egg from here!");
    }
  }
}
if (fEGO_ON_WATER && !posn(oEGO, 12, 106, 57, 166) && !rfFALLING_OUT_OF_THE_TREE) {
  set(rfFALLING_OUT_OF_THE_TREE);
  set(fFALLING_OUT_OF_TREE);
  ignore.blocks(oEGO);
  set.view(oEGO, 105);
  print("Apparently you lost your grip while climbing this tree and you fall to the hard earth below.");
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  vTEMP_Y = 166;
  vTEMP_STEP = 3;
  sound(SND_FALLING, fSOUND_END_MARKER);
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, fMISC_END_MARKER);
}
if (fEGO_TOUCHED_TRIGGER_LINE && !posn(oEGO, 12, 106, 57, 166) && !rfFALLING_OUT_OF_THE_TREE) {
  set(rfFALLING_OUT_OF_THE_TREE);
  set(fFALLING_OUT_OF_TREE);
  ignore.blocks(oEGO);
  set.priority(oEGO, 4);
  set.view(oEGO, 105);
  print("Apparently you lost your grip while climbing this tree and you fall to the hard earth below.");
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  vTEMP_Y = 166;
  vTEMP_STEP = 3;
  sound(SND_FALLING, fSOUND_END_MARKER);
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, fMISC_END_MARKER);
}
if (posn(oEGO, 12, 106, 57, 166) && !rfFALLING_OUT_OF_THE_TREE) {
  if (fEGO_TOUCHED_TRIGGER_LINE || fEGO_ON_WATER) {
    set(rfFALLING_OUT_OF_THE_TREE);
    set(fFALLING_OUT_OF_TREE);
    ignore.blocks(oEGO);
    set.priority(oEGO, 5);
    set.view(oEGO, 105);
    print("Apparently you lost your grip while climbing this tree and you fall to the hard earth below.");
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vTEMP_Y = 166;
    vTEMP_STEP = 3;
    sound(SND_FALLING, fSOUND_END_MARKER);
    move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, fMISC_END_MARKER);
  }
}
#if RUBBER_TREE
if (!rfFALLING_OUT_OF_THE_TREE) {
  get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
  vRESTORE_DIR = vEGO_DIR;
}
#endif
#if BUILD_TARGET == APPLE_IIGS
if (fSOUND_END_MARKER) {
#else
if (fMISC_END_MARKER) {
#endif
  new.room(ROOM14_BASE_GOLDEN_EGG_TREE);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM14_BASE_GOLDEN_EGG_TREE);
}
return;

#message 1 "There is a rather large bird's nest here."
#message 2 "There is a pure golden egg lying in the nest!"
#message 3 "The nest is empty."
#message 4 "The golden egg is smooth and shiny."
#message 5 "You already have the golden egg from the nest."
#message 6 "There is no egg here to get."
#message 7 "The golden egg is so heavy you almost drop it."
#message 8 "You can't get the egg from here!"
#message 9 "Apparently you lost your grip while climbing this tree and you fall to the hard earth below."
