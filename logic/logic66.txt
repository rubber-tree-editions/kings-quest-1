#include "vars.txt"

#define rfFALLEN_OFF_STEPS rf0
#define rfFALLING_OFF_STEPS rf1

#define rvTEMP_PRIORITY rv0

#if RUBBER_TREE
  #define rvRESTORE_LOOP rv1
#endif

#if BUILD_TARGET == APPLE_IIGS
  #define _fFALLING fFALLING
#else
  #define _fFALLING rfFALLING_OFF_STEPS
#endif

#if RUBBER_TREE
if (fUNDO && (_fFALLING || fGAME_OVER)) {
  set(fRESTORE_STATE);
  reset(fUNDO);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  vEGO_DIR = DIR_NONE;
  set.view(oEGO, 0);
  reset(fCYCLE_EGO_ANIMATION);
  reset(_fFALLING);
  release.priority(oEGO);
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  set.loop.v(oEGO, rvRESTORE_LOOP);
  draw(oEGO);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif
if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.sound(SND_FALLING);
  set.horizon(45);
  if (vPREV_ROOM_NO == ROOM19_BASE_MOUNTAIN_DOOR) {
    position(oEGO, 21, 162);
  }
  if (vPREV_ROOM_NO == ROOM67_MOUNTAIN_STAIRWAY_NEAR_BOTTOM) {
    position(oEGO, 132, 46);
    set.loop(oEGO, 1);
    if (VERSION_MAJOR >= 2) {
      move.obj(oEGO, 131, 47, 0, fMISC_END_MARKER);
    }
    else {
      move.obj(oEGO, 132, 46, 0, fMISC_END_MARKER);
    }
  }
  draw(oEGO);
  show.pic();
}
if (fGOAT_COMPANION) {
  reset(fGOAT_COMPANION);
  set(fGOAT_IS_GONE);
  print("The old goat is afraid of the dark and decides to stay outside.");
  vSCORE -= 5;
  #if RUBBER_TREE
  reset(fPOINTS_FOR_GOAT_COMPANION);
  #endif
}
if (said("examine", "stairway") || said("examine", "room")) {
  print("The stairs go up into the heart of the mountain.");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !_fFALLING) {
  #if RUBBER_TREE
    set(fSTORE_STATE);
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    if (vRESTORE_X < 58) {
      vRESTORE_Y = 128;
    }
    else {
      vRESTORE_Y -= 8;
    }
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fEGO_PREOCCUPIED);
    prevent.input();
    start.motion(oEGO);
  }
  if (VERSION_MAJOR >= 2) {
    set(fTENSE_SITUATION);
    set(fTHREAT_OF_SORCERER_OR_OGRE);
  }
  set(_fFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvTEMP_PRIORITY);
  set.priority.v(oEGO, rvTEMP_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFALLEN_OFF_STEPS);
  sound(SND_FALLING, fSOUND_END_MARKER);
}
if (rfFALLEN_OFF_STEPS) {
  reset(rfFALLEN_OFF_STEPS);
  print("Oops!  Those stairs are pretty slippery, aren't they?");
  erase(oEGO);
  set(fGAME_OVER);
  if (BUILD_TARGET == APPLE_IIGS) {
    accept.input();
  }
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM67_MOUNTAIN_STAIRWAY_NEAR_BOTTOM);
}
if (posn(oEGO, 16, 165, 31, 166)) {
  new.room(ROOM19_BASE_MOUNTAIN_DOOR);
}
if (BUILD_TARGET == APPLE_IIGS) {
  set(fFALL_DANGER);
}
#if RUBBER_TREE
if (vEGO_DIR == DIR_EAST || vEGO_DIR == DIR_NORTHEAST || vEGO_DIR == DIR_SOUTHEAST) {
  rvRESTORE_LOOP = 0;
}
else if (vEGO_DIR == DIR_WEST || vEGO_DIR == DIR_NORTHWEST || vEGO_DIR == DIR_SOUTHWEST) {
  rvRESTORE_LOOP = 1;
}
#endif
return;

#message 1 "The old goat is afraid of the dark and decides to stay outside."
#message 2 "The stairs go up into the heart of the mountain."
#message 3 "Oops!  Those stairs are pretty slippery, aren't they?"
