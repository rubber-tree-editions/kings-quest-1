#include "vars.txt"

#define BX_NEAR_HOLE 1, 75, 65, 166
#define BX_HOLE_EXIT_ZONE 31, 111, 32, 130

#define rvCOUNTDOWN_TO_SND_IN_WELL rv0
#define rvGAME_OVER_COUNTDOWN rv1

#if VERSION_MAJOR >= 2
  #define rfDEAD_AND_SUNK rf1
  #define rfDEAD rf2
#else
  #define rfDEAD_AND_SUNK rf0
  #define rfDEAD rf1
#endif

#if RUBBER_TREE
if (fUNDO) {
  if (rfDEAD || rfDEAD_AND_SUNK || fGAME_OVER) {
    reset(fUNDO);
    erase(oEGO);
    stop.sound();
    reset(rfDEAD);
    observe.blocks(oEGO);
    reset(rfDEAD_AND_SUNK);
    rvGAME_OVER_COUNTDOWN = 0;
    set.view(oEGO, 73);
    load.view(114);
    set(fEGO_ON_WATER);
    vCOUNTDOWN_LO = 250;
    if (VERSION_MAJOR >= 2) {
      vCOUNTDOWN_HI = 2;
    }
    else {
      vCOUNTDOWN_HI = 3;
    }
    if (vPREV_ROOM_NO == ROOM49_INSIDE_WELL) {
      position(oEGO, 75, 26);
      stop.motion(oEGO);
    }
    else {
      position(oEGO, 38, 116);
      vEGO_DIR = DIR_EAST;
    }
    draw(oEGO);
    player.control();
    if (fGAME_OVER) {
      reset(fGAME_OVER);
      return;
    }
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_IN_WELL);
  }
  ignore.horizon(oEGO);
  load.view(73);
  set.view(oEGO, 73);
  load.view(114);
  set(fEGO_ON_WATER);
  if (vPREV_ROOM_NO == ROOM49_INSIDE_WELL) {
    position(oEGO, 75, 26);
  }
  else {
    position(oEGO, 38, 116);
  }
  if (vPREV_ROOM_NO == ROOM51_DRAGONS_LAIR) {
    if (!fPOINTS_FOR_LEAVING_DRAGONS_LAIR) {
      set(fPOINTS_FOR_LEAVING_DRAGONS_LAIR);
      vSCORE += 2;
    }
  }
  vCOUNTDOWN_LO = 250;
  if (VERSION_MAJOR >= 2) {
    vCOUNTDOWN_HI = 2;
  }
  else {
    vCOUNTDOWN_HI = 3;
  }
  if (fCARRYING_BUCKET && !fBUCKET_FILLED) {
    set(fBUCKET_FILLED);
    get("water");
    if (!fFILL_BUCKET_POINTS) {
      set(fFILL_BUCKET_POINTS);
      vSCORE += 2;
    }
  }
  draw(oEGO);
  show.pic();
  if (fEGO_INVISIBLE) {
    drop(iRING);
    vSCORE -= 3;
    print("Thrashing around in the water has caused you to lose the magic ring and become visible again.");
    reset(fCARRYING_RING);
    reset(fEGO_INVISIBLE);
    reset(fWEARING_RING);
    set(fLOST_RING);
    vINVISIBLE_COUNTDOWN_LO = 0;
    vINVISIBLE_COUNTDOWN_HI = 0;
  }
  if (BUILD_TARGET == APPLE_IIGS) {
    rvCOUNTDOWN_TO_SND_IN_WELL = 30;
  }
}
if (BUILD_TARGET == APPLE_IIGS) {
  rvCOUNTDOWN_TO_SND_IN_WELL--;
  if (rvCOUNTDOWN_TO_SND_IN_WELL == 1 && !rfDEAD) {
    rvCOUNTDOWN_TO_SND_IN_WELL = 30;
    sound(SND_IN_WELL, fMISC_END_MARKER);
  }
}
vCOUNTDOWN_LO--;
if (vCOUNTDOWN_LO == 0) {
  vCOUNTDOWN_HI--;
  if (vCOUNTDOWN_HI > 0) {
    vCOUNTDOWN_LO = 250;
  }
}
if (vCOUNTDOWN_LO == 0 && vCOUNTDOWN_HI == 0 && !rfDEAD) {
  set(rfDEAD);
  if (BUILD_TARGET == APPLE_IIGS) {
    rvGAME_OVER_COUNTDOWN = 20;
  }
  set.view(oEGO, 114);
  print("Not even you can hold your breath forever.  The well bottom has become your eternal resting place.");
  ignore.blocks(oEGO);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  if (vTEMP_Y < 141) {
    move.obj(oEGO, 65, 140, 1, rfDEAD_AND_SUNK);
  }
  if (vTEMP_Y > 140) {
    set(rfDEAD_AND_SUNK);
  }
}
if (BUILD_TARGET == APPLE_IIGS) {
  rvGAME_OVER_COUNTDOWN--;
  if (rvGAME_OVER_COUNTDOWN == 1 || rfDEAD_AND_SUNK) {
    rvGAME_OVER_COUNTDOWN = 0;
    reset(rfDEAD_AND_SUNK);
    set(fGAME_OVER);
  }
}
else if (rfDEAD_AND_SUNK) {
  reset(rfDEAD_AND_SUNK);
  set(fGAME_OVER);
}
if (said("examine", "water")) {
  print("This looks like typical well water.");
}
if (said("examine", "bottle")) {
  print("It looks like this bottle has been here for a very long time.");
}
if (said("get", "bottle")) {
  print("This is just an old, junky bottle.  You don't want it.");
}
if (said("examine", "well")) {
  print(m5);
}
if (said("examine", "hole")) {
  if (posn(oEGO, BX_NEAR_HOLE)) {
    print("It is very difficult to tell where the hole might lead.");
  }
  else {
    print("You are too far away to see clearly.");
  }
}
if (said("examine", "can")) {
  print("The rusty old can has been here awhile.");
}
if (said("get", "can")) {
  print("You have no use for this old can.");
}
if (posn(oEGO, BX_HOLE_EXIT_ZONE)) {
  reset(fEGO_ON_WATER);
  new.room(ROOM51_DRAGONS_LAIR);
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM49_INSIDE_WELL);
}
return;

#message 1 "Not even you can hold your breath forever.  The well bottom has become your eternal resting place."
#message 2 "This looks like typical well water."
#message 3 "It looks like this bottle has been here for a very long time."
#message 4 "This is just an old, junky bottle.  You don't want it."
#if VERSION_MAJOR >= 2
  #message 5 "You are at the bottom of the well.  Through the plants you spy what seems to be a hole in the side of the well."
#else
  #message 5 "You are at the bottom of th well.  Through the plants you spy what seems to be a hole in the side of the well."
#endif
#message 6 "It is very difficult to tell where the hole might lead."
#message 7 "You are too far away to see clearly."
#message 8 "The rusty old can has been here awhile."
#message 9 "You have no use for this old can."
#message 10 "Thrashing around in the water has caused you to lose the magic ring and become visible again."
#if BUILD_TARGET == APPLE_IIGS
  #message 99 "BREAK"
#endif
