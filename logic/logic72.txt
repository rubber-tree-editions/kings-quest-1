#include "vars.txt"

#define rfFELL_THROUGH_CLOUDS rf0
#define rfFALLING rf1
#define rfFELL_OFF_BEANSTALK rf2
#define rfPLAYING_SND_FALLING rf3
#if RUBBER_TREE
  #define rfFALLING_FROM_CLOUDS rf4
#endif

#define rvLOCK_PRIORITY rv0

#if RUBBER_TREE
if (fUNDO && (rfFALLING || fFALLING_TO_EARTH)) {
  stop.sound();
  reset(fUNDO);
  erase(oEGO);
  vRESTORE_ROOM = 0;
  reset(fFALLING_TO_EARTH);
  reset(rfFALLING);
  reset(rfPLAYING_SND_FALLING);
  release.priority(oEGO);
  if (rfFALLING_FROM_CLOUDS) {
    reset(fEGO_CLIMBING_BEANSTALK);
    reset(rfFALLING_FROM_CLOUDS);
    set.view(oEGO, 0);
    vEGO_DIR = 0;
    if (vRESTORE_DIR == DIR_SOUTH) {
      set.loop(oEGO, 2);
    }
    else if (vRESTORE_DIR == DIR_NORTH) {
      set.loop(oEGO, 3);
    }
    else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
      set.loop(oEGO, 0);
    }
    else {
      set.loop(oEGO, 1);
    }
  }
  else {
    set(fEGO_CLIMBING_BEANSTALK);
    set.view(oEGO, 74);
    vEGO_DIR = 0;
  }
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  reset(fCYCLE_EGO_ANIMATION);
  player.control();
  draw(oEGO);
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.view(74);
  load.sound(SND_FALLING);
  #if RUBBER_TREE
  if (vRESTORE_ROOM == vROOM_NO)
  {
    reset(fUNDO);
    vRESTORE_ROOM = 0;
    reset(fFALLING_TO_EARTH);
    set(fEGO_CLIMBING_BEANSTALK);
    set.view(oEGO, 74);
    vEGO_DIR = 0;
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    position.v(oEGO, vRESTORE_X, vRESTORE_Y);
    vTEMP_STEP_SIZE = EGO_STEP_SIZE;
    step.size(oEGO, vTEMP_STEP_SIZE);
    reset(fCYCLE_EGO_ANIMATION);
    player.control();
  }
  else
  #endif
  {
  if (vPREV_ROOM_NO == ROOM56_CLOUDS_GRASS_EDGE) {
    position(oEGO, 145, 109);
  }
  if (vPREV_ROOM_NO == ROOM71_BEANSTALK_NEAR_TOP) {
    set.view(oEGO, 74);
    position(oEGO, 66, 165);
    move.obj(oEGO, 66, 165, 0, fMISC_END_MARKER);
  }
  }
  draw(oEGO);
  show.pic();
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fTENSE_SITUATION);
    set(fTHREAT_OF_SORCERER_OR_OGRE);
    set(fFALL_DANGER);
  }
}
if (fFALLING_TO_EARTH && !rfFELL_OFF_BEANSTALK && !rfPLAYING_SND_FALLING) {
  sound(SND_FALLING, fSOUND_END_MARKER);
  set(rfPLAYING_SND_FALLING);
}
if (VERSION_MAJOR < 2 && fSOUND_END_MARKER) {
  if (!rfFELL_OFF_BEANSTALK) {
    reset(rfPLAYING_SND_FALLING);
    reset(fSOUND_END_MARKER);
  }
}
if (!posn(oEGO, 65, 100, 84, 120) && !fEGO_CLIMBING_BEANSTALK && !fEGO_INVISIBLE) {
  if (said("climb", "beanstalk")) {
    print("You need to be closer to do that.");
  }
}
if (fEGO_INVISIBLE) {
  if (said("climb", "beanstalk")) {
    print("You can't do that when you are invisible!");
  }
}
if (fEGO_CLIMBING_BEANSTALK) {
  if (said("examine", "beanstalk")) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_Y > 140) {
      print("Your heart thuds.  Dare you climb into the clouds?");
    }
    if (vTEMP_Y < 141) {
      print("It is amazing that you actually climbed up this high!");
    }
  }
}
if (!fEGO_CLIMBING_BEANSTALK) {
  if (said("examine", "beanstalk")) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_X > 89) {
      print("It is hard to believe those seeds you planted grew this high.");
    }
    if (vTEMP_X < 90) {
      print("It is a long, long way down....");
    }
  }
}
if (said("examine", "clouds") || said("examine", "room")) {
  print("You are in the Land of the Clouds.  It is rumored a giant lives up here.");
}
if (fEGO_TOUCHED_TRIGGER_LINE
  && !rfFALLING
  && !posn(oEGO, 65, 100, 82, 120)
  && !fEGO_CLIMBING_BEANSTALK
) {
  #if RUBBER_TREE
    set(rfFALLING_FROM_CLOUDS);
    vRESTORE_DIR = vEGO_DIR;
  #endif
  set(rfFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvLOCK_PRIORITY);
  set.priority.v(oEGO, rvLOCK_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_THROUGH_CLOUDS);
  sound(SND_FALLING, fSOUND_END_MARKER_2);
}
if (fEGO_TOUCHED_TRIGGER_LINE && !rfFALLING && posn(oEGO, 65, 100, 82, 120) && fEGO_INVISIBLE) {
  #if RUBBER_TREE
    set(rfFALLING_FROM_CLOUDS);
    vRESTORE_DIR = vEGO_DIR;
  #endif
  set(rfFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvLOCK_PRIORITY);
  set.priority.v(oEGO, rvLOCK_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_THROUGH_CLOUDS);
  sound(SND_FALLING, fSOUND_END_MARKER_2);
}
if (rfFELL_THROUGH_CLOUDS) {
  reset(rfFELL_THROUGH_CLOUDS);
  #if RUBBER_TREE
  if (rfFALLING_FROM_CLOUDS)
  #endif
  {
  erase(oEGO);
  print("Whoops!  You fall toward a distant earth.");
  set(fGAME_OVER);
  }
}
if (fEGO_TOUCHED_TRIGGER_LINE && posn(oEGO, 65, 100, 82, 120) && !fEGO_INVISIBLE) {
  if (fEGO_CLIMBING_BEANSTALK) {
    reset(fEGO_CLIMBING_BEANSTALK);
    erase(oEGO);
    set.view(oEGO, 0);
    position(oEGO, 85, 112);
    draw(oEGO);
    move.obj(oEGO, 85, 112, 0, fMISC_END_MARKER);
    goto(FINALCHECKS);
  }
  if (!fEGO_CLIMBING_BEANSTALK) {
    set(fEGO_CLIMBING_BEANSTALK);
    erase(oEGO);
    set.view(oEGO, 74);
    position(oEGO, 70, 123);
    draw(oEGO);
    move.obj(oEGO, 70, 123, 0, fMISC_END_MARKER);
  }
}
if (fEGO_TOUCHED_TRIGGER_LINE && fEGO_CLIMBING_BEANSTALK) {
  program.control();
  print("Whoops!  You fall toward a distant earth.");
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  #if RUBBER_TREE
    vRESTORE_ROOM = vROOM_NO;
  #endif
  reset(fEGO_CLIMBING_BEANSTALK);
  set(fFALLING_TO_EARTH);
  erase(oEGO);
  set.view(oEGO, 105);
  if (vTEMP_X > 75) {
    vTEMP_X = 80;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  if (vTEMP_X < 76) {
    vTEMP_X = 65;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  vTEMP_Y = 166;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_OFF_BEANSTALK);
}
FINALCHECKS:
  #if RUBBER_TREE
    if (!rfFALLING && !fFALLING_TO_EARTH) {
      get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    }
  #endif
  if (rfFELL_OFF_BEANSTALK || posn(oEGO, 0, 166, 159, 167)) {
    new.room(ROOM71_BEANSTALK_NEAR_TOP);
  }
  if (vEGO_EDGE_CODE == EDGE_RIGHT) {
    new.room(ROOM56_CLOUDS_GRASS_EDGE);
  }
  return;

#message 1 "If you were to jump or duck here you might fall."
#message 2 "You need to be closer to do that."
#message 3 "You can't do that when you are invisible!"
#message 4 "Whoops!  You fall toward a distant earth."
#message 5 "Your heart thuds.  Dare you climb into the clouds?"
#message 6 "It is a long, long way down...."
#message 7 "It is amazing that you actually climbed up this high!"
#message 8 "It is hard to believe those seeds you planted grew this high."
#message 9 "You are in the Land of the Clouds.  It is rumored a giant lives up here."
