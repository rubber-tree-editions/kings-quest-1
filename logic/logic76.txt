#include "vars.txt"

#define rfLEPRECHAUN_CAUGHT_YOU rf0
#define rfLEPRECHAUNS_DANCING_AWAY rf1
#define rfPLAYING_FIDDLE rf2
#define rfPLAYED_SND_FIDDLE_TUNE rf3
#if RUBBER_TREE
  #define rvINTRO_MESSAGE_COUNTDOWN rv1
#endif

#define rvCOUNTDOWN rv0

#define oOTHER_LEPRECHAUNS o1
#define oLEPRECHAUN1 o2
#define oLEPRECHAUN2 o13

#if RUBBER_TREE
if (fUNDO && fGAME_OVER) {
  reset(fSCRIPTED_MOTION);
  reset(fGAME_OVER);
  reset(fGAME_OVER_PREPARATION);
  reset(fUNDO);
  if (vRESTORE_ROOM != ROOM76_LEPRECHAUN_ANTECHAMBER) {
    new.room.v(vRESTORE_ROOM);
    return;
  }
  set(fRESTORE_STATE);
  erase(oEGO);
  erase(oLEPRECHAUN1);
  erase(oLEPRECHAUN2);
  set.view(oEGO, 0);
  stop.motion(oEGO);
  position(oEGO, 113, 146);
  vEGO_DIR = DIR_WEST;
  position(oLEPRECHAUN1, 60, 136);
  position(oLEPRECHAUN2, 45, 119);
  follow.ego(oLEPRECHAUN1, 5, rfLEPRECHAUN_CAUGHT_YOU);
  reset(fSCRIPTED_MOTION);
  start.cycling(oLEPRECHAUN1);
  draw(oEGO);
  draw(oLEPRECHAUN1);
  draw(oLEPRECHAUN2);
  player.control();
  rvINTRO_MESSAGE_COUNTDOWN = 3;
  return;
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
  if (fOVERRIDE_RESET_ENTRANCE) {
    reset(fOVERRIDE_RESET_ENTRANCE);
  }
  else if (vRESTORE_ROOM == ROOM76_LEPRECHAUN_ANTECHAMBER) {
    set(fRESTORE_STATE);
    reset(fDANCING_LEPRECHAUNS);
    vPREV_ROOM_NO = ROOM75_RAT_STOMPING_GROUND;
    vEGO_DIR = DIR_WEST;
    reset(fLEPRECHAUN_KING_GONE);
    if (fCARRYING_SCEPTRE) {
      reset(fSCEPTRE_TAKEN);
      reset(fCARRYING_SCEPTRE);
      drop("sceptre");
      vSCORE -= 6;
    }
    if (fCARRYING_SHIELD) {
      reset(fSHIELD_TAKEN);
      reset(fCARRYING_SHIELD);
      drop("shield");
      vSCORE -= 8;
    }
  }
  else if (vRESTORE_ROOM != ROOM22_CONDOR_AND_CAVE && fCARRYING_MUSHROOM) {
    vRESTORE_ROOM = ROOM76_LEPRECHAUN_ANTECHAMBER;
    set(fSTORE_STATE);
  }
  #endif
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (vPREV_ROOM_NO == ROOM75_RAT_STOMPING_GROUND) {
    position(oEGO, 113, 146);
  }
  if (vPREV_ROOM_NO == ROOM77_LEPRECHAUN_KING_THRONE_ROOM) {
    position(oEGO, 73, 163);
  }
  if (fCARRYING_FIDDLE) {
    load.view(50);
    animate.obj(oOTHER_LEPRECHAUNS);
    set.view(oOTHER_LEPRECHAUNS, 50);
    position(oOTHER_LEPRECHAUNS, 96, 78);
    if (BUILD_TARGET == APPLE_IIGS) {
      load.sound(SND_FIDDLE_TUNE2);
    }
    else {
      load.sound(SND_FIDDLE_TUNE);
    }
  }
  if (!fDANCING_LEPRECHAUNS) {
    if (VERSION_MAJOR >= 2) {
      load.view(47);
    }
    load.view(78);
    load.view(79);
    load.view(80);
    animate.obj(oLEPRECHAUN1);
    animate.obj(oLEPRECHAUN2);
    set.view(oLEPRECHAUN1, 79);
    if (VERSION_MAJOR >= 2) {
      set.view(oLEPRECHAUN2, 47);
    }
    else {
      set.view(oLEPRECHAUN2, 80);
    }
    position(oLEPRECHAUN1, 60, 136);
    position(oLEPRECHAUN2, 45, 119);
    stop.cycling(oLEPRECHAUN2);
    stop.motion(oLEPRECHAUN2);
  }
  if (!fDANCING_LEPRECHAUNS) {
    draw(oLEPRECHAUN2);
    draw(oLEPRECHAUN1);
    if (fCARRYING_CLOVER) {
      wander(oLEPRECHAUN1);
    }
    else {
      follow.ego(oLEPRECHAUN1, 5, rfLEPRECHAUN_CAUGHT_YOU);
    }
  }
  draw(oEGO);
  show.pic();
  if (!fDANCING_LEPRECHAUNS) {
    if (!fCARRYING_CLOVER && fEGO_INVISIBLE) {
      print("The leprechauns' magic powers detect your unseen presence.");
    }
    if (!fCARRYING_CLOVER && !fEGO_INVISIBLE) {
      print("The leprechauns do not like your invasion of their territory.  They may not let you get any further.");
    }
    if (fCARRYING_CLOVER) {
      print("The leprechauns revere the power of the fourleaf clover. With it in your possession they will leave you absolutely alone.");
    }
  }
}
#if RUBBER_TREE
rvINTRO_MESSAGE_COUNTDOWN--;
if (rvINTRO_MESSAGE_COUNTDOWN == 1) {
  if (!fDANCING_LEPRECHAUNS) {
    if (!fCARRYING_CLOVER && fEGO_INVISIBLE) {
      print("The leprechauns' magic powers detect your unseen presence.");
    }
    if (!fCARRYING_CLOVER && !fEGO_INVISIBLE) {
      print("The leprechauns do not like your invasion of their territory.  They may not let you get any further.");
    }
    if (fCARRYING_CLOVER) {
      print("The leprechauns revere the power of the fourleaf clover. With it in your possession they will leave you absolutely alone.");
    }
  }
}
#endif
if (rfLEPRECHAUN_CAUGHT_YOU) {
  reset(rfLEPRECHAUN_CAUGHT_YOU);
  print("The leprechaun catches you and holds you with his small,  but strong arms.  I don't think the leprechaun will ever let go.");
  stop.motion(oEGO);
  set(fSCRIPTED_MOTION);
  stop.cycling(oEGO);
  stop.motion(oLEPRECHAUN1);
  stop.cycling(oLEPRECHAUN1);
  set(fGAME_OVER);
}
if (rfPLAYING_FIDDLE) {
  if (said("play", "fiddle")) {
    print("You are already playing the fiddle.");
  }
}
if (rvCOUNTDOWN > 0) {
  rvCOUNTDOWN--;
}
if (!fDANCING_LEPRECHAUNS && !rfPLAYING_FIDDLE) {
  if (fCARRYING_FIDDLE && said("play", "fiddle")) {
    if (BUILD_TARGET == APPLE_IIGS) {
      stop.motion(oEGO);
    }
    set(rfLEPRECHAUNS_DANCING_AWAY);
    set(rfPLAYING_FIDDLE);
    if (BUILD_TARGET == APPLE_IIGS) {
      rvCOUNTDOWN = 145;
    }
    else {
      rvCOUNTDOWN = 45;
    }
    if (BUILD_TARGET != APPLE_IIGS) {
      draw(oOTHER_LEPRECHAUNS);
    }
    if (BUILD_TARGET == APPLE_IIGS) {
      sound(SND_FIDDLE_TUNE2, rfPLAYED_SND_FIDDLE_TUNE);
    }
    else {
      sound(SND_FIDDLE_TUNE, rfPLAYED_SND_FIDDLE_TUNE);
    }
    if (VERSION_MAJOR >= 2) {
      erase(oLEPRECHAUN2);
      set.view(oLEPRECHAUN2, 80);
      draw(oLEPRECHAUN2);
    }
    start.cycling(oLEPRECHAUN2);
    wander(oLEPRECHAUN2);
    erase(oLEPRECHAUN1);
    set.view(oLEPRECHAUN1, 78);
    draw(oLEPRECHAUN1);
    wander(oLEPRECHAUN1);
    print("Leprechauns love fiddle music!  Have you ever seen such frenzied dancing?");
    set(fDANCING_LEPRECHAUNS);
    vSCORE += 3;
  }
}
if (rvCOUNTDOWN > 1 && rfPLAYED_SND_FIDDLE_TUNE) {
  reset(rfPLAYED_SND_FIDDLE_TUNE);
  if (BUILD_TARGET == APPLE_IIGS) {
    sound(SND_FIDDLE_TUNE2, rfPLAYED_SND_FIDDLE_TUNE);
  }
  else {
    sound(SND_FIDDLE_TUNE, rfPLAYED_SND_FIDDLE_TUNE);
  }
}
if (rvCOUNTDOWN == 1) {
  rvCOUNTDOWN = 0;
  stop.sound();
  reset(rfLEPRECHAUNS_DANCING_AWAY);
  reset(rfPLAYING_FIDDLE);
  erase(oLEPRECHAUN2);
  erase(oLEPRECHAUN1);
  if (BUILD_TARGET == APPLE_IIGS) {
    print("Totally involved in the music, they dance right out of the room.");
    start.motion(oEGO);
  }
  else {
    erase(oOTHER_LEPRECHAUNS);
    print("Totally involved in the music, they dance right out of the room.");
  }
}
if (rfLEPRECHAUNS_DANCING_AWAY) {
  if (vOBJECT_TOUCHING_EDGE == 13) {
    erase(oLEPRECHAUN2);
  }
  if (vOBJECT_TOUCHING_EDGE == 2) {
    erase(oLEPRECHAUN1);
  }
}
if (said("examine", "room")) {
  if (!fDANCING_LEPRECHAUNS) {
    print(m7);
  }
  else {
    print("You are in the antechamber of the Hall of the Leprechaun King.");
  }
}
if (said("close", "doors")) {
  print("I can think of no good reason to close the door.");
}
if (!fDANCING_LEPRECHAUNS) {
  if (said("examine", "leprechauns")) {
    print("They are tiny people dressed all in green.");
  }
  if (said("talk", "leprechauns") || said("hello") || said("say", "hello")) {
    print("I would not try talking to the leprechauns.  You might say the wrong thing.");
  }
  if (said("murder", "leprechauns")) {
    print("You would not get all of them before they got you.");
  }
}
if (fDANCING_LEPRECHAUNS) {
  if (said("anyword", "leprechauns")) {
    print("There are not any leprechauns here.");
  }
}
if (posn(oEGO, 128, 133, 129, 154)) {
  new.room(ROOM75_RAT_STOMPING_GROUND);
}
if (posn(oEGO, 0, 166, 159, 167)) {
  new.room(ROOM77_LEPRECHAUN_KING_THRONE_ROOM);
}
return;

#message 1 "The leprechauns' magic powers detect your unseen presence."
#message 2 "The leprechauns do not like your invasion of their territory.  They may not let you get any further."
#message 3 "The leprechauns revere the power of the fourleaf clover. With it in your possession they will leave you absolutely alone."
#message 4 "Leprechauns love fiddle music!  Have you ever seen such frenzied dancing?"
#message 5 "Totally involved in the music, they dance right out of the room."
#message 6 "The leprechaun catches you and holds you with his small,  but strong arms.  I don't think the leprechaun will ever let go."
#if VERSION_MAJOR >= 2
  #message 7 "You are in the antechamber of the Hall of the Leprechaun King.  There are leprechauns in the room with you."
#else
  #message 7 "You are in the antechamber of the Hall of the Leprechaun King.  There is a leprechaun in the room with you."
#endif
#message 8 "You are in the antechamber of the Hall of the Leprechaun King."
#message 9 "I can think of no good reason to close the door."
#message 10 "They are tiny people dressed all in green."
#message 11 "I would not try talking to the leprechauns.  You might say the wrong thing."
#message 12 "You would not get all of them before they got you."
#message 13 "There are not any leprechauns here."
#message 14 "You are already playing the fiddle."
