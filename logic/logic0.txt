#include "vars.txt"

// Note: Even though this is a global script, all code that refers to oGOAT
//   is designed to be run in a room where object 13 refers to the goat.
// In reality, object 13 may not always be the goat (e.g. Room 76).
#define oGOAT o13

if (vERROR_CODE > 0) {
  call(LOGIC98_ERROR);
}
if (vROOM_NO == ROOM_NULL) {
  if (BUILD_TARGET != APPLE_IIGS) {
    set.game.id("KQ1");
  }
  script.size(100);
  #if BUILD_TARGET == AMIGA
    set(fJOYSTICK_SENSITIVITY_ENABLED);
    vJOYSTICK_SENSITIVITY = 3;
  #elif BUILD_TARGET == APPLE_IIGS
    if (vCOMPUTER_TYPE == COMPUTERTYPE_PC) {
      set.game.id("PQ");
    }
    else {
      set.game.id("KQ");
    }
  #endif
  vMAX_SCORE = 158;
  if (BUILD_TARGET == APPLE_IIGS) {
    vCYCLE_DELAY = 0;
  }
  else {
    vTEMP_CYCLE_DELAY = 2;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (fRESTART_GAME_EXECUTED) {
    new.room(ROOM1_CASTLE_EAST);
  }
  else {
    if (VERSION_MAJOR >= 2) {
      set.menu("Info");
      #if BUILD_TARGET == MACINTOSH
        set.menu.item("Help     ", cHELP);
        set.menu.item("About KQ", cABOUT);
      #else
        set.menu.item("About KQ", cABOUT);
        set.menu.item("Help    ", cHELP);
      #endif
      set.menu("File");
      #if BUILD_TARGET == APPLE_IIGS
        set.menu.item("Save Game      <Shift-5>", cSAVE);
        set.menu.item("Restore Game   <Shift-7>", cRESTORE);
      #else
        set.menu.item("Save Game      <F5>", cSAVE);
        set.menu.item("Restore Game   <F7>", cRESTORE);
      #endif
      set.menu.item("-------------------", cNOTHING);
      #if BUILD_TARGET == APPLE_IIGS
        set.menu.item("Restart Game   <Shift-9>", cRESTART);
      #else
        set.menu.item("Restart Game   <F9>", cRESTART);
      #endif
      #if BUILD_TARGET == MACINTOSH
        set.menu.item("Pause         <Esc>", cPAUSE);
      #endif
      set.menu.item("-------------------", cNOTHING);
      #if BUILD_TARGET == APPLE_IIGS
        set.menu.item("Quit        <Option-Z>", cQUIT);
      #else
        set.menu.item("Quit        <Alt Z>", cQUIT);
      #endif
      set.menu("Game");
      #if BUILD_TARGET == DOS
        set.menu.item("See Object     <F4>", cSEE_OBJECT);
        set.menu.item("Inventory     <Tab>", cINVENTORY);
      #elif BUILD_TARGET == APPLE_IIGS
        set.menu.item("See Object     <Shift-4>", cSEE_OBJECT);
        set.menu.item("Inventory     <Tab>", cINVENTORY);
      #else
        set.menu.item("Inventory     <Tab>", cINVENTORY);
        set.menu.item("See Object     <F4>", cSEE_OBJECT);
        set.menu.item("Toggle Sound   <F2>", cTOGGLESOUND);
      #endif
      #if BUILD_TARGET == MACINTOSH
        set.menu("Action");
      #else
        set.menu("Action ");
      #endif
      set.menu.item("Swim  =", cSWIM);
      set.menu.item("Jump  0", cJUMP);
      set.menu.item("Duck  -", cDUCK);
      set.menu("Special");
      #if BUILD_TARGET == DOS
        set.menu.item("Sound On/Off       <F2>", cTOGGLESOUND);
        if (vCOMPUTER_TYPE != COMPUTERTYPE_AMIGA) {
          if (vCOMPUTER_TYPE == COMPUTERTYPE_ATARI_ST) {
            set.menu.item("Joystick/Mouse <Ctrl J>", cINITJOYSTICK);
          }
          else {
            if (vCOMPUTER_TYPE == COMPUTERTYPE_PC && vVIDEO_MODE != 3 && vVIDEO_MODE != 2) {
              set.menu.item("Graphics Mode  <Ctrl R>", cTOGGLEGFX);
            }
            set.menu.item("Reset Joystick <Ctrl J>", cINITJOYSTICK);
          }
        }
        set.menu.item("Pause             <Esc>", cPAUSE);
      #else
        #if BUILD_TARGET == MACINTOSH
          if (vCOMPUTER_TYPE == COMPUTERTYPE_PC && vVIDEO_MODE != 3 && vVIDEO_MODE != 2) {
            set.menu.item("Toggle RGB     <Ctrl R>", cTOGGLEGFX);
          }
        }
        #elif BUILD_TARGET == APPLE_IIGS
          set.menu.item("Sound On/Off   <Shift-2>", cTOGGLESOUND);
        #endif
        set.menu.item("Reset Joystick <Ctrl J>", cINITJOYSTICK);
      #endif
      #if BUILD_TARGET == DOS
        set.menu("Speed");
        set.menu.item("Normal", cSPEED_NORMAL);
        set.menu.item("Fast  ", cSPEED_FAST);
        set.menu.item("Slow  ", cSPEED_SLOW);
      #elif BUILD_TARGET == MACINTOSH
        if (vCOMPUTER_TYPE != COMPUTERTYPE_AMIGA) {
          set.menu("Speed");
          set.menu.item("Normal", cSPEED_NORMAL);
          set.menu.item("Fast  ", cSPEED_FAST);
          set.menu.item("Slow  ", cSPEED_SLOW);
        }
      #else
        set.menu.item("Pause           <Esc>", cPAUSE);
      #endif
      submit.menu();
    }
    new.room(ROOM83_TITLE_SCREEN);
  }
}
if (vROOM_NO == ROOM52_IN_WELL_UNDERWATER) {
  set(fEGO_ON_WATER);
}
if (BUILD_TARGET == DOS || BUILD_TARGET == APPLE_IIGS) {
  if (fROOM_SCRIPT_EXECUTED || fRESTORE_GAME_EXECUTED) {
    #if BUILD_TARGET == APPLE_IIGS
      if (vROOM_NO != ROOM1_CASTLE_EAST) {
        clear.lines(24, 24, "");
      }
    #endif
    if (vROOM_NO != ROOM83_TITLE_SCREEN) {
      set(fJOYSTICK_SENSITIVITY_ENABLED);
      disable.item(cNOTHING);
      set(fENABLE_MENU);
      vJOYSTICK_SENSITIVITY = 3;
    }
  }
}
if (fROOM_SCRIPT_EXECUTED) {
  if (BUILD_TARGET == APPLE_IIGS) {
    if (vPREV_ROOM_NO != ROOM70_BEANSTALK_NEAR_BOTTOM) {
      reset(fTENSE_SITUATION);
      reset(fTHREAT_OF_SORCERER_OR_OGRE);
      reset(fFALL_DANGER);
      reset(fEGO_PREOCCUPIED);
    }
    else {
      disable.item(cJUMP);
      disable.item(cDUCK);
      set(fFALL_DANGER);
    }
    vDROWN_GAME_OVER_COUNTDOWN = 0;
    reset(fSCRIPTED_MOTION);
    vTEMP_CYCLE_DELAY = 0;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
    reset(fDEBUG_MODE);
  }
  animate.obj(oEGO);
  load.view(0);
  if (vROOM_NO == ROOM52_IN_WELL_UNDERWATER) {
    vEGO_VIEW_NO = 0;
  }
  if (vROOM_NO == ROOM65_IN_GINGERBREAD_HOUSE && vPREV_ROOM_NO == ROOM80_IN_MID_AIR) {
    vEGO_VIEW_NO = 105;
  }
  if (fEGO_DUCKING || fJUMPING) {
    reset(fEGO_DUCKING);
    reset(fJUMPING);
    reset(fCYCLE_EGO_ANIMATION);
    vEGO_VIEW_NO = 0;
  }
  if (vPREV_ROOM_NO == ROOM10_GOAT_PEN_WEST && vROOM_NO != ROOM11_GOAT_PEN_EAST) {
    if (fGOAT_COMPANION) {
      vEGO_VIEW_NO = 0;
    }
  }
  if (vPREV_ROOM_NO == ROOM11_GOAT_PEN_EAST && vROOM_NO != ROOM10_GOAT_PEN_WEST) {
    if (fGOAT_COMPANION) {
      vEGO_VIEW_NO = 0;
    }
  }
  load.view.v(vEGO_VIEW_NO);
  set.view.v(oEGO, vEGO_VIEW_NO);
  load.logics(LOGIC102_DEFAULT_RESPONSES);
  if (BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH) {
    if (fCARRYING_FIDDLE) {
      load.sound(SND_FIDDLE_TUNE);
    }
  }
  if (vROOM_NO != ROOM53_KING_EDWARD_THRONE_ROOM
    && vROOM_NO != ROOM58_GIANT_STOMPING_GROUND
    && vROOM_NO != ROOM49_INSIDE_WELL
    && vROOM_NO != ROOM51_DRAGONS_LAIR
    && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
    && vROOM_NO != ROOM12_TOP_OF_WELL
    && vROOM_NO != ROOM10_GOAT_PEN_WEST
    && vROOM_NO != ROOM11_GOAT_PEN_EAST
  ) {
    load.view(75);
    load.view(87);
  }
  load.view(110);
  if (vROOM_NO != ROOM83_TITLE_SCREEN) {
    configure.screen(1, 22, 0);
  }
  if (fCARRYING_MUSHROOM || fEGO_TINY) {
    load.view(60);
  }
  if (fGOAT_COMPANION || vROOM_YOU_KILLED_THE_GOAT_IN > ROOM_NULL) {
    load.view(62);
  }
  if (BUILD_TARGET != APPLE_IIGS) {
    reset(fDEBUG_MODE);
  }
  observe.objs(oEGO);
  observe.blocks(oEGO);
  reset(fSWIMMING_LOGIC_LOADED);
  reset(fSWIMMING_IMPOSSIBLE_HERE);
  reset(fMISC_END_MARKER);
  reset(fSOUND_END_MARKER);
  reset(fSOUND_END_MARKER_2);
  reset(fFALLING);
  reset(fVILLAIN_GOT_YOU);
  if (BUILD_TARGET != APPLE_IIGS) {
    reset(fTENSE_SITUATION);
  }
  vTEMP_RESULT = 0;
  vTEMP_RESULT_2 = 0;
  vTEMP_RESULT_3 = 0;
  vTEMP_COUNTER = 20;
  do {
    reset.v(vTEMP_COUNTER);
    vTEMP_COUNTER++;
  } while (vTEMP_COUNTER < 45);
  vTEMP_COUNTER = 30;
  do {
    lindirectn(vTEMP_COUNTER, 0);
    vTEMP_COUNTER++;
  } while (vTEMP_COUNTER < 45);
  set.string(sPROMPT, ">");
  set.cursor.char("_");
  #if RUBBER_TREE
  if (vROOM_NO == ROOM63_UP_GOLDEN_EGG_TREE
      || vROOM_NO == ROOM30_WALNUT_TREE
      || vROOM_NO == ROOM6_POUCH_STUMP) {
    if (fTAKEN_GOLDEN_EGG && fPOUCH_TAKEN && fWALNUT_TAKEN && !fCARRYING_GOLD_WALNUT && !fCARRYING_WALNUT && !fCARRYING_POUCH && !fCARRYING_DIAMONDS && !fCARRYING_GOLDEN_EGG) {
      if (!fRAT_IS_GONE && !fCARRYING_SWISS_CHEESE) {
        if (vCUPBOARD_STATE == CUPBOARDSTATE_CLOSED_NO_CHEESE || vCUPBOARD_STATE == CUPBOARDSTATE_OPEN_NO_CHEESE) {
          set(fREPLENISH_MINOR_TREASURE);
        }
      }
      if (!fTROLL_TOOK_TREASURE && !fGOAT_KILLED_TROLL) {
        if (fGOAT_IS_GONE || vROOM_YOU_KILLED_THE_GOAT_IN != ROOM_NULL) {
          set(fREPLENISH_MINOR_TREASURE);
        }
      }
    }
  }
  #endif
}
if (BUILD_TARGET != AMIGA) {
  get.posn(oEGO, vTEMP_X_2, vTEMP_Y_2);
}
if (VERSION_MAJOR < 2) {
  if (fGAME_OVER) {
    if (!fGAME_OVER_PREPARATION) {
      if (BUILD_TARGET == AMIGA) {
        disable.item(cSAVE);
        disable.item(cJUMP);
        disable.item(cSWIM);
        disable.item(cDUCK);
      }
      load.logics(LOGIC100_GAME_OVER);
      load.sound(SND_GAME_OVER);
      vGAME_OVER_MESSAGE_COUNTDOWN = 40;
      set(fGAME_OVER_PREPARATION);
    }
    program.control();
    stop.motion(oEGO);
    call(LOGIC100_GAME_OVER);
    return;
  }
}
if (VERSION_MAJOR >= 2) {
  if (controller(cSHOW_MENU) && vROOM_NO != ROOM83_TITLE_SCREEN) {
    menu.input();
  }
}
#if BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
  if (controller(cDEBUG)) {
    load.logics(LOGIC99_DEBUG);
    set(fDEBUG_MODE);
    #if BUILD_TARGET == DOS
      print("VERSION 2.0F");
    #else
      print("VERSION 2.0C");
    #endif
    version();
  }
#elif BUILD_TARGET == APPLE_IIGS
  if (controller(cDEBUG)) {
    if (fDEBUG_MODE) {
      clear.lines(24, 24, "");
      reset(fDEBUG_MODE);
    }
    print("  King's Quest \n\nApple IIgs Version \n\n    1.0S-88223 \n");
    version();
  }
#endif
// using #if because "kqdbg" is not in other dictionaries
#if BUILD_TARGET == APPLE_IIGS
  if (said("kqdbg")) {
    toggle(fDEBUG_MODE);
    clear.lines(24, 24, "");
    load.logics(99);
    print("  King's Quest \n\nApple IIgs Version \n\n    1.0S-88223 \n");
    version();
  }
#endif
if (VERSION_MAJOR >= 2) {
  if (
    said("fast")
  #if BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
    || controller(cSPEED_FAST)
  #endif
  ) {
    vTEMP_CYCLE_DELAY = 0;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (
    said("normal")
  #if BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
    || controller(cSPEED_NORMAL)
  #endif
  ) {
    if (BUILD_TARGET == APPLE_IIGS) {
      vTEMP_CYCLE_DELAY = 0;
    }
    else {
      vTEMP_CYCLE_DELAY = 2;
    }
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (
    said("slow")
  #if BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
    || controller(cSPEED_SLOW)
  #endif
  ) {
    if (BUILD_TARGET == APPLE_IIGS) {
      vTEMP_CYCLE_DELAY = 2;
    }
    else {
      vTEMP_CYCLE_DELAY = 4;
    }
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (said("quit") || controller(cQUIT)) {
    stop.sound();
    quit(0);
  }
  if (said("restore", "game") || controller(cRESTORE)) {
    restore.game();
  }
  if (said("restart", "game") || controller(cRESTART)) {
    restart.game();
  }
  if (controller(cINVENTORY)) {
    status();
  }
  #if RUBBER_TREE
    if (said("undo") || controller(cUNDO)) {
      set(fUNDO);
    }
  #endif
  if (controller(cSEE_OBJECT)) {
    set(fSTATUS_TO_SELECT_ITEMS);
    status();
    if (vSELECTED_INV_ITEM > 0 && vSELECTED_INV_ITEM != 255) {
      vTEMP_RESULT = vSELECTED_INV_ITEM;
      vTEMP_RESULT += 116;
      if (vTEMP_RESULT == 125 && vBOWL_STATE == BOWLSTATE_FULL_OF_STEW) {
        vTEMP_RESULT++;
      }
      if (
        (vTEMP_RESULT == 127 || vTEMP_RESULT == 142)
        && fBUCKET_FILLED
      #if BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
        && !fEGO_ON_WATER
      #endif
      ) {
        vTEMP_RESULT = 128;
      }
      show.obj.v(vTEMP_RESULT);
    }
    reset(fSTATUS_TO_SELECT_ITEMS);
  }
  if (controller(cABOUT)) {
    #if BUILD_TARGET == APPLE_IIGS
      print("   King's Quest\n\nby Roberta Williams.\n");
    #else
      print("King's Quest was designed by Roberta Williams.");
    #endif
  }
  if (
    #if BUILD_TARGET == APPLE_IIGS
      said("help") ||
    #endif
    controller(cHELP)
  ) {
    call(LOGIC92_HELP);
  }
}
else if (BUILD_TARGET == AMIGA) {
  if (controller(cHELP) && vROOM_NO != ROOM83_TITLE_SCREEN) {
    call(LOGIC92_HELP);
  }
}
else {
  if (controller(cHELP)) {
    call(LOGIC92_HELP);
  }
}
if (controller(cTOGGLESOUND)) {
  toggle(fSOUND_ENABLED);
}
if (BUILD_TARGET == ATARI_ST || BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH) {
  if (controller(cTOGGLEGFX)) {
    toggle.monitor();
  }
}
if (controller(cINITJOYSTICK)) {
  init.joy();
}
if (controller(cCLEARINPUT)) {
  cancel.line();
}
#if BUILD_TARGET == AMIGA
  if (controller(cABOUT)) {
    print(
      "        King's Quest" "\n"
      "             by" "\n"
      "      Roberta Williams" "\n"
      "" "\n"
      "    Original version by" "\n"
      "Arthur Abraham, Chuck Tingley" "\n"
      "        Ken McNeill" "\n"
      "" "\n"
      "        Graphics by" "\n"
      " Doug McNeill, Greg Rowland" "\n"
      "" "\n"
      "      This version by" "\n"
      "       Sol Ackerman" "\n"
      "" "\n"
      "(C) 1986 Sierra On-Line, Inc."
    );
  }
#endif
if (BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH) {
  if (fGAME_OVER) {
    if (!fGAME_OVER_PREPARATION) {
      load.logics(LOGIC100_GAME_OVER);
      load.sound(SND_GAME_OVER);
      vGAME_OVER_MESSAGE_COUNTDOWN = 40;
      set(fGAME_OVER_PREPARATION);
    }
    program.control();
    stop.motion(oEGO);
    disable.item(cSAVE);
    disable.item(cPAUSE);
    disable.item(cSWIM);
    disable.item(cJUMP);
    disable.item(cDUCK);
    call(LOGIC100_GAME_OVER);
    return;
  }
}
else if (BUILD_TARGET == APPLE_IIGS) {
  if (fGAME_OVER) {
    goto(END_OF_BOWL_LOGIC);
  }
}
if (vROOM_NO > ROOM52_IN_WELL_UNDERWATER
  || vROOM_NO == ROOM3_ROCK_OVER_HOLE
  || vROOM_NO == ROOM6_POUCH_STUMP
  || vROOM_NO == ROOM9_FAIRY_GODMOTHER_STOMPING_GROUND
  || vROOM_NO == ROOM10_GOAT_PEN_WEST
  || vROOM_NO == ROOM11_GOAT_PEN_EAST
  || vROOM_NO == ROOM12_TOP_OF_WELL
  || vROOM_NO == ROOM13_WOLF_STOMPING_GROUND
  || vROOM_NO == ROOM14_BASE_GOLDEN_EGG_TREE
  || vROOM_NO == ROOM15_CARROT_PATCH_WEST
  || vROOM_NO == ROOM16_CARROT_PATCH_EAST
  || vROOM_NO == ROOM19_BASE_MOUNTAIN_DOOR
  || vROOM_NO == ROOM21_WITCH_STOMPING_GROUND
  || vROOM_NO == ROOM22_CONDOR_AND_CAVE
  || vROOM_NO == ROOM24_CLOVER_PATCH
  || vROOM_NO == ROOM27_BIG_FOREGROUND_TREE
  || vROOM_NO == ROOM28_OUTSIDE_GINGERBREAD_HOUSE
  || vROOM_NO == ROOM29_SORCERER_STOMPING_GROUND
  || vROOM_NO == ROOM30_WALNUT_TREE
  || vROOM_NO == ROOM31_FREE_BOWL
  || vROOM_NO == ROOM35_DWARF_MEADOW
  || vROOM_NO == ROOM36_ROCK_WITH_TINY_HOLE
  || vROOM_NO == ROOM38_WILD_FLOWER_BED
  || vROOM_NO == ROOM44_OUTSIDE_WOODCUTTER_COTTAGE_FRONT
  || vROOM_NO == ROOM45_OUTSIDE_WOODCUTTER_COTTAGE_BACK
  || vROOM_NO == ROOM46_OGRE_STOMPING_GROUND
  || vROOM_NO == ROOM48_LEPRECHAUN_HOLE
  || vROOM_NO == ROOM50_CAVE_ADJOINING_DRAGONS_LAIR
  || vROOM_NO == ROOM51_DRAGONS_LAIR
) {
  if (said("swim") || said("go", "swim") || said("swim", "anyword") || controller(cSWIM)) {
    print("You need to be in water to swim.");
  }
}
if (BUILD_TARGET == AMIGA) {
  get.posn(oEGO, vTEMP_X_2, vTEMP_Y_2);
}
#if BUILD_TARGET != AMIGA
  if (vEGO_DIR == DIR_NONE) {
    if (fCYCLE_EGO_ANIMATION) {
      start.cycling(oEGO);
    }
    else {
      stop.cycling(oEGO);
    }
  }
  else if (
    !fSCRIPTED_MOTION
    #if VERSION_MAJOR >= 2
      && !fEGO_DUCKING
    #endif
  )
#endif
{
  if (vEGO_DIR == vSAVED_EGO_DIR && vTEMP_X_2 == vSAVED_EGO_X && vTEMP_Y_2 == vSAVED_EGO_Y) {
    stop.cycling(oEGO);
  }
  else {
    start.cycling(oEGO);
  }
}
if (BUILD_TARGET == AMIGA) {
  vSAVED_EGO_X = vTEMP_X_2;
  vSAVED_EGO_Y = vTEMP_Y_2;
  vSAVED_EGO_DIR = vEGO_DIR;
  if (fCYCLE_EGO_ANIMATION) {
    start.cycling(oEGO);
  }
  else if (__OR__(vEGO_DIR <= DIR_NONE)) {
    stop.cycling(oEGO);
  }
}
if (BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH) {
  if (said("save", "game") || controller(cSAVE)) {
    save.game();
  }
}
else if (BUILD_TARGET == APPLE_IIGS) {
  if (said("save", "game") || controller(cSAVE)) {
    if (!fGAME_OVER) {
      save.game();
    }
    else {
      reset(fSAID_ACCEPTED_USER_INPUT);
    }
  }
}
else {
  if (controller(cDEBUG)) {
    load.logics(LOGIC99_DEBUG);
    set(fDEBUG_MODE);
    print(m17);
    version();
  }
  if (said("fast")) {
    vTEMP_CYCLE_DELAY = 0;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (said("normal")) {
    vTEMP_CYCLE_DELAY = 2;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  if (said("slow")) {
    vTEMP_CYCLE_DELAY = 4;
    vCYCLE_DELAY = vTEMP_CYCLE_DELAY;
  }
  #if BUILD_TARGET == ATARI_ST
  if ((said("quit") || controller(cQUIT))) {
  #else
  if ((said("quit") || controller(cQUIT)) && vROOM_NO != ROOM83_TITLE_SCREEN) {
  #endif
    quit(0);
  }
  #if BUILD_TARGET == ATARI_ST
  if ((said("save", "game") || controller(cSAVE))) {
  #else
  if ((said("save", "game") || controller(cSAVE)) && vROOM_NO != ROOM83_TITLE_SCREEN) {
  #endif
    save.game();
  }
  #if BUILD_TARGET == ATARI_ST
  if ((said("restore", "game") || controller(cRESTORE))) {
  #else
  if ((said("restore", "game") || controller(cRESTORE)) && vROOM_NO != ROOM83_TITLE_SCREEN) {
  #endif
    restore.game();
  }
  #if BUILD_TARGET == ATARI_ST
  if ((said("restart", "game") || controller(cRESTART))) {
  #else
  if ((said("restart", "game") || controller(cRESTART)) && vROOM_NO != ROOM83_TITLE_SCREEN) {
  #endif
    restart.game();
  }
  #if BUILD_TARGET == ATARI_ST
  if (controller(cINVENTORY)) {
  #else
  if (controller(cINVENTORY) && vROOM_NO != ROOM83_TITLE_SCREEN) {
  #endif
    status();
  }
}
#if RUBBER_TREE
#if BUILD_TARGET == ATARI_ST
  if ((said("undo") || controller(cUNDO))) {
#else
  if ((said("undo") || controller(cUNDO)) && vROOM_NO != ROOM83_TITLE_SCREEN) {
#endif
    set(fUNDO);
  }
#endif
if (controller(cPREVINPUT)) {
  echo.line();
}
if (controller(cPAUSE)) {
  pause();
}
if (vROOM_NO == ROOM21_WITCH_STOMPING_GROUND) { }
else {
  if (said("jump") || controller(cJUMP)) {
    if (!fJUMPING
        && !fEGO_CLIMBING_BEANSTALK
        && !fFALLING_TO_EARTH
        && !fEGO_INVISIBLE
        && !fEGO_TINY
        && vROOM_NO != ROOM79_IN_WOODCUTTER_COTTAGE
        && !fEGO_ON_WATER
      #if BUILD_TARGET != APPLE_IIGS
        && !fTENSE_SITUATION
      #endif
        && !fEGO_DUCKING
        && vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE
      #if BUILD_TARGET == APPLE_IIGS
        && !fEGO_PLAYING_FIDDLE
        && !fEGO_PREOCCUPIED
        && !fTENSE_SITUATION
      #endif
        && vROOM_NO != ROOM29_SORCERER_STOMPING_GROUND
        && vROOM_NO != ROOM13_WOLF_STOMPING_GROUND
        && vROOM_NO != ROOM53_KING_EDWARD_THRONE_ROOM
        && vROOM_NO != ROOM58_GIANT_STOMPING_GROUND
        && vROOM_NO != ROOM49_INSIDE_WELL
        && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
        && vROOM_NO != ROOM63_UP_GOLDEN_EGG_TREE
        && vROOM_NO != ROOM80_IN_MID_AIR
        && vROOM_NO != ROOM48_LEPRECHAUN_HOLE
        && vROOM_NO != ROOM73_CAVE_CEILING_HOLE
        && vROOM_NO != ROOM65_IN_GINGERBREAD_HOUSE
        && vROOM_NO != ROOM12_TOP_OF_WELL
        && vROOM_NO != ROOM14_BASE_GOLDEN_EGG_TREE
        && vROOM_NO != ROOM10_GOAT_PEN_WEST
        && vROOM_NO != ROOM11_GOAT_PEN_EAST
        && vROOM_NO != ROOM51_DRAGONS_LAIR
    ) {
      if (BUILD_TARGET == APPLE_IIGS) {
        set(fEGO_PREOCCUPIED);
      }
      set(fJUMPING);
      stop.motion(oEGO);
      set.cel(oEGO, 0);
      vTEMP_CYCLE_TIME = 2;
      cycle.time(oEGO, vTEMP_CYCLE_TIME);
      set.view(oEGO, 75);
      set(fCYCLE_EGO_ANIMATION);
      end.of.loop(oEGO, fJUMPED);
    }
    else {
      print("There is no reason to do that now.");
    }
  }
  if (fJUMPED) {
    reset(fJUMPING);
    reset(fJUMPED);
    reset(fCYCLE_EGO_ANIMATION);
    vTEMP_CYCLE_TIME_2 = 1;
    cycle.time(oEGO, vTEMP_CYCLE_TIME_2);
    start.motion(oEGO);
    set.view(oEGO, 0);
    if (BUILD_TARGET == APPLE_IIGS) {
      reset(fEGO_PREOCCUPIED);
    }
  }
}
#if BUILD_TARGET == APPLE_IIGS
  if (vAQUATIC_ACTIVITY > AQUATICACTIVITY_NONE) { }
  else
#endif
{
  if (said("duck") || controller(cDUCK)) {
    if (!fEGO_DUCKING
        && !fEGO_CLIMBING_BEANSTALK
        && !fFALLING_TO_EARTH
        && !fEGO_INVISIBLE
        && !fEGO_TINY
        && !fEGO_ON_WATER
        && !fTENSE_SITUATION
        && vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE
        && !fJUMPING
        && !fTHREAT_OF_SORCERER_OR_OGRE
      #if BUILD_TARGET == APPLE_IIGS
        && !fEGO_PREOCCUPIED
      #endif
        && vROOM_NO != ROOM29_SORCERER_STOMPING_GROUND
        && vROOM_NO != ROOM12_TOP_OF_WELL
        && vROOM_NO != ROOM53_KING_EDWARD_THRONE_ROOM
        && vROOM_NO != ROOM58_GIANT_STOMPING_GROUND
        && vROOM_NO != ROOM49_INSIDE_WELL
        && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
        && vROOM_NO != ROOM63_UP_GOLDEN_EGG_TREE
        && vROOM_NO != ROOM80_IN_MID_AIR
        && vROOM_NO != ROOM48_LEPRECHAUN_HOLE
        && vROOM_NO != ROOM73_CAVE_CEILING_HOLE
        && vROOM_NO != ROOM65_IN_GINGERBREAD_HOUSE
        && vROOM_NO != ROOM79_IN_WOODCUTTER_COTTAGE
        && vROOM_NO != ROOM14_BASE_GOLDEN_EGG_TREE
        && vROOM_NO != ROOM10_GOAT_PEN_WEST
        && vROOM_NO != ROOM11_GOAT_PEN_EAST
        && vROOM_NO != ROOM13_WOLF_STOMPING_GROUND
      #if VERSION_MAJOR >= 2
        && vROOM_NO != ROOM51_DRAGONS_LAIR
      #endif
        && !fCARRIED_BY_WITCH
      #if BUILD_TARGET == APPLE_IIGS
        && !fEGO_PLAYING_FIDDLE
      #endif
    ) {
      set(fEGO_DUCKING);
      set.view(oEGO, 87);
      set(fCYCLE_EGO_ANIMATION);
      if (VERSION_MAJOR >= 2) {
        start.cycling(oEGO);
      }
      end.of.loop(oEGO, fDUCKED);
    }
    else if (!fFULLY_DUCKED) {
      print("There is no reason to do that now.");
    }
  }
  if (fDUCKED) {
    stop.cycling(oEGO);
    stop.motion(oEGO);
    set(fFULLY_DUCKED);
    reset(fDUCKED);
    reset(fCYCLE_EGO_ANIMATION);
  }
  if (fFULLY_DUCKED) {
    if (
      said("stand up") || controller(cDUCK)
    #if VERSION_MAJOR >= 2
      || said("jump") || controller(cJUMP)
    #endif
    ) {
      reset(fEGO_DUCKING);
      reset(fFULLY_DUCKED);
      start.motion(oEGO);
      set.view(oEGO, 0);
      start.cycling(oEGO);
    }
  }
}
if (said("thank", "anyword")) {
  print("You're welcome!");
}
if (fCARRYING_DAGGER) {
  if (said("examine", "dagger")) {
    show.obj(INV_DAGGER);
  }
  if (said("get", "dagger")) {
    print("You already have it.");
  }
  if (said("throw", "dagger")) {
    if (vROOM_NO != ROOM18_CLIFFS_LAKE_WEST
      && vROOM_NO != ROOM25_RICKETY_BRIDGE
      && vROOM_NO != ROOM29_SORCERER_STOMPING_GROUND
      && vROOM_NO != ROOM39_WEATHER_BEATEN_BRIDGE
      && vROOM_NO != ROOM41_OPEN_SIDED_BRIDGE
      && vROOM_NO != ROOM46_OGRE_STOMPING_GROUND
      && vROOM_NO != ROOM51_DRAGONS_LAIR
      && vROOM_NO != ROOM58_GIANT_STOMPING_GROUND
    ) {
      print("There is no reason to throw the dagger.");
    }
    else {
      reset(fSAID_ACCEPTED_USER_INPUT);
    }
  }
}
if (fDAGGER_LOST && vROOM_YOU_KILLED_THE_GOAT_IN != vROOM_NO) {
  if (said("anyword", "dagger")) {
    print("You had the dagger and lost it.  Sorry.");
  }
}
if (fDAGGER_LOST && vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
  if (said("anyword", "dagger")) {
    print("You don't want the dagger anymore.");
  }
}
if (fCARROT_EATEN && said("eat", "carrot")) {
  print("You already ate the carrot.");
}
if (fCARRYING_CARROT) {
  if (said("eat", "carrot")) {
    if (!fGOAT_COMPANION) {
      print("Mmmm!  There's nothing like a crisp, fresh carrot.");
      drop("carrot");
      reset(fCARRYING_CARROT);
      reset(fPOINTS_FOR_CARROT);
      vSCORE -= 2;
      set(fCARROT_EATEN);
    }
    if (fGOAT_COMPANION) {
      print("Seeing how much the goat likes that carrot, eating it may be hazardous to your health.");
    }
  }
  if (said("examine", "carrot")) {
    show.obj(INV_CARROT);
  }
  if (said("get", "carrot")) {
    print("You already have a carrot.");
  }
  if (fGOAT_COMPANION) {
#if VERSION_MAJOR >= 2
    if (vROOM_NO != ROOM10_GOAT_PEN_WEST && vROOM_NO != ROOM11_GOAT_PEN_EAST)
#endif
    if (said("feed", "carrot")
      || said("feed", "goat", "carrot")
      || said("feed", "carrot", "goat")
      || said("give", "carrot")
      || said("give", "goat", "carrot")
      || said("give", "carrot", "goat")
    ) {
      print("The goat quickly eats the carrot and disappears.");
      drop("carrot");
      reset(fCARRYING_CARROT);
      reset(fPOINTS_FOR_CARROT);
      vSCORE -= 2;
      if (VERSION_MAJOR < 2) {
        if (vROOM_NO == ROOM10_GOAT_PEN_WEST || vROOM_NO == ROOM11_GOAT_PEN_EAST) {
          set.view(oEGO, 0);
        }
      }
      erase(oGOAT);
      set(fGOAT_IS_GONE);
      reset(fGOAT_COMPANION);
    }
    if (VERSION_MAJOR >= 2) {
      if (vROOM_NO == ROOM10_GOAT_PEN_WEST || vROOM_NO == ROOM11_GOAT_PEN_EAST) {
        if (
          said("feed", "carrot")
          || said("feed", "goat", "carrot")
          || said("feed", "carrot", "goat")
          || said("give", "carrot")
          || said("give", "goat", "carrot")
          || said("give", "carrot", "goat")
        ) {
          print("There is no reason to do that now.");
        }
      }
    }
    if (said("show", "carrot") || said("show", "carrot", "goat") || said("show", "goat", "carrot")) {
      print("It is not nice to tease the goat.");
    }
  }
}
if (fCARRYING_GOLD_WALNUT) {
  if (said("open", "walnut") || said("crack", "walnut")) {
    print("You have already opened the walnut.");
  }
  if (said("eat", "walnut")) {
    print("Don't do that!  This walnut is not edible since it is pure gold.");
  }
  if (said("examine", "walnut")) {
    show.obj(INV_GOLDEN_WALNUT);
  }
}
if (fCARRYING_WALNUT) {
  if (said("eat", "walnut")) {
    print("Ouch!  It would be much easier if you opened the walnut first.");
  }
  if (said("examine", "walnut")) {
    show.obj(INV_WALNUT);
  }
  if (said("open", "walnut") || said("crack", "walnut")) {
    if (vROOM_NO == ROOM30_WALNUT_TREE) {
      sound(SND_GOLDEN_WALNUT, fMISC_END_MARKER);
    }
    print("When you open the walnut, you discover the nut inside is pure gold!");
    reset(fCARRYING_WALNUT);
    set(fCARRYING_GOLD_WALNUT);
    drop("walnut");
    get("gold walnut");
    vSCORE += 3;
  }
}
if (vROOM_NO != ROOM49_INSIDE_WELL && said("dive", "anyword")) {
  if (vAQUATIC_ACTIVITY > AQUATICACTIVITY_NONE) {
    print("The water is very deep here. Without aid, it is impossible to swim under the water for any length of time.");
  }
  else {
    print("You need to be in the water to try that.");
  }
}
if (vSPELL_COUNTDOWN_HI == 0 && vSPELL_COUNTDOWN_LO == 0 && fPROTECTED_BY_SPELL) {
  print("\"Ye best be careful, young Graham.  The mystic protective spell of mine has weakened and departed.\"");
  reset(fPROTECTED_BY_SPELL);
}
else {
  vSPELL_COUNTDOWN_LO--;
  if (vSPELL_COUNTDOWN_LO == 0) {
    vSPELL_COUNTDOWN_HI--;
    if (vSPELL_COUNTDOWN_HI != 0) {
      vSPELL_COUNTDOWN_LO = 250;
    }
  }
}
if (VERSION_MAJOR >= 2) {
  if (fCARRYING_BUCKET && fBUCKET_FILLED && said("examine", "bucket")) {
    show.obj(INV_BUCKET_OF_WATER);
  }
}
if (fCARRYING_BUCKET && fBUCKET_FILLED && !fEGO_ON_WATER) {
  if (said("get", "bucket") || said("get", "water")) {
    print("You already have it.");
  }
  if (
#if VERSION_MAJOR < 2
    said("examine", "bucket") || 
#endif
    said("examine", "water")
  ) {
    show.obj(INV_BUCKET_OF_WATER);
  }
  if (said("drink", "water") || said("get", "drink")) {
    set(fDRANK_WATER);
    print("You drain the bucket of water to quench your thirst.");
    reset(fBUCKET_FILLED);
    drop("water");
    vSCORE -= 2;
    reset(fFILL_BUCKET_POINTS);
  }
}
if (fEGO_ON_WATER && (said("drink", "water") || said("get", "drink"))) {
  print("You open your mouth while swimming and quench your thirst.");
}
if (fCARRYING_BUCKET && !fBUCKET_FILLED) {
  if (fEGO_ON_WATER) {
    if (said("get", "water") || said("fill", "bucket")) {
      reset(fDRANK_WATER);
      print("With difficulty, you fill the bucket while swimming.");
      get("water");
      set(fBUCKET_FILLED);
      if (!fFILL_BUCKET_POINTS) {
        vSCORE += 2;
      }
      set(fFILL_BUCKET_POINTS);
    }
  }
  if (said("examine", "bucket")) {
    show.obj(INV_EMPTY_BUCKET);
  }
}
if (fCARRYING_MUSHROOM) {
  if (said("get", "mushroom")) {
    print("You already have it.");
  }
  if (said("examine", "mushroom")) {
    show.obj(INV_MUSHROOM);
  }
  if (said("eat", "mushroom")) {
    if (fEGO_INVISIBLE) {
      print("You try, but being invisible makes it impossible.");
    }
    if (!fEGO_CLIMBING_BEANSTALK
      && !fFALLING_TO_EARTH
      && !fEGO_INVISIBLE
      && !fON_WELL_ROPE
      && !fIN_WELL_BUCKET
      && !f175_UNKNOWN
      && vROOM_NO != ROOM21_WITCH_STOMPING_GROUND
      && vROOM_NO != ROOM22_CONDOR_AND_CAVE
      && vROOM_NO != ROOM80_IN_MID_AIR
      && vROOM_NO != ROOM65_IN_GINGERBREAD_HOUSE
      && vROOM_NO != ROOM14_BASE_GOLDEN_EGG_TREE
      && vROOM_NO != ROOM63_UP_GOLDEN_EGG_TREE
    #if BUILD_TARGET == APPLE_IIGS
      && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
      && vROOM_NO != ROOM51_DRAGONS_LAIR
      && vROOM_NO != ROOM49_INSIDE_WELL
      && !fEGO_PREOCCUPIED
    #endif
      && vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE
      && vROOM_NO != ROOM12_TOP_OF_WELL
      && vROOM_NO != ROOM73_CAVE_CEILING_HOLE
      && vROOM_NO != ROOM74_CAVE_CORNER
      && vROOM_NO != ROOM75_RAT_STOMPING_GROUND
    ) {
      if (BUILD_TARGET == APPLE_IIGS) {
        stop.sound();
        reset(fPLAYED_SND_FIDDLE_TUNE);
        reset(fEGO_PLAYING_FIDDLE);
        disable.item(cJUMP);
        disable.item(cDUCK);
        reset(fFULLY_DUCKED);
      }
      print("As soon as you swallow the first bite, you have this strange shrinking feeling...  It's true!  You are now no bigger than a mouse!");
      if (BUILD_TARGET == APPLE_IIGS) {
        start.motion(oEGO);
        reset(fCYCLE_EGO_ANIMATION);
        reset(fEGO_DUCKING);
      }
      set.view(oEGO, 60);
      vTINY_COUNTDOWN = 200;
      if (BUILD_TARGET == APPLE_IIGS) {
        start.cycling(oEGO);
      }
      drop("mushroom");
      reset(fCARRYING_MUSHROOM);
      set(fEATEN_MUSHROOM);
      set(fEGO_TINY);
      vSCORE -= 1;
      if (vROOM_NO == ROOM78_IN_CAVE_WITH_TINY_HOLE) {
        vSCORE += 3;
      }
    }
    else {
      print("Now is not a good time to try and eat the mushroom.");
    }
  }
}
if (fEGO_TINY && vTINY_COUNTDOWN > 0) {
  vTINY_COUNTDOWN--;
  if (vTINY_COUNTDOWN == 1 && vROOM_NO != ROOM36_ROCK_WITH_TINY_HOLE && vROOM_NO != ROOM78_IN_CAVE_WITH_TINY_HOLE) {
    set.view(oEGO, 0);
    print("You feel a stretching sensation... Oh, you're back to your original size.");
    reset(fEGO_TINY);
    if (BUILD_TARGET == APPLE_IIGS) {
      enable.item(cJUMP);
      enable.item(cDUCK);
    }
  }
}
if (fCARRYING_RING) {
  if (said("examine", "ring")) {
    show.obj(INV_GOLD_RING);
  }
  if (said("get", "ring")) {
    print("You already have it.");
  }
  if (said("put on", "ring") || said("get", "ring", "pocket") || said("get", "ring", "pocket")) {
    if (fWEARING_RING) {
      print("The ring is already on your finger.");
    }
    if (!fWEARING_RING) {
      set(fWEARING_RING);
      print("You slip the beautiful ring on one of your fingers.");
    }
  }
  if (said("take off", "ring")) {
    if (!fWEARING_RING) {
      print("You are not wearing the ring.");
    }
    if (fWEARING_RING && !fEGO_INVISIBLE) {
      drop(iRING);
      vSCORE -= 3;
      print("I don't know why, but you drop the magic ring.");
      reset(fCARRYING_RING);
      reset(fWEARING_RING);
      set(fLOST_RING);
    }
    if (fWEARING_RING && fEGO_INVISIBLE) {
      drop(iRING);
      vSCORE -= 3;
      print("When the ring disappears, you reappear!");
      set.view(oEGO, 0);
      reset(fCARRYING_RING);
      reset(fEGO_INVISIBLE);
      reset(fWEARING_RING);
      set(fLOST_RING);
      vINVISIBLE_COUNTDOWN_LO = 0;
      vINVISIBLE_COUNTDOWN_HI = 0;
    }
  }
  if (said("rub", "ring")) {
    if (BUILD_TARGET == APPLE_IIGS) {
      if (fEGO_PREOCCUPIED) {
        reset(fSAID_ACCEPTED_USER_INPUT);
      }
    }
    if (!fWEARING_RING) {
      print("The magic ring is still in your pocket.  You can't rub it there.");
    }
    if (fWEARING_RING && fGOAT_COMPANION) {
      print("You rub the magic ring but the goat seems to have a strange effect on it.");
    }
    if (fWEARING_RING && fEGO_INVISIBLE) {
      print("There is no need to rub the ring as you are already invisible.");
    }
    if (fWEARING_RING && !fGOAT_COMPANION && !fEGO_INVISIBLE) {
      if (
        vROOM_NO != ROOM65_IN_GINGERBREAD_HOUSE
        && !fEGO_CLIMBING_BEANSTALK
        && !fFALLING_TO_EARTH
        && vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE
        && vROOM_NO != ROOM63_UP_GOLDEN_EGG_TREE
        && vROOM_NO != ROOM80_IN_MID_AIR
        && vROOM_NO != ROOM49_INSIDE_WELL
        && vROOM_NO != ROOM51_DRAGONS_LAIR
        && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
        && vROOM_NO != ROOM12_TOP_OF_WELL
        && vROOM_NO != ROOM79_IN_WOODCUTTER_COTTAGE
      #if BUILD_TARGET == APPLE_IIGS
        && !fEGO_DUCKING
        && !fEGO_PREOCCUPIED
      #endif
        && !fPLAYER_NOT_IN_CONTROL
        && !fCARRIED_BY_WITCH
        && !fEGO_TINY
      ) {
        if (BUILD_TARGET == APPLE_IIGS) {
          set(fEGO_PREOCCUPIED);
          reset(fJUMPING);
          reset(fJUMPED);
        }
        set.view(oEGO, 110);
        print("When you rub the magic ring, you mysteriously disappear, except for your footprints.");
        set(fEGO_INVISIBLE);
        if (BUILD_TARGET == APPLE_IIGS) {
          disable.item(cJUMP);
          disable.item(cDUCK);
        }
        vINVISIBLE_COUNTDOWN_LO = 250;
        vINVISIBLE_COUNTDOWN_HI = 10;
      }
      if (vROOM_NO == ROOM65_IN_GINGERBREAD_HOUSE
        || fEGO_CLIMBING_BEANSTALK
        || fFALLING_TO_EARTH
        || vAQUATIC_ACTIVITY > AQUATICACTIVITY_NONE
        || vROOM_NO == ROOM63_UP_GOLDEN_EGG_TREE
        || vROOM_NO == ROOM80_IN_MID_AIR
        || vROOM_NO == ROOM49_INSIDE_WELL
        || vROOM_NO == ROOM51_DRAGONS_LAIR
        || vROOM_NO == ROOM52_IN_WELL_UNDERWATER
        || vROOM_NO == ROOM12_TOP_OF_WELL
        || vROOM_NO == ROOM79_IN_WOODCUTTER_COTTAGE
        || fPLAYER_NOT_IN_CONTROL
        || fCARRIED_BY_WITCH
        || fEGO_TINY
      ) {
        print("You're too busy right now to rub the ring.");
      }
    }
  }
}
if (fEGO_INVISIBLE) {
  vINVISIBLE_COUNTDOWN_LO--;
  if (vINVISIBLE_COUNTDOWN_LO == 0) {
    if (vINVISIBLE_COUNTDOWN_HI > 0) {
      vINVISIBLE_COUNTDOWN_HI--;
      vINVISIBLE_COUNTDOWN_LO = 250;
    }
  }
}
if (vINVISIBLE_COUNTDOWN_LO == 1 && vINVISIBLE_COUNTDOWN_HI == 0) {
  vINVISIBLE_COUNTDOWN_LO = 0;
  set.view(oEGO, 0);
  reset(fCARRYING_RING);
  drop(iRING);
  reset(fWEARING_RING);
  set(fLOST_RING);
  reset(fEGO_INVISIBLE);
  print("The spell to make you invisible has expired and you are once again visible to dangerous creatures, but, the ring has now disappeared.");
  if (BUILD_TARGET == APPLE_IIGS) {
    enable.item(cJUMP);
    enable.item(cDUCK);
  }
  vSCORE -= 3;
  if (BUILD_TARGET == APPLE_IIGS) {
    reset(fEGO_PREOCCUPIED);
  }
}
if (fCARRYING_MAGIC_BEANS) {
  #if BUILD_TARGET == APPLE_IIGS
    if (said("take", "magic beans")) {
      print("You already have the beans.");
    }
  #endif
  if (said("examine", "magic beans")) {
    show.obj(INV_MAGIC_BEANS);
  }
  if (said("eat", "magic beans")) {
    print("You're going to be sorry!!  These were magic beans!");
    vSCORE -= 5;
    drop("beans");
    reset(fCARRYING_MAGIC_BEANS);
  }
  if (said("plant", "magic beans")) {
    if (
      vROOM_NO != ROOM6_POUCH_STUMP
    #if BUILD_TARGET != APPLE_IIGS
      && vROOM_NO != ROOM22_CONDOR_AND_CAVE
    #endif
      && vROOM_NO != ROOM31_FREE_BOWL
      && vROOM_NO != ROOM35_DWARF_MEADOW
      && vROOM_NO != ROOM38_WILD_FLOWER_BED
    ) {
      print("That is not a bad idea, but this is not a very suitable place to plant these beans.  Why don't you try it elsewhere?");
    }
    else {
      reset(fSAID_ACCEPTED_USER_INPUT);
    }
  }
}
if (fCARRYING_BOWL) {
  if (vBOWL_STATE == BOWLSTATE_FULL_OF_STEW) {
    if (said("eat", "stew")) {
      if (vROOM_NO == ROOM79_IN_WOODCUTTER_COTTAGE) {
        print("It is very inconsiderate to eat in front of these poor, starving people.  The pains of guilt set in on you.");
      }
      else {
        print("You eat every bite of the delicious stew.");
      }
      vBOWL_STATE = BOWLSTATE_EMPTY;
      reset(fPOINTS_FOR_STEW);
      vSCORE -= 2;
      #if RUBBER_TREE
        vSTEW_PTS_PLUS_2 += 2;
      #endif
      goto(END_OF_BOWL_LOGIC);
    }
    if (said("taste", "stew")) {
      print("Even a small portion of the stew is very appetizing.");
    }
    if (said("examine", "stew")) {
      print("The stew is chock full of vegetables and meat.");
    }
    if (said("examine", "bowl") || said("look inside", "bowl")) {
      show.obj(INV_BOWL_OF_STEW);
    }
    if (
      said("fill")
  #if VERSION_MAJOR < 2
      || said("fill", "bowl")
  #endif
      || said("get", "stew")
  #if VERSION_MAJOR >= 2
      || said("say", "fill")
  #endif
    ) {
      print("You already have a full bowl of stew.");
    }
  }
  if (vBOWL_STATE == BOWLSTATE_EMPTY) {
    if (said("examine", "bowl") || said("look inside", "bowl")) {
      show.obj(INV_EMPTY_BOWL);
    }
    if (said("read", "bowl") || said("read", "word") || said("examine", "word") || said("read", "bottom", "bowl") || said("examine", "bottom", "bowl")) {
      print("The word on the bottom of the bowl says \"fill\".");
      if (!fPOINTS_FOR_STEW) {
        set(fPOINTS_FOR_STEW);
        vSCORE += 1;
        #if RUBBER_TREE
          vSTEW_PTS_PLUS_2 -= 1;
        #endif
      }
    }
    if (
      said("fill")
#if VERSION_MAJOR >= 2
      || said("say", "fill")
#endif
#if VERSION_MAJOR < 2
      || said("fill", "bowl")
#endif
    ) {
      if (vROOM_NO == ROOM79_IN_WOODCUTTER_COTTAGE) {
        print("The bowl fills with the delicious stew, but the woodcutters are unaware of it.");
      }
      else {
        print("After uttering that strange word, the bowl starts quivering and immediately fills up with a delicious, hot stew.");
      }
      vBOWL_STATE = BOWLSTATE_FULL_OF_STEW;
      if (!fPOINTS_FOR_STEW) {
        set(fPOINTS_FOR_STEW);
        vSCORE += 2;
        #if RUBBER_TREE
          vSTEW_PTS_PLUS_2 -= 2;
        #endif
      }
    }
    if (said("eat", "stew") || said("taste", "stew") || said("examine", "stew")) {
      print("You do not have any stew.");
    }
  }
}
END_OF_BOWL_LOGIC:
if (fCARRYING_PEBBLES) {
  if (said("examine", "pebbles")) {
    show.obj(INV_PEBBLES);
  }
  if (said("throw", "pebbles")) {
    print("You throw one of your pebbles.");
  }
  if (said("get", "pebbles")) {
    print("You already have it.");
  }
}
if (fCARRYING_CHEST) {
  if (said("get", "chest")) {
    print("You already have it.");
  }
  if (said("examine", "chest") || said("look inside", "chest")) {
    show.obj(INV_CHEST);
  }
  if (said("open", "chest")) {
    print("You are dazzled by the countless supply of gold coins spilling from the magic chest.  You quickly close the chest.");
  }
  if (said("get", "coins")) {
    print("You are already carrying an endless supply of gold coins in the magic chest.");
  }
  if (said("examine", "coins")) {
    print("You open the chest and admire the gold coins for a moment. You then close the chest.");
  }
}
if (!fGOAT_COMPANION) {
  if (vROOM_YOU_KILLED_THE_GOAT_IN <= ROOM_NULL && vROOM_NO != ROOM10_GOAT_PEN_WEST && vROOM_NO != ROOM11_GOAT_PEN_EAST) {
    if (said("anyword", "goat")) {
      print("There is no goat here.");
    }
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN > ROOM_NULL && vROOM_NO != ROOM10_GOAT_PEN_WEST && vROOM_NO != ROOM11_GOAT_PEN_EAST) {
    if (said("anyword", "goat")) {
      print("You've already killed the goat.");
    }
  }
}
if (fGOAT_COMPANION) {
  if (!fCARRYING_DAGGER && said("murder", "goat")) {
    print("You don't have any way to kill the old goat.");
  }
  if (fCARRYING_DAGGER && said("murder", "goat")) {
    if (
      vROOM_NO != ROOM25_RICKETY_BRIDGE
      && vROOM_NO != ROOM39_WEATHER_BEATEN_BRIDGE
      && vROOM_NO != ROOM41_OPEN_SIDED_BRIDGE
#if BUILD_TARGET == APPLE_IIGS
      && vROOM_NO != ROOM2_CASTLE_WEST
#endif
    ) {
      if (vROOM_NO == ROOM10_GOAT_PEN_WEST || vROOM_NO == ROOM11_GOAT_PEN_EAST) {
        get.posn(oEGO, vTEMP_X, vTEMP_Y);
        print("You hurl the dagger at the old goat... he tries to avoid the flying dagger, but your aim is true... as the dagger strikes, he slumps to the ground... dead.");
        set.view(oEGO, 0);
        set.view(oGOAT, 62);
        position.v(oGOAT, vTEMP_X, vTEMP_Y);
        draw(oGOAT);
        vROOM_YOU_KILLED_THE_GOAT_IN = vROOM_NO;
        vTEMP_STEP_SIZE = 0;
        step.size(oGOAT, vTEMP_STEP_SIZE);
        drop("dagger");
        vSCORE -= 5;
        reset(fCARRYING_DAGGER);
        reset(fGOAT_COMPANION);
        set(fDAGGER_LOST);
      }
      else {
        print("You hurl the dagger at the old goat... he tries to avoid the flying dagger, but your aim is true... as the dagger strikes, he slumps to the ground... dead.");
        erase(oGOAT);
        set.view(oGOAT, 62);
        draw(oGOAT);
        vROOM_YOU_KILLED_THE_GOAT_IN = vROOM_NO;
        vTEMP_STEP_SIZE = 0;
        step.size(oGOAT, vTEMP_STEP_SIZE);
        drop("dagger");
        vSCORE -= 5;
        reset(fCARRYING_DAGGER);
        reset(fGOAT_COMPANION);
        set(fDAGGER_LOST);
      }
    }
  }
  if (said("examine", "goat")) {
    print(m51);
  }
  if (said("talk", "goat")) {
    print("The goat is not much for conversation.");
  }
}
if (fCARRYING_GOLD_KEY) {
  if (said("examine", "gold key")) {
    show.obj(INV_GOLD_KEY);
  }
  if (said("get", "gold key")) {
    print("You already have it.");
  }
}
if (fCARRYING_NOTE) {
  if (said("examine", "note")) {
    show.obj(INV_NOTE);
  }
#if BUILD_TARGET == APPLE_IIGS
  if (said("take", "note")) {
    print("You already have it.");
  }
}
if (fCARRYING_NOTE && !fGAME_OVER) {
#endif
  if (
    said("read", "note")
    || said("read", "message")
#if VERSION_MAJOR >= 2
    || said("read", "message", "note")
#endif
  ) {
    print("The note reads, \"Sometimes it is wise to think backwards.\"");
    if (!fPOINTS_FOR_READING_NOTE) {
      set(fPOINTS_FOR_READING_NOTE);
      vSCORE += 1;
    }
  }
  if (BUILD_TARGET != APPLE_IIGS) {
    if (said("get", "note")) {
      print("You already have it.");
    }
  }
}
if (fCARRYING_SWISS_CHEESE) {
  if (said("get", "swiss cheese")) {
    print("You already have it.");
  }
  if (said("examine", "swiss cheese")) {
    show.obj(INV_SWISS_CHEESE);
  }
#if BUILD_TARGET == APPLE_IIGS
}
if (fCARRYING_SWISS_CHEESE && !fGAME_OVER) {
#endif
  if (said("eat", "swiss cheese")) {
    print("Mmmm!  That Swiss cheese was as delicious as it looked.");
    drop("cheese");
    reset(fCARRYING_SWISS_CHEESE);
    vSCORE -= 2;
  }
}
if (fCARRYING_CLOVER) {
  if (said("examine", "four leaf clover")) {
    show.obj(INV_CLOVER);
  }
  if (said("get", "four leaf clover")) {
    print("You already have it.");
  }
}
if (fCARRYING_SLINGSHOT) {
  if (said("get", "slingshot")) {
    print("You already have it.");
  }
  if (said("examine", "slingshot")) {
    show.obj(INV_SLINGSHOT);
  }
}
if (fCARRYING_SHIELD) {
  if (said("get", "shield")) {
    print("You already have it.");
  }
  if (said("examine", "shield")) {
    show.obj(INV_SHIELD);
  }
}
if (fCARRYING_SCEPTRE) {
  if (said("get", "sceptre")) {
    print("You already have it.");
  }
  if (said("examine", "sceptre")) {
    show.obj(INV_SCEPTRE);
  }
}
if (fCARRYING_DIAMONDS) {
  if (said("get", "pouch of diamonds")) {
    print("You already have it.");
  }
  if (said("examine", "pouch of diamonds") || said("examine", "bag of jewels")) {
    show.obj(INV_POUCH_OF_DIAMONDS);
  }
  if (said("open", "bag of jewels")) {
    print("You have already opened the pouch.");
  }
}
if (fCARRYING_POUCH) {
  if (said("get", "bag of jewels")) {
    print("You already have it.");
  }
  if (said("examine", "bag of jewels")) {
    show.obj(INV_CLOSED_POUCH);
  }
  #if BUILD_TARGET == APPLE_IIGS
  if (!fCARRYING_DIAMONDS && !fGAME_OVER) {
  #else
  if (!fCARRYING_DIAMONDS) {
  #endif
    if (said("open", "bag of jewels") || said("look inside", "bag of jewels")) {
      print("Cautiously, you open the pouch and see many sparkling and flashing diamonds!  Quickly, you close it again so as not to lose any.");
      get("pouch of diamonds");
      drop("pouch");
      vSCORE += 3;
      set(fCARRYING_DIAMONDS);
      reset(fCARRYING_POUCH);
    }
  }
}
if (fCARRYING_GOLDEN_EGG) {
  if (said("get", "golden egg")) {
    print("You already have it.");
  }
  if (said("examine", "golden egg")) {
    show.obj(INV_GOLDEN_EGG);
  }
  if (said("eat", "golden egg")) {
    print("This egg will be tough to crack.");
  }
}
if (BUILD_TARGET == APPLE_IIGS && fPLAYED_SND_FIDDLE_TUNE) {
  stop.sound();
  start.motion(oEGO);
  reset(fPLAYED_SND_FIDDLE_TUNE);
  reset(fEGO_PLAYING_FIDDLE);
  enable.item(cJUMP);
  enable.item(cDUCK);
  reset(fEGO_PREOCCUPIED);
}
if (fCARRYING_FIDDLE) {
  if (said("get", "fiddle")) {
    print("You already have it.");
  }
  if (said("examine", "fiddle")) {
    show.obj(INV_FIDDLE);
  }
  if (
    vROOM_NO != ROOM76_LEPRECHAUN_ANTECHAMBER
    && vROOM_NO != ROOM77_LEPRECHAUN_KING_THRONE_ROOM
#if VERSION_MAJOR >= 2
    && vROOM_NO != ROOM53_KING_EDWARD_THRONE_ROOM
    && vROOM_NO != ROOM65_IN_GINGERBREAD_HOUSE
    && vROOM_NO != ROOM80_IN_MID_AIR
    && vROOM_NO != ROOM49_INSIDE_WELL
    && vROOM_NO != ROOM52_IN_WELL_UNDERWATER
    && vROOM_NO != ROOM12_TOP_OF_WELL
    && vAQUATIC_ACTIVITY != AQUATICACTIVITY_DROWNING
    && vAQUATIC_ACTIVITY != AQUATICACTIVITY_SWIMMING
    && !fPLAYER_NOT_IN_CONTROL
    && !fCARRIED_BY_WITCH
#endif
#if BUILD_TARGET == APPLE_IIGS
    && !fGAME_OVER
#endif
#if VERSION_MAJOR >= 2
    && !fEGO_CLIMBING_BEANSTALK
    && !fON_WELL_ROPE
    && !fFALLING_TO_EARTH
#endif
#if BUILD_TARGET == APPLE_IIGS
    && !fJUMPING
    && !fEGO_TINY
    && !fTENSE_SITUATION
    && vROOM_NO != ROOM58_GIANT_STOMPING_GROUND
#endif
#if VERSION_MAJOR >= 2
    && !f175_UNKNOWN
    && !fEGO_PLAYING_FIDDLE
#endif
#if BUILD_TARGET == APPLE_IIGS
    && !fEGO_PREOCCUPIED
    && !fFULLY_DUCKED
    && !fEGO_ON_WATER
    && !fFALLING
    && !fFALL_DANGER
#endif
  ) {
    if (said("play", "fiddle")) {
      if (BUILD_TARGET == APPLE_IIGS) {
        set(fEGO_PREOCCUPIED);
        disable.item(cJUMP);
        disable.item(cDUCK);
        load.sound(SND_FIDDLE_TUNE);
        set(fEGO_PLAYING_FIDDLE);
        sound(SND_FIDDLE_TUNE, fPLAYED_SND_FIDDLE_TUNE);
      }
      else if (VERSION_MAJOR >= 2) {
        set(fEGO_PLAYING_FIDDLE);
      }
      print("You play a lively, toe-tapping tune on the fiddle.");
      if (BUILD_TARGET == APPLE_IIGS) {
        if (!fEGO_ON_WATER && !fFALLING && !fEGO_TOUCHED_TRIGGER_LINE) {
          stop.motion(oEGO);
        }
        else {
          set(fPLAYED_SND_FIDDLE_TUNE);
        }
      }
      else if (VERSION_MAJOR >= 2) {
        sound(SND_FIDDLE_TUNE, fPLAYED_SND_FIDDLE_TUNE);
      }
      else if (vROOM_NO == ROOM79_IN_WOODCUTTER_COTTAGE) {
        sound(SND_FIDDLE_TUNE, fMISC_END_MARKER);
      }
    }
  }
}
if (BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH) {
  if (fPLAYED_SND_FIDDLE_TUNE) {
    reset(fPLAYED_SND_FIDDLE_TUNE);
    reset(fEGO_PLAYING_FIDDLE);
  }
}
if (fCARRYING_MIRROR) {
  if (said("get", "mirror")) {
    print("You already have it.");
  }
  if (said("examine", "mirror") || said("look inside", "mirror")) {
    show.obj(INV_MIRROR);
  }
}
if (vTREASURE_COUNT == 3) {
  reset(fNO_BUSINESS_WITH_KING);
}
if (fDEBUG_MODE) {
  call(LOGIC99_DEBUG);
}
if (BUILD_TARGET == APPLE_IIGS) {
  if (fGAME_OVER) {
    if (!fGAME_OVER_PREPARATION) {
      load.logics(LOGIC100_GAME_OVER);
      load.sound(SND_GAME_OVER);
      load.view(114);
      vGAME_OVER_MESSAGE_COUNTDOWN = 40;
      set(fGAME_OVER_PREPARATION);
    }
    program.control();
    stop.motion(oEGO);
    disable.item(cSAVE);
    disable.item(cPAUSE);
    disable.item(cSWIM);
    disable.item(cJUMP);
    disable.item(cDUCK);
    call(LOGIC100_GAME_OVER);
    if (vROOM_NO == ROOM46_OGRE_STOMPING_GROUND || vROOM_NO == ROOM13_WOLF_STOMPING_GROUND) {
      call.v(vROOM_NO);
    }
    return;
  }
}
call.v(vROOM_NO);
#if RUBBER_TREE
  if (fUNDO) {
    print("Sorry! UNDO cannot be used to reverse any kind of action, only a fatal (or potentially fatal) misstep.");
    reset(fUNDO);
  }
#endif
call(LOGIC102_DEFAULT_RESPONSES);
if (BUILD_TARGET != AMIGA) {
  get.posn(oEGO, vSAVED_EGO_X, vSAVED_EGO_Y);
  vSAVED_EGO_DIR = vEGO_DIR;
}
if (fSTORE_STATE) {
  reset(fSTORE_STATE);
  vSTATE_FLAGS = 0;
  if (fCARRYING_WALNUT) vSTATE_FLAGS += STATEFLAGS_CLOSED_WALNUT;
  if (fCARRYING_POUCH) vSTATE_FLAGS += STATEFLAGS_CLOSED_POUCH;
  if (!fPOINTS_FOR_READING_NOTE) vSTATE_FLAGS += STATEFLAGS_UNREAD_NOTE;
  if (fCARRYING_SWISS_CHEESE) vSTATE_FLAGS += STATEFLAGS_UNEATEN_CHEESE;
  if (fCARRYING_MAGIC_BEANS) vSTATE_FLAGS += STATEFLAGS_UNEATEN_BEANS;
  if (fCARRYING_CARROT) vSTATE_FLAGS += STATEFLAGS_UNEATEN_CARROT;
  if (fBUCKET_FILLED) vSTATE_FLAGS += STATEFLAGS_UNDRUNK_WATER;
  if (vBOWL_STATE == BOWLSTATE_FULL_OF_STEW) set(fSTATE_BOWL_FULL); else reset(fSTATE_BOWL_FULL);
  vSTEW_PTS_PLUS_2 = 2;
  if (fEGO_INVISIBLE) {
    vSTATE_INVISCD_HI = vINVISIBLE_COUNTDOWN_HI;
    vSTATE_INVISCD_LO = vINVISIBLE_COUNTDOWN_LO;
  }
  else if (fWEARING_RING) {
    vSTATE_INVISCD_HI = 102;
  }
  else if (fCARRYING_RING) {
    vSTATE_INVISCD_HI = 103;
  }
  else {
    vSTATE_INVISCD_HI = 101;
  }
  if (fPROTECTED_BY_SPELL) {
    vSTATE_PROTECTCD_HI = vSPELL_COUNTDOWN_HI;
    vSTATE_PROTECTCD_LO = vSPELL_COUNTDOWN_LO;
  }
  else {
    vSTATE_PROTECTCD_HI = 101;
  }
  if (fEGO_TINY) {
    vSTATE_TINYCD = vTINY_COUNTDOWN;
  }
  else if (fCARRYING_MUSHROOM) {
    vSTATE_TINYCD = 202;
  }
  else {
    vSTATE_TINYCD = 201;
  }
}
if (fRESTORE_STATE) {
  reset(fRESTORE_STATE);
  if (vSTATE_FLAGS > 0) {
    vTEMP_RESULT = vSTATE_FLAGS;
    if (vSTATE_FLAGS >= STATEFLAGS_UNDRUNK_WATER) {
      vSTATE_FLAGS -= STATEFLAGS_UNDRUNK_WATER;
      if (!fBUCKET_FILLED) {
        set(fBUCKET_FILLED);
        vSCORE += 2;
        get("water");
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_UNEATEN_CARROT) {
      vSTATE_FLAGS -= STATEFLAGS_UNEATEN_CARROT;
      if (!fCARRYING_CARROT) {
        set(fCARRYING_CARROT);
        get("carrot");
        if (!fPOINTS_FOR_CARROT) {
          set(fPOINTS_FOR_CARROT);
          vSCORE += 2;
        }
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_UNEATEN_BEANS) {
      vSTATE_FLAGS -= STATEFLAGS_UNEATEN_BEANS;
      if (!fCARRYING_MAGIC_BEANS) {
        set(fCARRYING_MAGIC_BEANS);
        get("beans");
        vSCORE += 5;
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_UNEATEN_CHEESE) {
      vSTATE_FLAGS -= STATEFLAGS_UNEATEN_CHEESE;
      if (!fCARRYING_SWISS_CHEESE) {
        set(fCARRYING_SWISS_CHEESE);
        get("cheese");
        vSCORE += 2;
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_UNREAD_NOTE) {
      vSTATE_FLAGS -= STATEFLAGS_UNREAD_NOTE;
      if (fPOINTS_FOR_READING_NOTE) {
        reset(fPOINTS_FOR_READING_NOTE);
        vSCORE -= 1;
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_CLOSED_POUCH) {
      vSTATE_FLAGS -= STATEFLAGS_CLOSED_POUCH;
      if (fCARRYING_DIAMONDS) {
        reset(fCARRYING_DIAMONDS);
        set(fCARRYING_POUCH);
        drop("pouch of diamonds");
        get("pouch");
        vSCORE -= 3;
      }
    }
    if (vSTATE_FLAGS >= STATEFLAGS_CLOSED_WALNUT) {
      vSTATE_FLAGS -= STATEFLAGS_CLOSED_WALNUT;
      if (fCARRYING_GOLD_WALNUT) {
        reset(fCARRYING_GOLD_WALNUT);
        set(fCARRYING_WALNUT);
        drop("gold walnut");
        get("walnut");
        vSCORE -= 3;
      }
    }
    vSTATE_FLAGS = vTEMP_RESULT;
  }
  if (fSTATE_BOWL_FULL) {
    vBOWL_STATE = BOWLSTATE_FULL_OF_STEW;
  }
  else {
    vBOWL_STATE = BOWLSTATE_EMPTY;
  }
  vSCORE += vSTEW_PTS_PLUS_2;
  vSCORE -= 2;
  vSTEW_PTS_PLUS_2 = 2;
  if (vSTATE_PROTECTCD_HI != 101) {
    vSPELL_COUNTDOWN_HI = vSTATE_PROTECTCD_HI;
    vSPELL_COUNTDOWN_LO = vSTATE_PROTECTCD_LO;
    set(fPROTECTED_BY_SPELL);
  }
  if (vSTATE_INVISCD_HI != 101) {
    if (!fCARRYING_RING) {
      set(fCARRYING_RING);
      get(iRING);
      vSCORE += 3;
      reset(fLOST_RING);
    }
    if (vSTATE_INVISCD_HI == 102) {
      reset(fWEARING_RING);
      reset(fEGO_INVISIBLE);
    }
    else {
      set(fWEARING_RING);
      if (vSTATE_INVISCD_HI == 103) {
        reset(fEGO_INVISIBLE);
      }
      else {
        set(fEGO_INVISIBLE);
        vINVISIBLE_COUNTDOWN_HI = vSTATE_INVISCD_HI;
        vINVISIBLE_COUNTDOWN_LO = vSTATE_INVISCD_LO;
        set.view(oEGO, 110);
      }
    }
  }
  if (vSTATE_TINYCD != 201) {
    if (vSTATE_TINYCD == 202) {
      if (fEGO_TINY) {
        reset(fEGO_TINY);
        set.view(oEGO, 0);
      }
      if (!fCARRYING_MUSHROOM) {
        set(fCARRYING_MUSHROOM);
        get("mushroom");
        vSCORE += 1;
      }
    }
    else {
      if (!fEGO_TINY) {
        set(fEGO_TINY);
        set.view(oEGO, 60);
      }
      vTINY_COUNTDOWN = vSTATE_TINYCD;
    }
  }
}
return;

#message 1 "You splash around in the water for a while but seem  to have forgotten how to swim. Unfortunately, you drown."
#message 2 "Where do you think it could be?"
#message 3 "The goat is not much for conversation."
#message 4 "\"Ye best be careful, young Graham.  The mystic protective spell of mine has weakened and departed.\""
#message 5 "KQ1"
#message 6 "You feel a stretching sensation... Oh, you're back to your original size."
#message 7 "You're too busy right now to rub the ring."
#message 8 "There is no reason to do that now."
#message 9 "You had the dagger and lost it.  Sorry."
#message 10 "You already have it."
#message 11 "As soon as you swallow the first bite, you have this strange shrinking feeling...  It's true!  You are now no bigger than a mouse!"
#message 12 "You need to be closer to the water if you want to swim."
#message 13 "You drain the bucket of water to quench your thirst."
#message 14 "You open your mouth while swimming and quench your thirst."
#message 15 "With difficulty, you fill the bucket while swimming."
#message 16 "Mmmm!  There's nothing like a crisp, fresh carrot."
#if BUILD_TARGET == AMIGA
  #message 17 "VERSION 1.0U"
#elif BUILD_TARGET == ATARI_ST
  #message 17 "VERSION 1.0V"
#elif BUILD_TARGET == DOS
  #message 17 "VERSION 2.0F"
#elif BUILD_TARGET == MACINTOSH
  #message 17 "VERSION 2.0C"
#elif BUILD_TARGET == APPLE_IIGS
  #message 17 "  King's Quest \n\nApple IIgs Version \n\n    1.0S-88223 \n"
#endif
#message 18 "You already have a carrot."
#message 19 "You already ate the carrot."
#message 20 "The goat quickly eats the carrot and disappears."
#message 21 "Now is not a good time to try and eat the mushroom."
#message 22 "You do not have a bowl."
#message 23 "It is not nice to tease the goat."
#message 24 "You are not carrying it."
#message 25 "There is no reason to drop it."
#message 26 "You are already swimming."
#message 27 "The water is very deep here. Without aid, it is impossible to swim under the water for any length of time."
#message 28 "You need to be in the water to try that."
#message 29 "You need to be in water to swim."
#message 30 ">"
#message 32 "You do not have any stew."
#message 33 "The stew is chock full of vegetables and meat."
#message 34 "You already have a full bowl of stew."
#message 35 "The bowl fills with the delicious stew, but the woodcutters are unaware of it."
#message 36 "After uttering that strange word, the bowl starts quivering and immediately fills up with a delicious, hot stew."
#message 37 "It is very inconsiderate to eat in front of these poor, starving people.  The pains of guilt set in on you."
#message 38 "You eat every bite of the delicious stew."
#message 39 "OK."
#message 40 "You're welcome!"
#message 42 "You are dazzled by the countless supply of gold coins spilling from the magic chest.  You quickly close the chest."
#message 43 "You are already carrying an endless supply of gold coins in the magic chest."
#message 44 "You open the chest and admire the gold coins for a moment. You then close the chest."
#message 45 "There is no reason to throw the dagger."
#message 46 "You don't want the dagger anymore."
#message 47 "You hurl the dagger at the old goat... he tries to avoid the flying dagger, but your aim is true... as the dagger strikes, he slumps to the ground... dead."
#message 48 "You don't have any way to kill the old goat."
#message 49 "There is no goat here."
#message 50 "You've already killed the goat."
#if VERSION_MAJOR >= 2
  #message 51 "The old goat, trotting briskly, seems happy to be with you."
#else
  #message 51 "The old goat is trotting briskly by your side."
#endif
#message 52 "Seeing how much the goat likes that carrot, eating it may be hazardous to your health."
#message 53 "You have already opened the walnut."
#message 54 "When you open the walnut, you discover the nut inside is pure gold!"
#message 55 "Ouch!  It would be much easier if you opened the walnut first."
#message 56 "Don't do that!  This walnut is not edible since it is pure gold."
#message 60 "The note reads, \"Sometimes it is wise to think backwards.\""
#message 63 "Mmmm!  That Swiss cheese was as delicious as it looked."
#message 65 "The word on the bottom of the bowl says \"fill\"."
#message 66 "Even a small portion of the stew is very appetizing."
#message 68 "You throw one of your pebbles."
#message 73 "This egg will be tough to crack."
#message 75 "You play a lively, toe-tapping tune on the fiddle."
#message 78 "You have already opened the pouch."
#message 79 "Cautiously, you open the pouch and see many sparkling and flashing diamonds!  Quickly, you close it again so as not to lose any."
#message 81 "You try, but being invisible makes it impossible."
#message 84 "You're going to be sorry!!  These were magic beans!"
#message 85 "That is not a bad idea, but this is not a very suitable place to plant these beans.  Why don't you try it elsewhere?"
#message 88 "The ring is already on your finger."
#message 89 "You slip the beautiful ring on one of your fingers."
#message 90 "I don't know why, but you drop the magic ring."
#message 91 "Nothing happens."
#message 92 "When the ring disappears, you reappear!"
#message 93 "When you rub the magic ring, you mysteriously disappear, except for your footprints."
#message 94 "You rub the magic ring but the goat seems to have a strange effect on it."
#message 95 "With disappointment, you see that nothing happens."
#message 96 "The magic ring is still in your pocket.  You can't rub it there."
#message 97 "The spell to make you invisible has expired and you are once again visible to dangerous creatures, but, the ring has now disappeared."
#message 98 "You will have to find it before you can get it."
#message 99 "You do not have it."
#message 100 "_"
#message 101 "You are not wearing the ring."
#message 102 "There is no need to rub the ring as you are already invisible."
#if BUILD_TARGET == AMIGA
  #message 103 "        King's Quest\n             by\n      Roberta Williams\n\n    Original version by\nArthur Abraham, Chuck Tingley\n        Ken McNeill\n\n        Graphics by\n Doug McNeill, Greg Rowland\n\n      This version by\n       Sol Ackerman\n\n(C) 1986 Sierra On-Line, Inc."
#elif BUILD_TARGET != ATARI_ST
  #message 150 "Info"
  #if BUILD_TARGET == MACINTOSH
    #message 151 "Help     "
  #else
    #message 151 "Help    "
  #endif
  #message 152 "About KQ"
  #message 153 "File"
  #if BUILD_TARGET == APPLE_IIGS
    #message 154 "Save Game      <Shift-5>"
    #message 155 "Restore Game   <Shift-7>"
  #else
    #message 154 "Save Game      <F5>"
    #message 155 "Restore Game   <F7>"
  #endif
  #message 156 "-------------------"
  #if BUILD_TARGET == APPLE_IIGS
    #message 157 "Restart Game   <Shift-9>"
    #message 158 "Pause           <Esc>"
    #message 159 "Quit        <Option-Z>"
  #else
    #message 157 "Restart Game   <F9>"
    #if BUILD_TARGET == MACINTOSH
      #message 158 "Pause         <Esc>"
    #else
      #message 158 "Pause             <Esc>"
    #endif
    #message 159 "Quit        <Alt Z>"
  #endif
  #message 160 "Game"
  #message 161 "Inventory     <Tab>"
  #if BUILD_TARGET == APPLE_IIGS
    #message 162 "See Object     <Shift-4>"
    #message 163 "Sound On/Off   <Shift-2>"
  #else
    #message 162 "See Object     <F4>"
    #if BUILD_TARGET == MACINTOSH
      #message 163 "Toggle Sound   <F2>"
    #else
      #message 163 "Sound On/Off       <F2>"
    #endif
  #endif
  #if BUILD_TARGET == MACINTOSH
    #message 164 "Action"
  #else
    #message 164 "Action "
  #endif
  #message 165 "Swim  ="
  #message 166 "Special"
  #if BUILD_TARGET == MACINTOSH
    #message 167 "Toggle RGB     <Ctrl R>"
  #else
    #message 167 "Graphics Mode  <Ctrl R>"
  #endif
  #message 168 "Reset Joystick <Ctrl J>"
  #message 169 "Speed"
  #message 170 "Normal"
  #message 171 "Fast  "
  #message 172 "Slow  "
  #if BUILD_TARGET == APPLE_IIGS
    #message 173 "   King's Quest\n\nby Roberta Williams.\n"
  #else
    #message 173 "King's Quest was designed by Roberta Williams."
  #endif
  #message 174 "Jump  0"
  #message 175 "Duck  -"
  #if BUILD_TARGET != MACINTOSH
    #message 176 "Joystick/Mouse <Ctrl J>"
  #endif
#endif
#if BUILD_TARGET == APPLE_IIGS
  #message 197 "You already have the beans."
  #message 198 "PQ"
  #message 199 "KQ"
#endif