#include "vars.txt"

#define rvCOUNTDOWN_LO rv0
#define rvCOUNTDOWN_HI rv1

#if RUBBER_TREE
if (fUNDO && fGAME_OVER) {
  reset(fUNDO);
  reset(fGAME_OVER);
  reset(fGAME_OVER_PREPARATION);
  if (vRESTORE_ROOM != ROOM78_IN_CAVE_WITH_TINY_HOLE) {
    new.room.v(vRESTORE_ROOM);
    return;
  }
  set(fRESTORE_STATE);
  erase(oEGO);
  position(oEGO, 146, 131);
  if (VERSION_MAJOR >= 2) {
    move.obj(oEGO, 145, 130, 0, fMISC_END_MARKER);
  }
  else {
    move.obj(oEGO, 146, 131, 0, fMISC_END_MARKER);
  }
  draw(oEGO);
  return;
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
  if (vRESTORE_ROOM != ROOM22_CONDOR_AND_CAVE && fCARRYING_MUSHROOM) {
    vRESTORE_ROOM = ROOM78_IN_CAVE_WITH_TINY_HOLE;
    set(fSTORE_STATE);
  }
  #endif
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (vPREV_ROOM_NO == ROOM36_ROCK_WITH_TINY_HOLE) {
    set.view(oEGO, 60);
    position(oEGO, 3, 81);
  }
  if (vPREV_ROOM_NO == ROOM77_LEPRECHAUN_KING_THRONE_ROOM) {
    position(oEGO, 146, 131);
    if (VERSION_MAJOR >= 2) {
      move.obj(oEGO, 145, 130, 0, fMISC_END_MARKER);
    }
    else {
      move.obj(oEGO, 146, 131, 0, fMISC_END_MARKER);
    }
  }
  if (VERSION_MAJOR >= 2 && !fCARRYING_MUSHROOM) {
    rvCOUNTDOWN_LO = 250;
    rvCOUNTDOWN_HI = 4;
  }
  draw(oEGO);
  show.pic();
}
if (VERSION_MAJOR >= 2) {
  if (!fCARRYING_MUSHROOM) {
    rvCOUNTDOWN_LO--;
    if (rvCOUNTDOWN_LO == 0) {
      if (rvCOUNTDOWN_HI > 0) {
        rvCOUNTDOWN_HI--;
        rvCOUNTDOWN_LO = 250;
      }
    }
  }
  if (rvCOUNTDOWN_LO == 1 && rvCOUNTDOWN_HI == 0) {
    stop.cycling(oEGO);
    print("You seem to be stuck in these underground caverns.  As you have no way of leaving, it looks like you'll spend the rest of your life here." );
    set(fGAME_OVER);
  }
}
if (fEGO_TINY) {
  ignore.blocks(oEGO);
}
else {
  observe.blocks(oEGO);
}
if (vTINY_COUNTDOWN == 1 && fEGO_TINY) {
  if (posn(oEGO, 1, 75, 12, 82)) {
    #if !RUBBER_TREE
    reset(fEGO_TINY);
    #endif
    print("You feel a stretching sensation... Oh, you're back to your original size.  You can't get out of the small cave!");
    set(fGAME_OVER);
  }
  else {
    set.view(oEGO, 0);
    print("You feel a stretching sensation... Oh, you're back to your original size.");
    if (BUILD_TARGET == APPLE_IIGS) {
      rvCOUNTDOWN_LO = 40;
    }
    reset(fEGO_TINY);
  }
}
if (said("examine", "stairway")) {
  print("The stairs are very steep and narrow.");
}
if (said("examine", "hole") || said("look inside", "hole")) {
  print("The small hole leads to the outside world.");
}
if (said("go", "hole") || said("go", "hole")) {
  if (fEGO_TINY) {
    print("Go ahead.  Walk right in.");
  }
  else {
    print("You are much too big to fit through that small hole.");
    #if RUBBER_TREE
    if (!fCARRYING_MUSHROOM) {
      stop.cycling(oEGO);
      print("You seem to be stuck in these underground caverns.  As you have no way of leaving, it looks like you'll spend the rest of your life here." );
      set(fGAME_OVER);
    }
    #endif
  }
}
if (said("examine", "cave") || said("examine", "room")) {
  print("This is a small cave with stairs on one side and a very small hole on the other side.");
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM77_LEPRECHAUN_KING_THRONE_ROOM);
}
if (posn(oEGO, 0, 0, 1, 167)) {
  new.room(ROOM36_ROCK_WITH_TINY_HOLE);
}
return;

#message 1 "You feel a stretching sensation... Oh, you're back to your original size.  You can't get out of the small cave!"
#message 2 "The stairs are very steep and narrow."
#message 3 "The small hole leads to the outside world."
#message 4 "You are much too big to fit through that small hole."
#message 5 "You feel a stretching sensation... Oh, you're back to your original size."
#message 6 "Go ahead.  Walk right in."
#message 7 "This is a small cave with stairs on one side and a very small hole on the other side."
#if VERSION_MAJOR >= 2
  #message 8 "You seem to be stuck in these underground caverns.  As you have no way of leaving, it looks like you'll spend the rest of your life here." 
#endif
