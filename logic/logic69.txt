#include "vars.txt"

#define rfFALLEN_OFF_STEPS rf0
#define rfFALLING_OFF_STEPS rf1

#define rvTEMP_PRIORITY rv0

#if BUILD_TARGET == APPLE_IIGS
  #define _fFALLING fFALLING
#else
  #define _fFALLING rfFALLING_OFF_STEPS
#endif

#if RUBBER_TREE
if (fUNDO && (_fFALLING || fGAME_OVER)) {
  set(fRESTORE_STATE);
  reset(fUNDO);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  vEGO_DIR = DIR_NONE;
  set.view(oEGO, 0);
  reset(fCYCLE_EGO_ANIMATION);
  reset(_fFALLING);
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  if (vPREV_ROOM_NO == ROOM68_MOUNTAIN_STAIRWAY_NEAR_TOP) {
    set.loop(oEGO, 1);
  }
  else {
    set.loop(oEGO, 0);
  }
  draw(oEGO);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.sound(SND_FALLING);
  if (vPREV_ROOM_NO == ROOM59_CLOUDS_MOUNTAIN_CAVE) {
    position(oEGO, 18, 97);
  }
  if (vPREV_ROOM_NO == ROOM68_MOUNTAIN_STAIRWAY_NEAR_TOP) {
    position(oEGO, 144, 155);
    set.loop(oEGO, 1);
    if (VERSION_MAJOR >= 2) {
      move.obj(oEGO, 143, 154, 0, fMISC_END_MARKER);
    }
    else {
      move.obj(oEGO, 144, 155, 0, fMISC_END_MARKER);
    }
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "stairway") || said("examine", "room")) {
  print("The stairs descend into the lower reaches of the mountain.");
}
if (said("examine", "opening")) {
  print("You can see sky, light, and smell clouds through the opening.");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !_fFALLING) {
  #if RUBBER_TREE
    set(fSTORE_STATE);
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    if (vRESTORE_X <= 81) {
      vRESTORE_X = 82;
    }
    vRESTORE_Y -= 7;
    vRESTORE_DIR = vEGO_DIR;
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    prevent.input();
    set(fEGO_PREOCCUPIED);
  }
  if (VERSION_MAJOR >= 2) {
    set(fTENSE_SITUATION);
    set(fTHREAT_OF_SORCERER_OR_OGRE);
  }
  set(_fFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvTEMP_PRIORITY);
  set.priority.v(oEGO, rvTEMP_PRIORITY);
  print("Oh dear!  With a slip of the foot, you fall head over heels down the long stairway.");
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFALLEN_OFF_STEPS);
  sound(SND_FALLING, fSOUND_END_MARKER);
}
if (rfFALLEN_OFF_STEPS) {
  reset(rfFALLEN_OFF_STEPS);
  erase(oEGO);
  set(fGAME_OVER);
  if (BUILD_TARGET == APPLE_IIGS) {
    accept.input();
  }
}
if (posn(oEGO, 0, 0, 1, 167)) {
  new.room(ROOM59_CLOUDS_MOUNTAIN_CAVE);
}
if (posn(oEGO, 138, 156, 155, 157)) {
  new.room(ROOM68_MOUNTAIN_STAIRWAY_NEAR_TOP);
}
if (BUILD_TARGET == APPLE_IIGS) {
  set(fFALL_DANGER);
}
return;

#message 1 "The stairs descend into the lower reaches of the mountain."
#message 2 "You can see sky, light, and smell clouds through the opening."
#message 3 "Oh dear!  With a slip of the foot, you fall head over heels down the long stairway."
