#include "vars.txt"

#define rfFALLEN_THROUGH_THE_CLOUDS rf0
#define rfHIDE_EGO rf1

#if RUBBER_TREE
if (fUNDO && (fGAME_OVER || fSCRIPTED_MOTION)) {
  reset(fSCRIPTED_MOTION);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  stop.motion(oEGO);  
  reset(fCYCLE_EGO_ANIMATION);
  reset(rfFALLEN_THROUGH_THE_CLOUDS);
  reset(rfHIDE_EGO);
  reset(fSOUND_END_MARKER);
  stop.cycling(oEGO);
  set.view(oEGO, 0);
  vEGO_DIR = DIR_NONE;
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  release.priority(oEGO);
  draw(oEGO);
  reset(fUNDO);
  reset(fFALLING);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  ignore.horizon(oEGO);
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.sound(SND_FALLING);
  load.view(105);
  if (vPREV_ROOM_NO == ROOM62_CLOUDLANDS_SOUTHEAST) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_X > 152) {
      position(oEGO, 150, 164);
    }
  }
  if (vPREV_ROOM_NO == ROOM69_MOUNTAIN_STAIRWAY_TOP) {
    position(oEGO, 116, 110);
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "cave")) {
  print("The dark cave burrows its way into a steep mountain.");
}
if (said("climb", "mountains")) {
  print("The mountain is too steep to climb.");
}
if (said("examine", "mountains")) {
  print("You are near the top of a very high mountain.  It steeply descends toward a valley floor.");
}
if (said("examine", "clouds") || said("examine", "room")) {
  print("You are in the Land of the Clouds.  It is rumored that a giant lives here.");
}
if (said("examine", "tree")) {
  print("These are the most unusual looking trees.");
}
if (said("examine", "flowers")) {
  print("These are unusual looking flowers.");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !fSCRIPTED_MOTION && posn(oEGO, 0, 84, 159, 167)) {
  #if RUBBER_TREE
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    vRESTORE_X -= 5;
    vRESTORE_DIR = vEGO_DIR;
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    prevent.input();
    set(fFALLING);
    set(fEGO_PREOCCUPIED);
  }
  set(fSCRIPTED_MOTION);
  stop.cycling(oEGO);
  sound(SND_FALLING, fSOUND_END_MARKER);
  move.obj(oEGO, 153, 166, 5, rfHIDE_EGO);
}
if (fEGO_TOUCHED_TRIGGER_LINE && posn(oEGO, 0, 0, 159, 83)) {
  #if RUBBER_TREE
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    if (vRESTORE_X > 100) {
      vRESTORE_X = 100;
    }
    if (vRESTORE_Y < 43) {
      vRESTORE_Y = 43;
    }
    vRESTORE_DIR = vEGO_DIR;
    set(fSCRIPTED_MOTION);
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    prevent.input();
    set(fEGO_PREOCCUPIED);
    set(fFALLING);
  }
  reset(fEGO_TOUCHED_TRIGGER_LINE);
  program.control();
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  sound(SND_FALLING, fSOUND_END_MARKER);
  erase(oEGO);
}
if (rfHIDE_EGO) {
  reset(rfHIDE_EGO);
  #if RUBBER_TREE
  if (fSCRIPTED_MOTION)
  #endif
  {
    erase(oEGO);
  }
}
if (fSOUND_END_MARKER) {
  reset(fSOUND_END_MARKER);
  set(rfFALLEN_THROUGH_THE_CLOUDS);
}
if (rfFALLEN_THROUGH_THE_CLOUDS) {
  reset(rfFALLEN_THROUGH_THE_CLOUDS);
  #if RUBBER_TREE
  if (fSCRIPTED_MOTION)
  #endif
  {
  print("Oops, you've missed the edge and fallen through the  clouds.");
  set(fGAME_OVER);
  if (BUILD_TARGET == APPLE_IIGS) {
    accept.input();
  }
  }
}
if (posn(oEGO, 135, 94, 136, 120)) {
  new.room(ROOM69_MOUNTAIN_STAIRWAY_TOP);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM58_GIANT_STOMPING_GROUND);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  if (vTEMP_X < 153) {
    new.room(ROOM62_CLOUDLANDS_SOUTHEAST);
  }
}
if (BUILD_TARGET == APPLE_IIGS) {
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  if (vTEMP_Y < 50 || posn(oEGO, 118, 120, 154, 167) || posn(oEGO, 96, 31, 131, 92)) {
    set(fFALL_DANGER);
  }
  else {
    reset(fFALL_DANGER);
  }
}
return;

#message 1 "The dark cave burrows its way into a steep mountain."
#message 2 "The mountain is too steep to climb."
#message 3 "You are near the top of a very high mountain.  It steeply descends toward a valley floor."
#message 4 "You are in the Land of the Clouds.  It is rumored that a giant lives here."
#message 5 "Oops, you've missed the edge and fallen through the  clouds."
#message 6 "These are the most unusual looking trees."
#message 7 "These are unusual looking flowers."
