#include "vars.txt"

#define rfFALLING_SOUND_PLAYED rf0
#define rfHIDE_EGO rf1

#define rvTEMP_PRIORITY rv0

#if RUBBER_TREE
#define rvRESTORE_EGO_X rv1
#define rvRESTORE_EGO_Y rv2
#define rvRESTORE_EGO_DIR rv3
#endif

#if RUBBER_TREE
if (fUNDO && fGAME_OVER) {
  reset(fSCRIPTED_MOTION);
  stop.sound();
  erase(oEGO);
  position.v(oEGO, rvRESTORE_EGO_X, rvRESTORE_EGO_Y);
  reset(fCYCLE_EGO_ANIMATION);
  stop.cycling(oEGO);
  set.view(oEGO, 0);
  set.dir(oEGO, rvRESTORE_EGO_DIR);
  release.priority(oEGO);
  draw(oEGO);
  reset(fUNDO);
  reset(fGAME_OVER);
  reset(fGAME_OVER_PREPARATION);
  reset(fFALLING);
  player.control();
  return;
}
#endif
if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.sound(SND_FALLING);
  set.horizon(45);
  draw(oEGO);
  show.pic();
}
if (said("examine", "room") || said("examine", "clouds")) {
  print("You are in the Land of the Clouds.  It is rumored that a giant lives up here.");
}
if (said("examine", "tree") || said("examine", "flowers")) {
  print("This is very strange vegetation.");
}
if (fEGO_TOUCHED_TRIGGER_LINE) {
  #if RUBBER_TREE
    get.posn(oEGO, rvRESTORE_EGO_X, rvRESTORE_EGO_Y);
    if (rvRESTORE_EGO_X >= 22) {
      rvRESTORE_EGO_Y -= 5;
    }
    else {
      rvRESTORE_EGO_X += 5;
    }
    rvRESTORE_EGO_DIR = vEGO_DIR;
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    prevent.input();
    set(fEGO_PREOCCUPIED);
    set(fFALLING);
  }
  set(fSCRIPTED_MOTION);
  program.control();
  stop.cycling(oEGO);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvTEMP_PRIORITY);
  set.priority.v(oEGO, rvTEMP_PRIORITY);
  vTEMP_Y += 8;
  vTEMP_STEP = 4;
  sound(SND_FALLING, fSOUND_END_MARKER);
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfHIDE_EGO);
}
if (rfHIDE_EGO) {
  reset(rfHIDE_EGO);
  erase(oEGO);
}
if (fSOUND_END_MARKER) {
  reset(fSOUND_END_MARKER);
  set(rfFALLING_SOUND_PLAYED);
}
if (rfFALLING_SOUND_PLAYED) {
  reset(rfFALLING_SOUND_PLAYED);
  print("You should know that it is not possible to walk on clouds!");
  set(fGAME_OVER);
  if (BUILD_TARGET == APPLE_IIGS) {
    accept.input();
  }
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM57_CLOUDLANDS_NORTHWEST);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM61_CLOUDLANDS_SOUTH);
}
if (BUILD_TARGET == APPLE_IIGS) {
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  if (vTEMP_Y > 150 || vTEMP_X < 11 || posn(oEGO, 0, 80, 40, 167)) {
    set(fFALL_DANGER);
  }
  else {
    reset(fFALL_DANGER);
  }
}
return;

#message 1 "You are in the Land of the Clouds.  It is rumored that a giant lives up here."
#message 2 "You should know that it is not possible to walk on clouds!"
#message 3 "This is very strange vegetation."
