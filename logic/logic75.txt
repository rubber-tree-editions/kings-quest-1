#include "vars.txt"

#define PRI_DOOR 9

#define rfRAT_IS_HERE rf0
#define rfRAT_CAUGHT_YOU rf1
#define rfWARNED_RAT_CAN_SMELL_YOU rf2
#define rfDOOR_OPEN rf3
#define rfRAT_LEFT rf4
#define rfPLAYED_SND_VILLAIN rf5
#define rfPLAYED_SND_RAT rf6

#define rvRAT_STEP_SIZE rv0
#define rvCOUNTDOWN_TO_SND_RAT rv1

#define oRAT o1
#define oDOOR o2

#if RUBBER_TREE
if (fUNDO && fGAME_OVER) {
  set(fRESTORE_STATE);
  reset(fGAME_OVER);
  reset(fGAME_OVER_PREPARATION);
  reset(fUNDO);
  if (vBIG_UNDO == BIGUNDO_CONDOR) {
    new.room(ROOM22_CONDOR_AND_CAVE);
    return;
  }
  else if (vBIG_UNDO == BIGUNDO_HOLE) {
    new.room(ROOM48_LEPRECHAUN_HOLE);
    return;
  }
  erase(oEGO);
  erase(oRAT);
  set.view(oEGO, 0);
  set.loop(oEGO, 1);
  stop.motion(oEGO);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  position(oRAT, 36, 127);
  vEGO_DIR = DIR_WEST;
  set(rfRAT_IS_HERE);
  follow.ego(oRAT, RAT_REACH_DISTANCE, rfRAT_CAUGHT_YOU);
  draw(oEGO);
  draw(oRAT);
  player.control();
  return;
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
  if (vBIG_UNDO == BIGUNDO_NONE) {
    set(fSTORE_STATE);
  }
  #endif
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_DOOR_OPENING);
  }
  if (vPREV_ROOM_NO == ROOM74_CAVE_CORNER) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vTEMP_X += 150;
    #if RUBBER_TREE
      vRESTORE_X = vTEMP_X;
      vRESTORE_Y = vTEMP_Y;
    #endif
    position.v(oEGO, vTEMP_X, vTEMP_Y);
  }
  if (vPREV_ROOM_NO == ROOM76_LEPRECHAUN_ANTECHAMBER) {
    if (BUILD_TARGET == APPLE_IIGS) {
      load.sound(SND_DOOR_CLOSED);
    }
    position(oEGO, 34, 125);
    #if RUBBER_TREE
      vRESTORE_X = 34;
      vRESTORE_Y = 125;
    #endif
  }
  ignore.objs(oEGO);
  load.view(95);
  if (!fRAT_IS_GONE) {
    if (BUILD_TARGET != APPLE_IIGS) {
      load.sound(SND_VILLAIN);
    }
    load.view(45);
    animate.obj(oRAT);
    set.view(oRAT, 45);
    position(oRAT, 36, 127);
    set(rfRAT_IS_HERE);
    follow.ego(oRAT, RAT_REACH_DISTANCE, rfRAT_CAUGHT_YOU);
    observe.blocks(oRAT);
    rvRAT_STEP_SIZE = 3;
    step.size(oRAT, rvRAT_STEP_SIZE);
  }
  if (BUILD_TARGET != APPLE_IIGS) {
    load.sound(SND_DOOR_OPENING);
  }
  load.view(46);
  animate.obj(oDOOR);
  set.view(oDOOR, 46);
  if (vPREV_ROOM_NO == ROOM76_LEPRECHAUN_ANTECHAMBER) {
    set.cel(oDOOR, 5);
  }
  else {
    set.cel(oDOOR, 0);
  }
  ignore.blocks(oDOOR);
  set.priority(oDOOR, PRI_DOOR);
  position(oDOOR, 25, 119);
  draw(oDOOR);
  if (rfRAT_IS_HERE) {
    draw(oRAT);
  }
  draw(oEGO);
  if (BUILD_TARGET != APPLE_IIGS) {
    show.pic();
  }
  if (vPREV_ROOM_NO == ROOM76_LEPRECHAUN_ANTECHAMBER) {
    reverse.loop(oDOOR, fMISC_END_MARKER_2);
    sound(SND_DOOR_OPENING, fMISC_END_MARKER);
  }
  else {
    stop.update(oDOOR);
  }
  if (BUILD_TARGET == APPLE_IIGS) {
    if (!fRAT_IS_GONE) {
      load.sound(SND_VILLAIN);
      load.sound(SND_RAT);
      rvCOUNTDOWN_TO_SND_RAT = 9;
      set(rfPLAYED_SND_RAT);
    }
    show.pic();
  }
}
if (BUILD_TARGET == APPLE_IIGS) {
  if (rfRAT_CAUGHT_YOU) {
    set.view(oEGO, 95);
    print("Be careful around this rat next time!  He's dangerous!!");
    wander(oRAT);
    set(fGAME_OVER);
    goto RETURN;
  }
  if (!fRAT_IS_GONE && rfPLAYED_SND_VILLAIN) {
    rvCOUNTDOWN_TO_SND_RAT--;
    if (rvCOUNTDOWN_TO_SND_RAT == 1) {
      random(5, 10, rvCOUNTDOWN_TO_SND_RAT);
      random(1, 8, vTEMP_RESULT);
      if (vTEMP_RESULT == 2 && rfPLAYED_SND_RAT) {
        sound(SND_RAT, rfPLAYED_SND_RAT);
      }
    }
  }
}
if (fMISC_END_MARKER_2) {
  reset(fMISC_END_MARKER_2);
  stop.update(oDOOR);
  if (BUILD_TARGET == APPLE_IIGS && vPREV_ROOM_NO == ROOM76_LEPRECHAUN_ANTECHAMBER) {
    sound(SND_DOOR_CLOSED, fMISC_END_MARKER);
  }
}
if (rfRAT_IS_HERE) {
  reset(rfRAT_IS_HERE);
  if (BUILD_TARGET == APPLE_IIGS) {
    sound(SND_VILLAIN, rfPLAYED_SND_VILLAIN);
  }
  else {
    sound(SND_VILLAIN, fMISC_END_MARKER);
  }
}
if (posn(oEGO, 0, 0, 54, 167)) {
  observe.blocks(oEGO);
}
else {
  ignore.blocks(oEGO);
}
#if BUILD_TARGET == APPLE_IIGS
if (fEGO_INVISIBLE && !rfWARNED_RAT_CAN_SMELL_YOU && !fRAT_IS_GONE) {
#else
if (fEGO_INVISIBLE && !rfWARNED_RAT_CAN_SMELL_YOU) {
#endif
  set(rfWARNED_RAT_CAN_SMELL_YOU);
  print("Being invisible won't hide you from this rat's keen sense of smell.");
}
if (fRAT_IS_GONE) {
  if (said("anyword", "rat") || said("anyword", "mouse")) {
    print("There is no rat here.");
  }
  if (said("examine", "cave") || said("examine", "room")) {
    print("It is very dark and dreary in here.");
  }
}
if (!fRAT_IS_GONE) {
  distance(oEGO, oRAT, vTEMP_RESULT_2);
  if (said("examine", "rat")) {
    print("It is a big rat with dripping teeth and small, greedy eyes.");
  }
  if (said("examine", "cave") || said("examine", "room")) {
    print("It is very dark and dreary.  The rat seems to like it here.");
  }
  if (said("examine", "mouse")) {
    print("This is no mouse, this is a big, ugly rat!");
  }
  if (said("pet", "rat")) {
    print("What!  Are you crazy?");
  }
  if (said("murder", "rat")) {
    print("You can't.  He moves too quickly.");
  }
  if (said("talk", "rat") || said("hello") || said("say", "hello")) {
    print("The rat rasps, \"Treasure!  Treasure!  Give treasure, or no go door!  Give treasure NOW!\"");
  }
  if (said("give", "treasure")
    || said("give", "rat", "treasure")
    || said("give", "treasure", "rat")
  ) {
    print("What kind of treasure would you like to give the  greedy rat?");
  }
  if (!fCARRYING_DIAMONDS) {
    if (said("give", "bag of jewels")
      || said("give", "rat", "bag of jewels")
      || said("give", "bag of jewels", "rat")
    ) {
      if (fCARRYING_POUCH && vTEMP_RESULT_2 < 35) {
        print("The rat doesn't want a tattered pouch.");
      }
      else {
        print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
      }
    }
  }
  if (fCARRYING_DIAMONDS) {
    if (said("give", "pouch of diamonds")
      || said("give", "rat", "pouch of diamonds")
      || said("give", "pouch of diamonds", "rat")
      || said("give", "bag of jewels")
      || said("give", "rat", "bag of jewels")
      || said("give", "bag of jewels", "rat")
    ) {
      if (
        fCARRYING_DIAMONDS &&
      #if BUILD_TARGET == APPLE_IIGS
        vTEMP_RESULT_2 > 14 &&
      #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
        vTEMP_RESULT_2 > 15 &&
      #endif
       vTEMP_RESULT_2 < 35
      ) {
        print("The huge rat takes the treasure you have offered and scuttles away.");
        move.obj(oRAT, 46, 105, 0, rfRAT_LEFT);
        drop("pouch of diamonds");
        reset(fCARRYING_DIAMONDS);
        set(fDWARF_OR_TROLL_HAS_DIAMONDS);
        vSCORE -= 6;
        #if RUBBER_TREE
          vRAT_TOOK_ITEM = INV_POUCH_OF_DIAMONDS;
        #endif
      }
      else {
        print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
      }
    }
  }
  if (said("give", "golden egg")
    || said("give", "rat", "golden egg")
    || said("give", "golden egg", "rat")) {
    if (
      fCARRYING_GOLDEN_EGG &&
    #if BUILD_TARGET == APPLE_IIGS
      vTEMP_RESULT_2 > 14 &&
    #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
      vTEMP_RESULT_2 > 15 &&
    #endif
      vTEMP_RESULT_2 < 35
    ) {
      print("The huge rat takes the treasure you have offered and scuttles away.");
      move.obj(oRAT, 46, 105, 0, rfRAT_LEFT);
      drop("gold egg");
      reset(fCARRYING_GOLDEN_EGG);
      set(fDWARF_OR_TROLL_HAS_EGG);
      vSCORE -= 6;
      #if RUBBER_TREE
        vRAT_TOOK_ITEM = INV_GOLDEN_EGG;
      #endif
    }
    else {
      print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
    }
  }
  if (said("give", "walnut") || said("give", "rat", "walnut") || said("give", "walnut", "rat")) {
    if (fCARRYING_WALNUT) {
      if (
      #if BUILD_TARGET == APPLE_IIGS
        vTEMP_RESULT_2 > 14 &&
      #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
        vTEMP_RESULT_2 > 15 &&
      #endif
        vTEMP_RESULT_2 < 35
      ) {
        print("The rat doesn't want an ordinary walnut.");
      }
      else {
        print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
      }
    }
    if (fCARRYING_GOLD_WALNUT) {
      if (
      #if BUILD_TARGET == APPLE_IIGS
        vTEMP_RESULT_2 > 14 &&
      #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
        vTEMP_RESULT_2 > 15 &&
      #endif
        vTEMP_RESULT_2 < 35
      ) {
        print("The huge rat takes the treasure you have offered and scuttles away.");
        move.obj(oRAT, 46, 105, 0, rfRAT_LEFT);
        drop("gold walnut");
        reset(fCARRYING_GOLD_WALNUT);
        set(fDWARF_OR_TROLL_HAS_WALNUT);
        vSCORE -= 6;
        #if RUBBER_TREE
          vRAT_TOOK_ITEM = INV_GOLDEN_WALNUT;
        #endif
      }
      else {
        print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
      }
    }
  }
  if (said("give", "sceptre")
    || said("give", "rat", "sceptre")
    || said("give", "sceptre", "rat")
  ) {
    if (
      fCARRYING_SCEPTRE &&
    #if BUILD_TARGET == APPLE_IIGS
      vTEMP_RESULT_2 > 14 &&
    #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
      vTEMP_RESULT_2 > 15 &&
    #endif
      vTEMP_RESULT_2 < 35
    ) {
      print("The huge rat takes the treasure you have offered and scuttles away.");
      move.obj(oRAT, 46, 105, 0, rfRAT_LEFT);
      drop("sceptre");
      reset(fCARRYING_SCEPTRE);
      set(fDWARF_OR_TROLL_HAS_SCEPTRE);
      vSCORE -= 6;
      #if RUBBER_TREE
        vRAT_TOOK_ITEM = INV_SCEPTRE;
      #endif
    }
    else {
      print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
    }
  }
  if (said("give", "swiss cheese")
    || said("give", "rat", "swiss cheese")
    || said("give", "swiss cheese", "rat")
    || said("feed", "swiss cheese")
    || said("feed", "rat", "swiss cheese")
    || said("feed", "swiss cheese", "rat")
  ) {
    if (!fCARRYING_SWISS_CHEESE) {
      print("That is a good idea, but you do not have any cheese.");
    }
    if (fCARRYING_SWISS_CHEESE) {
      if (
      #if BUILD_TARGET == APPLE_IIGS
        vTEMP_RESULT_2 > 14 &&
      #elif BUILD_TARGET == DOS || BUILD_TARGET == MACINTOSH
        vTEMP_RESULT_2 > 15 &&
      #endif
        vTEMP_RESULT_2 < 35
      ) {
        print("The rat drools at the sight of the cheese... and snatches it from your hand.  You might want to count your fingers.");
        move.obj(oRAT, 46, 105, 0, rfRAT_LEFT);
        drop("cheese");
        reset(fCARRYING_SWISS_CHEESE);
        set(fRAT_ATE_CHEESE);
        vSCORE -= 2;
        vSCORE += 4;
        #if RUBBER_TREE
          vRAT_TOOK_ITEM = INV_SWISS_CHEESE;
        #endif
      }
      else {
        print("It appears you are afraid of the rat!  You must get closer to give anything to this rat!");
      }
    }
  }
}
if (rfRAT_LEFT) {
  reset(rfRAT_LEFT);
  erase(oRAT);
  set(fRAT_IS_GONE);
  print("The rat scrambles to the wall and seems to magically merge with it.");
}
if (said("count", "fingers") || said("examine", "fingers")) {
  print("You count your fingers and are relieved to see you still have all of them.");
}
if (BUILD_TARGET != APPLE_IIGS && rfRAT_CAUGHT_YOU) {
  set.view(oEGO, 95);
  print("Be careful around this rat next time!  He's dangerous!!");
  wander(oRAT);
  set(fGAME_OVER);
}
distance(oEGO, oDOOR, vTEMP_RESULT_3);
if (said("open", "doors")) {
  if (vTEMP_RESULT_3 < 25) {
    start.update(oDOOR);
    if (BUILD_TARGET == APPLE_IIGS) {
      if (!fSOUND_ENABLED) {
        print("The door slowly opens.");
      }
      end.of.loop(oDOOR, rfDOOR_OPEN);
      sound(SND_DOOR_OPENING, fMISC_END_MARKER);
    }
    else {
      sound(SND_DOOR_OPENING, fMISC_END_MARKER);
      print("The door slowly opens.");
      end.of.loop(oDOOR, rfDOOR_OPEN);
    }
  }
  else {
    print("Get closer if you want to try.");
  }
}
#if BUILD_TARGET == APPLE_IIGS
if (rfDOOR_OPEN && fMISC_END_MARKER) {
#else
if (rfDOOR_OPEN) {
#endif
  new.room(ROOM76_LEPRECHAUN_ANTECHAMBER);
}
if (said("close", "doors")) {
  print("It is already closed.");
}
if (said("examine", "doors")) {
  print("The door is closed.");
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM74_CAVE_CORNER);
}
RETURN:
  return;

#message 1 "Being invisible won't hide you from this rat's keen sense of smell."
#message 2 "There is no rat here."
#message 3 "It is a big rat with dripping teeth and small, greedy eyes."
#message 4 "It is very dark and dreary.  The rat seems to like it here."
#message 5 "It is very dark and dreary in here."
#message 6 "This is no mouse, this is a big, ugly rat!"
#message 7 "The door slowly opens."
#message 8 "The door is closed."
#message 9 "What!  Are you crazy?"
#message 10 "Be careful around this rat next time!  He's dangerous!!"
#message 11 "It is already open."
#message 12 "It is already closed."
#message 13 "The door appears to be stuck and won't close."
#message 14 "Get closer if you want to try."
#message 15 "You can't.  He moves too quickly."
#message 16 "The rat rasps, \"Treasure!  Treasure!  Give treasure, or no go door!  Give treasure NOW!\""
#message 17 "You have no treasure to give the rat."
#message 18 "The rat doesn't want an ordinary walnut."
#message 19 "The rat doesn't want a tattered pouch."
#message 20 "What kind of treasure would you like to give the  greedy rat?"
#message 21 "The huge rat takes the treasure you have offered and scuttles away."
#message 22 "It appears you are afraid of the rat!  You must get closer to give anything to this rat!"
#message 23 "You do not have any."
#message 24 "The rat drools at the sight of the cheese... and snatches it from your hand.  You might want to count your fingers."
#message 25 "The rat scrambles to the wall and seems to magically merge with it."
#message 26 "That is a good idea, but you do not have any cheese."
#message 27 "You count your fingers and are relieved to see you still have all of them."
#if BUILD_TARGET == APPLE_IIGS
  #message 99 "BREAK"
#endif
