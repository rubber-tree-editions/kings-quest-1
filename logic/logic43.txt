#include "vars.txt"
#include "swimming-defs.txt"

#define PT_DEAD_GOAT 105, 58

#if RUBBER_TREE
if (fUNDO && (vAQUATIC_ACTIVITY == AQUATICACTIVITY_DROWNING || fGAME_OVER)) {
  stop.sound();
  reset(fUNDO);
  reset(rfDROWNING);
  vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
  set(fRESTORE_DROWN);
  reset(fEGO_PREOCCUPIED);
  reset(fCYCLE_EGO_ANIMATION);
  reset(fEGO_ON_WATER);
  reset(fSWIMMING);
  if (vRESTORE_Y == 0) {
    vRESTORE_ROOM = ROOM42_STONE_BRIDGE;
    if (fGAME_OVER) {
      reset(fGAME_OVER);
      reset(fGAME_OVER_PREPARATION);
    }
    vEGO_DIR = DIR_NONE;
    new.room(ROOM42_STONE_BRIDGE);
    return;
  }
  erase(oEGO);
  set.view(oEGO, 0);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  draw(oEGO);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  #if RUBBER_TREE
    vRESTORE_Y = 0;
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fTENSE_SITUATION);
    set(fTHREAT_OF_SORCERER_OR_OGRE);
    set(fFALL_DANGER);
  }
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  set.horizon(46);
  load.logics(LOGIC101_SWIMMING);
  call(LOGIC101_SWIMMING);
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  if (vPREV_ROOM_NO == ROOM6_POUCH_STUMP) {
    position(oEGO, 121, 47);
  }
  ignore.blocks(oEGO);
  draw(oEGO);
  show.pic();
}
if (VERSION_MAJOR >= 2 && fSHOW_GOAT_LEFT_MESSAGE) {
  reset(fSHOW_GOAT_LEFT_MESSAGE);
  print("It seems the old goat has wandered off by himself and is no longer following you.");
}
if (said("examine", "lake")) {
  print("The clear blue lake is surrounded by a pretty meadow.");
}
if (!fEGO_ON_WATER) {
  if (posn(oEGO, 96, 82, 115, 99)
    || posn(oEGO, 100, 108, 115, 120)
    || posn(oEGO, 30, 113, 67, 134)
    || posn(oEGO, 90, 121, 112, 130)
    || posn(oEGO, 30, 135, 67, 139)
    || posn(oEGO, 90, 131, 107, 142)
  ) {
    set(fEGO_ON_WATER);
    set(fMANUAL_EGO_WATER_POS);
  }
}
if (vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE && fEGO_ON_WATER) {
  vEGO_WATER_X = 255;
  if (posn(oEGO, 115, 104, 125, 111)
    || posn(oEGO, 113, 112, 124, 119)
    || posn(oEGO, 100, 120, 122, 126)
    || posn(oEGO, 100, 127, 118, 136)
    || posn(oEGO, 100, 137, 115, 142)
  ) {
    vEGO_WATER_X = 100;
    vEGO_WATER_Y = 121;
  }
  if (posn(oEGO, 30, 131, 55, 146)) {
    vEGO_WATER_X = 25;
    vEGO_WATER_Y = 123;
  }
  if (vEGO_WATER_X == 255) {
    vEGO_WATER_X = 0;
  }
  else {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vEGO_WATER_X -= vTEMP_X;
    vEGO_WATER_Y -= vTEMP_Y;
    set(fMANUAL_EGO_WATER_POS);
  }
}
if (!fBUCKET_FILLED) {
  if (said("drink", "water") || said("get", "drink")) {
    print("It tastes very refreshing to a thirsty wanderer.");
  }
}
if (!fBUCKET_FILLED && fCARRYING_BUCKET && said("get", "water")) {
  print("The crystal clear water fills up the bucket.");
  get("water");
  set(fBUCKET_FILLED);
  if (!fFILL_BUCKET_POINTS) {
    vSCORE += 2;
  }
  set(fFILL_BUCKET_POINTS);
}
if (vEGO_EDGE_CODE != EDGE_NONE) {
  if (fGOAT_WONT_SWIM_V2) {
    reset(fGOAT_WONT_SWIM_V2);
    reset(fGOAT_COMPANION);
    set(fGOAT_IS_GONE);
    if (VERSION_MAJOR >= 2) {
      set(fSHOW_GOAT_LEFT_MESSAGE);
    }
  }
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM6_POUCH_STUMP);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM44_OUTSIDE_WOODCUTTER_COTTAGE_FRONT);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM42_STONE_BRIDGE);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM38_WILD_FLOWER_BED);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
call(LOGIC101_SWIMMING);
#if RUBBER_TREE
  if (vAQUATIC_ACTIVITY == AQUATICACTIVITY_NONE) {
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    vRESTORE_DIR = vEGO_DIR;
  }
#endif
return;

#message 1 "The crystal clear water fills up the bucket."
#message 2 "It tastes very refreshing to a thirsty wanderer."
#message 3 "The clear blue lake is surrounded by a pretty meadow."
#if VERSION_MAJOR >= 2
  #message 4 "It seems the old goat has wandered off by himself and is no longer following you."
#endif
