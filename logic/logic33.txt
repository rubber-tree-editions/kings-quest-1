#include "vars.txt"

#define PT_DEAD_GOAT 56, 127

#define rfPULLED_UNDERWATER rf0
#define rfPLAYED_SND_RUSHING_WATER rf1

#if RUBBER_TREE
if (fUNDO && (rfPULLED_UNDERWATER || fEGO_ON_WATER || vAQUATIC_ACTIVITY != AQUATICACTIVITY_NONE)) {
  reset(fUNDO);
  reset(fEGO_ON_WATER);
  reset(rfPULLED_UNDERWATER);
  reset(fMANUAL_EGO_WATER_POS);
  erase(oEGO);
  vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
  vDROWN_GAME_OVER_COUNTDOWN = 0;
  reset(fCYCLE_EGO_ANIMATION);
  stop.cycling(oEGO);
  stop.sound();
  set.view(oEGO, 0);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  if (vRESTORE_DIR == DIR_SOUTH) {
    set.loop(oEGO, 2);
  }
  else if (vRESTORE_DIR == DIR_NORTH) {
    set.loop(oEGO, 3);
  }
  else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
    set.loop(oEGO, 0);
  }
  else {
    set.loop(oEGO, 1);
  }
  draw(oEGO);
  player.control();
  if (fGAME_OVER) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    return;
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (VERSION_MAJOR >= 2) {
    set.horizon(43);
  }
  else {
    set.horizon(40);
  }
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_RUSHING_WATER);
  }
  load.logics(LOGIC101_SWIMMING);
  call(LOGIC101_SWIMMING);
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  if (vPREV_ROOM_NO == ROOM32_EAST_OF_FREE_BOWL) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_X > 103) {
      position(oEGO, 143, 166);
    }
  }
  set(fSWIMMING_IMPOSSIBLE_HERE);
  ignore.blocks(oEGO);
  draw(oEGO);
  show.pic();
  if (BUILD_TARGET == APPLE_IIGS) {
    set(rfPLAYED_SND_RUSHING_WATER);
  }
}
if (BUILD_TARGET == APPLE_IIGS && !rfPULLED_UNDERWATER && rfPLAYED_SND_RUSHING_WATER && !fEGO_ON_WATER && !fEGO_PLAYING_FIDDLE && !posn(oEGO, 132, 59, 149, 69)) {
  sound(SND_RUSHING_WATER, rfPLAYED_SND_RUSHING_WATER);
}
if (fEGO_ON_WATER && !rfPULLED_UNDERWATER) {
  set(rfPULLED_UNDERWATER);
  print("Your swimming attempt proved fatal!  The raging current drags you under....");
}
if (said("examine", "stream")) {
  print("This is a Raging River filled with boulders and rapids.");
}
if (said("examine", "across") || said("examine", "across", "stream")) {
  print("You see a lovely green meadow on the other side of the river.");
  set(fSAID_ACCEPTED_USER_INPUT);
}
if (said("examine", "rocks")) {
  print("It is a massive boulder that splits the river.");
}
if (!fBUCKET_FILLED) {
  if (said("drink", "water") || said("get", "drink")) {
    if (posn(oEGO, 0, 48, 97, 128)
      || posn(oEGO, 98, 94, 159, 167)
      || posn(oEGO, 120, 44, 159, 86)
    ) {
      print("You drink deeply of the cold river water.");
    }
    else {
      print("You are not close enough to the Raging River.");
    }
  }
}
if (!fBUCKET_FILLED && fCARRYING_BUCKET && said("get", "water")) {
  if (posn(oEGO, 0, 48, 97, 128)
    || posn(oEGO, 98, 94, 159, 167)
    || posn(oEGO, 120, 44, 159, 86)
  ) {
    print("You carefully fill the bucket at the river's edge.");
    get("water");
    set(fBUCKET_FILLED);
    if (!fFILL_BUCKET_POINTS) {
      vSCORE += 2;
    }
    set(fFILL_BUCKET_POINTS);
  }
  else {
    print("You are not close enough to the Raging River.");
  }
}
if (BUILD_TARGET == APPLE_IIGS && posn(oEGO, 118, 50, 154, 80)) {
  set(fTENSE_SITUATION);
  set(fTHREAT_OF_SORCERER_OR_OGRE);
  set(fFALL_DANGER);
}
if (!fEGO_ON_WATER) {
  if (posn(oEGO, 14, 66, 54, 107)
    || posn(oEGO, 99, 64, 145, 82)
    || posn(oEGO, 114, 113, 133, 136)
  ) {
    set(fEGO_ON_WATER);
    set(fMANUAL_EGO_WATER_POS);
  }
}
if (vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE && fEGO_ON_WATER) {
  vEGO_WATER_X = 255;
  if (posn(oEGO, 0, 64, 23, 109)) {
    vEGO_WATER_X = 5;
    vEGO_WATER_Y = 90;
  }
  if (posn(oEGO, 24, 67, 47, 108)) {
    vEGO_WATER_X = 30;
    vEGO_WATER_Y = 91;
  }
  if (posn(oEGO, 47, 63, 77, 87)) {
    vEGO_WATER_X = 63;
    vEGO_WATER_Y = 81;
  }
  if (posn(oEGO, 47, 88, 77, 110)) {
    vEGO_WATER_X = 63;
    vEGO_WATER_Y = 96;
  }
  if (posn(oEGO, 78, 64, 98, 112)) {
    vEGO_WATER_X = 79;
    vEGO_WATER_Y = 90;
  }
  if (posn(oEGO, 99, 60, 131, 82)) {
    vEGO_WATER_X = 108;
    vEGO_WATER_Y = 78;
  }
  if (posn(oEGO, 132, 40, 159, 80)) {
    vEGO_WATER_X = 134;
    vEGO_WATER_Y = 69;
  }
  if (posn(oEGO, 95, 88, 145, 130)) {
    vEGO_WATER_X = 110;
    vEGO_WATER_Y = 115;
  }
  if (posn(oEGO, 98, 131, 147, 167)) {
    vEGO_WATER_X = 122;
    vEGO_WATER_Y = 157;
  }
  if (vEGO_WATER_X == 255) {
    vEGO_WATER_X = 0;
  }
  else {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vEGO_WATER_X -= vTEMP_X;
    vEGO_WATER_Y -= vTEMP_Y;
    set(fMANUAL_EGO_WATER_POS);
  }
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM48_LEPRECHAUN_HOLE);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM40_GNOME_LEAN_TO);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM34_PEBBLY_RIVER_BANK);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM32_EAST_OF_FREE_BOWL);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
call(LOGIC101_SWIMMING);
#if RUBBER_TREE
  if (!fEGO_ON_WATER && !rfPULLED_UNDERWATER && vAQUATIC_ACTIVITY == AQUATICACTIVITY_NONE) {
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
    vRESTORE_DIR = vEGO_DIR;
  }
#endif
return;

#message 1 "This is a Raging River filled with boulders and rapids."
#message 2 "You see a lovely green meadow on the other side of the river."
#message 3 "It is a massive boulder that splits the river."
#message 4 "You carefully fill the bucket at the river's edge."
#message 5 "You drink deeply of the cold river water."
#message 6 "You have no container for water."
#message 7 "You are not close enough to the Raging River."
#message 8 "Your swimming attempt proved fatal!  The raging current drags you under...."
