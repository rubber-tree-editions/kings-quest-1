#include "vars.txt"

#define rfFALLEN_OFF_SCREEN rf0
#define rfFALLING_SOUND_PLAYING rf1

#if RUBBER_TREE
if (fUNDO && fFALLING_TO_EARTH) {
  stop.sound();
  reset(fUNDO);
  if (vRESTORE_ROOM != vROOM_NO) {
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    vGAME_OVER_MESSAGE_COUNTDOWN = 40;
    new.room.v(vRESTORE_ROOM);
    return;
  }
  erase(oEGO);
  vRESTORE_ROOM = 0;
  set(fEGO_CLIMBING_BEANSTALK);
  reset(fFALLING_TO_EARTH);
  reset(rfFALLING_SOUND_PLAYING);
  reset(fGAME_OVER);
  reset(fGAME_OVER_PREPARATION);
  set.view(oEGO, 74);
  position.v(oEGO, vRESTORE_X, vRESTORE_Y);
  vEGO_DIR = 0;
  vTEMP_STEP_SIZE = EGO_STEP_SIZE;
  step.size(oEGO, vTEMP_STEP_SIZE);
  reset(fCYCLE_EGO_ANIMATION);
  player.control();
  draw(oEGO);
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (VERSION_MAJOR >= 2) {
    set.horizon(30);
  }
  load.view(105);
  load.view(74);
  load.sound(SND_FALLING);
#if RUBBER_TREE
  if (vRESTORE_ROOM == vROOM_NO) {
    vRESTORE_ROOM = 0;
    set(fEGO_CLIMBING_BEANSTALK);
    reset(fFALLING_TO_EARTH);
    reset(fGAME_OVER);
    reset(fGAME_OVER_PREPARATION);
    set.view(oEGO, 74);
    position.v(oEGO, vRESTORE_X, vRESTORE_Y);
    vEGO_DIR = 0;
    reset(fCYCLE_EGO_ANIMATION);
    player.control();
  }
  else
#endif
  {  
    if (vPREV_ROOM_NO == ROOM72_CLOUDS_LEFTMOST_WITH_BEANSTALK && fEGO_CLIMBING_BEANSTALK) {
      set.view(oEGO, 74);
      position(oEGO, 66, 40);
      move.obj(oEGO, 66, 40, 0, fMISC_END_MARKER);
      #if RUBBER_TREE
        vRESTORE_X = 66;
        vRESTORE_Y = 40;
      #endif
    }
    if (vPREV_ROOM_NO == ROOM72_CLOUDS_LEFTMOST_WITH_BEANSTALK && fFALLING_TO_EARTH) {
      program.control();
      set.view(oEGO, 105);
      get.posn(oEGO, vTEMP_X, vTEMP_Y);
      if (vTEMP_X > 75) {
        position(oEGO, 90, 29);
      }
      else {
        position(oEGO, 55, 29);
      }
      vTEMP_Y = 166;
      vTEMP_STEP = 4;
      set.priority(oEGO, 15);
      move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFALLEN_OFF_SCREEN);
    }
    if (vPREV_ROOM_NO == ROOM70_BEANSTALK_NEAR_BOTTOM) {
      set.view(oEGO, 74);
      position(oEGO, 69, 160);
      move.obj(oEGO, 69, 160, 0, fMISC_END_MARKER);
      #if RUBBER_TREE
        vRESTORE_X = 69;
        vRESTORE_Y = 160;
      #endif
    }
  }
  draw(oEGO);
  show.pic();
}
if (fFALLING_TO_EARTH && !rfFALLEN_OFF_SCREEN && !rfFALLING_SOUND_PLAYING) {
  sound(SND_FALLING, fSOUND_END_MARKER);
  set(rfFALLING_SOUND_PLAYING);
}
if (VERSION_MAJOR < 2 && fSOUND_END_MARKER) {
  if (!rfFALLEN_OFF_SCREEN) {
    reset(rfFALLING_SOUND_PLAYING);
    reset(fSOUND_END_MARKER);
  }
}
if (said("examine", "beanstalk")) {
  print("You can no longer see the bottom... it seems to go  on forever!");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !fFALLING_TO_EARTH) {
  #if RUBBER_TREE
    vRESTORE_ROOM = vROOM_NO;
    reset(fEGO_CLIMBING_BEANSTALK);
  #endif
  if (BUILD_TARGET == APPLE_IIGS) {
    set(fEGO_PREOCCUPIED);
    set(fFALLING);
  }
  program.control();
  print("You have to be careful when you get so high on the foliage!");
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  reset(fEGO_CLIMBING_BEANSTALK);
  set(fFALLING_TO_EARTH);
  erase(oEGO);
  set.view(oEGO, 105);
  if (vTEMP_X > 75) {
    vTEMP_X = 80;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  if (vTEMP_X < 76) {
    vTEMP_X = 65;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  start.cycling(oEGO);
  set(fCYCLE_EGO_ANIMATION);
  vTEMP_Y = 166;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFALLEN_OFF_SCREEN);
}
#if RUBBER_TREE
  if (!fFALLING_TO_EARTH) {
    get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
  }
#endif
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM72_CLOUDS_LEFTMOST_WITH_BEANSTALK);
}
if (rfFALLEN_OFF_SCREEN || posn(oEGO, 0, 166, 159, 167)) {
  new.room(ROOM70_BEANSTALK_NEAR_BOTTOM);
}
return;

#message 1 "If you were to jump or duck here you might fall."
#message 2 "You can no longer see the bottom... it seems to go  on forever!"
#message 3 "You have to be careful when you get so high on the foliage!"
