#include "vars.txt"

#define PT_DEAD_GOAT 27, 96

#define rfPULLED_UNDERWATER rf0
#define rfPLAYED_SND_RUSHING_WATER rf1

#define oPEBBLES o1

#if RUBBER_TREE
if (fUNDO) {
  if (fEGO_ON_WATER || rfPULLED_UNDERWATER || fGAME_OVER) {
    stop.sound();
    erase(oEGO);
    reset(fUNDO);
    reset(fEGO_PREOCCUPIED);
    reset(rfPULLED_UNDERWATER);
    reset(f44);
    object.on.anything(oEGO);
    vAQUATIC_ACTIVITY = AQUATICACTIVITY_NONE;
    if (vRESTORE_DIR == DIR_NORTH || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_NORTHWEST) {
      vRESTORE_Y += 3;
    }
    else if (vRESTORE_DIR == DIR_SOUTH || vRESTORE_DIR == DIR_SOUTHEAST || vRESTORE_DIR == DIR_SOUTHWEST) {
      vRESTORE_Y -= 3;
    }
    if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
      vRESTORE_X -= 3;
    }
    else if (vRESTORE_DIR == DIR_WEST || vRESTORE_DIR == DIR_NORTHWEST || vRESTORE_DIR == DIR_SOUTHWEST) {
      vRESTORE_X += 3;
    }
    if (vRESTORE_X > 105 && vRESTORE_Y < 90) {
      if (vRESTORE_Y > 63) {
        vRESTORE_Y = 63;
        if (vRESTORE_X < 130) {
          vRESTORE_X = 130;
        }
      }
    }
    reposition.to.v(oEGO, vRESTORE_X, vRESTORE_Y);
    object.on.land(oEGO);
    reset(fEGO_ON_WATER);
    reset(fCYCLE_EGO_ANIMATION);
    set.view(oEGO, 0);
    if (vRESTORE_DIR == DIR_SOUTH) {
      set.loop(oEGO, 2);
    }
    else if (vRESTORE_DIR == DIR_NORTH) {
      set.loop(oEGO, 3);
    }
    else if (vRESTORE_DIR == DIR_EAST || vRESTORE_DIR == DIR_NORTHEAST || vRESTORE_DIR == DIR_SOUTHEAST) {
      set.loop(oEGO, 0);
    }
    else {
      set.loop(oEGO, 1);
    }
    vDROWN_GAME_OVER_COUNTDOWN = 0;
    draw(oEGO);
    player.control();

    if (fGAME_OVER) {
      reset(fGAME_OVER);
      reset(fGAME_OVER_PREPARATION);
      //return;
    }
  }
}
#endif

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (BUILD_TARGET == APPLE_IIGS) {
    load.sound(SND_RUSHING_WATER);
    load.sound(SND_POINTS);
  }
  set.horizon(45);
  if (vPREV_ROOM_NO == ROOM47_MUSHROOM) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_X > 130) {
      position(oEGO, 135, 47);
    }
  }
  load.logics(LOGIC101_SWIMMING);
  call(LOGIC101_SWIMMING);
  if (fGOAT_COMPANION) {
    load.logics(LOGIC103_GOAT);
  }
  if (vROOM_YOU_KILLED_THE_GOAT_IN == vROOM_NO) {
    add.to.pic(62, 0, 0, PT_DEAD_GOAT, 0, 0);
  }
  if (!fPEBBLES_TAKEN) {
    load.view(26);
    animate.obj(oPEBBLES);
    set.view(oPEBBLES, 26);
    position(oPEBBLES, 80, 104);
    ignore.objs(oPEBBLES);
    draw(oPEBBLES);
    stop.update(oPEBBLES);
  }
  set(fSWIMMING_IMPOSSIBLE_HERE);
  ignore.blocks(oEGO);
  draw(oEGO);
  show.pic();
  if (BUILD_TARGET == APPLE_IIGS) {
    set(rfPLAYED_SND_RUSHING_WATER);
  }
}
if (BUILD_TARGET == APPLE_IIGS && !rfPULLED_UNDERWATER && rfPLAYED_SND_RUSHING_WATER && !fEGO_PLAYING_FIDDLE) {
  sound(SND_RUSHING_WATER, rfPLAYED_SND_RUSHING_WATER);
}
if (VERSION_MAJOR >= 2 && said("examine", "room")) {
  print("There are many smooth pebbles next to the river.");
}
if (said("examine", "stream") || said("examine", "beach")) {
  print("The river is dangerous and powerful.  There is a delta of pebbles on its edge.");
}
if (said("examine", "across") || said("examine", "across", "stream")) {
  print("You notice an enchanting meadow.");
  set(fSAID_ACCEPTED_USER_INPUT);
}
if (fEGO_ON_WATER && !rfPULLED_UNDERWATER) {
  set(rfPULLED_UNDERWATER);
  print("Your swimming attempt proved fatal!  The raging current drags you under... you are lost....");
}
if (vAQUATIC_ACTIVITY <= AQUATICACTIVITY_NONE && fEGO_ON_WATER) {
  vEGO_WATER_X = 255;
  if (posn(oEGO, 83, 39, 127, 60)) {
    vEGO_WATER_X = 96;
    vEGO_WATER_Y = 56;
  }
  if (posn(oEGO, 82, 61, 129, 108)) {
    vEGO_WATER_X = 109;
    vEGO_WATER_Y = 80;
  }
  if (posn(oEGO, 130, 61, 159, 108)) {
    vEGO_WATER_X = 129;
    vEGO_WATER_Y = 83;
  }
  if (vEGO_WATER_X == 255) {
    vEGO_WATER_X = 0;
  }
  else {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    vEGO_WATER_X -= vTEMP_X;
    vEGO_WATER_Y -= vTEMP_Y;
    set(fMANUAL_EGO_WATER_POS);
  }
}
if (!fBUCKET_FILLED) {
  if (said("drink", "water") || said("get", "drink")) {
    if (posn(oEGO, 77, 39, 159, 129)) {
      print("You drink deeply of the cold river water.");
    }
    else {
      print("You must venture closer to the river.");
    }
  }
}
if (!fBUCKET_FILLED && fCARRYING_BUCKET && said("get", "water")) {
  if (posn(oEGO, 77, 39, 159, 129)) {
    print("As you fill the bucket, the river almost rips it from your grasp.");
    get("water");
    set(fBUCKET_FILLED);
    if (!fFILL_BUCKET_POINTS) {
      vSCORE += 2;
    }
    set(fFILL_BUCKET_POINTS);
  }
  else {
    print("You must venture closer to the river.");
  }
}
if (said("get", "pebbles")) {
  if (fCARRYING_PEBBLES) {
    print("Your pockets cannot hold any more pebbles.");
  }
  if (!fCARRYING_PEBBLES && fPEBBLES_TAKEN) {
    print("The only pebbles worth getting are already gone.");
  }
  if (!fPEBBLES_TAKEN) {
    if (posn(oEGO, 68, 83, 109, 124)) {
      if (BUILD_TARGET == APPLE_IIGS) {
        stop.sound();
        sound(SND_POINTS, fMISC_END_MARKER);
      }
      print("You grab a handful of pebbles.");
      start.update(oPEBBLES);
      erase(oPEBBLES);
      set(fCARRYING_PEBBLES);
      set(fPEBBLES_TAKEN);
      vSCORE += 1;
      get("pebbles");
    }
    else {
      print("You are not close enough to the pebbles to get them.");
    }
  }
}
if (said("examine", "pebbles")) {
  if (!fCARRYING_PEBBLES && !fPEBBLES_TAKEN) {
    print("The pebbles are round and smooth.");
  }
  if (!fCARRYING_PEBBLES && fPEBBLES_TAKEN) {
    print("From here it looks like an ordinary delta of pebbles.");
  }
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM47_MUSHROOM);
}
if (vEGO_EDGE_CODE == EDGE_RIGHT) {
  new.room(ROOM33_RAGING_RIVER_SPLIT_BY_BOULDER);
}
if (vEGO_EDGE_CODE == EDGE_LEFT) {
  new.room(ROOM35_DWARF_MEADOW);
}
if (vEGO_EDGE_CODE == EDGE_BOTTOM) {
  new.room(ROOM31_FREE_BOWL);
}
if (fGOAT_COMPANION) {
  call(LOGIC103_GOAT);
}
#if RUBBER_TREE
if (!fEGO_ON_WATER) {
  get.posn(oEGO, vRESTORE_X, vRESTORE_Y);
  vRESTORE_DIR = vEGO_DIR;
}
#endif
call(LOGIC101_SWIMMING);
return;

#message 1 "You notice an enchanting meadow."
#message 2 "The river is dangerous and powerful.  There is a delta of pebbles on its edge."
#message 3 "Your pockets cannot hold any more pebbles."
#message 4 "As you fill the bucket, the river almost rips it from your grasp."
#message 5 "You drink deeply of the cold river water."
#message 6 "The pebbles are round and smooth."
#message 7 "You must venture closer to the river."
#message 8 "You grab a handful of pebbles."
#message 9 "The only pebbles worth getting are already gone."
#message 10 "From here it looks like an ordinary delta of pebbles."
#message 11 "You are not close enough to the pebbles to get them."
#message 12 "Your swimming attempt proved fatal!  The raging current drags you under... you are lost...."
#if VERSION_MAJOR >= 2
  #message 13 "There are many smooth pebbles next to the river."
#endif
