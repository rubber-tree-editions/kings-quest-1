#include "vars.txt"

LOGIC__HEADER

#define rfFELL_THROUGH_CLOUDS rf0
#define rfFALLING rf1
#define rfFELL_OFF_BEANSTALK rf2
#define rfPLAYING_SND_FALLING rf3

#define rvLOCK_PRIORITY rv0

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.view(74);
  load.sound(SND_FALLING);
  if (vPREV_ROOM_NO == ROOM56) {
    position(oEGO, 145, 109);
  }
  if (vPREV_ROOM_NO == ROOM71) {
    set.view(oEGO, 74);
    position(oEGO, 66, 165);
    move.obj(oEGO, 66, 165, 0, f148);
  }
  draw(oEGO);
  show.pic();
}
if (fFALLING_TO_EARTH && !rfFELL_OFF_BEANSTALK && !rfPLAYING_SND_FALLING) {
  sound(SND_FALLING, fSOUND_END_MARKER);
  set(rfPLAYING_SND_FALLING);
}
if (fSOUND_END_MARKER) {
  if (!rfFELL_OFF_BEANSTALK) {
    reset(rfPLAYING_SND_FALLING);
    reset(fSOUND_END_MARKER);
  }
}
if (!posn(oEGO, 65, 100, 84, 120) && !fEGO_CLIMBING_BEANSTALK && !fEGO_INVISIBLE) {
  if (said("climb", "beanstalk")) {
    print("You need to be closer to do that.");
  }
}
if (fEGO_INVISIBLE) {
  if (said("climb", "beanstalk")) {
    print("You can't do that when you are invisible!");
  }
}
if (fEGO_CLIMBING_BEANSTALK) {
  if (said("examine", "beanstalk")) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_Y > 140) {
      print("Your heart thuds.  Dare you climb into the clouds?");
    }
    if (vTEMP_Y < 141) {
      print("It is amazing that you actually climbed up this high!");
    }
  }
}
if (!fEGO_CLIMBING_BEANSTALK) {
  if (said("examine", "beanstalk")) {
    get.posn(oEGO, vTEMP_X, vTEMP_Y);
    if (vTEMP_X > 89) {
      print("It is hard to believe those seeds you planted grew this high.");
    }
    if (vTEMP_X < 90) {
      print("It is a long, long way down....");
    }
  }
}
if (said("examine", "clouds") || said("examine", "room")) {
  print("You are in the Land of the Clouds.  It is rumored a giant lives up here.");
}
if (fEGO_TOUCHED_TRIGGER_LINE
  && !rf1
  && !posn(oEGO, 65, 100, 82, 120)
  && !fEGO_CLIMBING_BEANSTALK
) {
  set(rfFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(f98);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvLOCK_PRIORITY);
  set.priority.v(oEGO, rvLOCK_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_THROUGH_CLOUDS);
  sound(SND_FALLING, f198);
}
if (fEGO_TOUCHED_TRIGGER_LINE && !rfFALLING && posn(oEGO, 65, 100, 82, 120) && fEGO_INVISIBLE) {
  set(rfFALLING);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(f98);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rvLOCK_PRIORITY);
  set.priority.v(oEGO, rvLOCK_PRIORITY);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_THROUGH_CLOUDS);
  sound(SND_FALLING, f198);
}
if (rfFELL_THROUGH_CLOUDS) {
  reset(rfFELL_THROUGH_CLOUDS);
  erase(oEGO);
  print("Whoops!  You fall toward a distant earth.");
  set(fGAME_OVER);
}
if (fEGO_TOUCHED_TRIGGER_LINE && posn(oEGO, 65, 100, 82, 120) && !fEGO_INVISIBLE) {
  if (fEGO_CLIMBING_BEANSTALK) {
    reset(fEGO_CLIMBING_BEANSTALK);
    erase(oEGO);
    set.view(oEGO, 0);
    position(oEGO, 85, 112);
    draw(oEGO);
    move.obj(oEGO, 85, 112, 0, f148);
    goto(FINALCHECKS);
  }
  if (!fEGO_CLIMBING_BEANSTALK) {
    set(fEGO_CLIMBING_BEANSTALK);
    erase(oEGO);
    set.view(oEGO, 74);
    position(oEGO, 70, 123);
    draw(oEGO);
    move.obj(oEGO, 70, 123, 0, f148);
  }
}
if (fEGO_TOUCHED_TRIGGER_LINE && fEGO_CLIMBING_BEANSTALK) {
  program.control();
  print("Whoops!  You fall toward a distant earth.");
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  reset(fEGO_CLIMBING_BEANSTALK);
  set(fFALLING_TO_EARTH);
  erase(oEGO);
  set.view(oEGO, 105);
  if (vTEMP_X > 75) {
    vTEMP_X = 80;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  if (vTEMP_X < 76) {
    vTEMP_X = 65;
    position.v(oEGO, vTEMP_X, vTEMP_Y);
    draw(oEGO);
  }
  start.cycling(oEGO);
  set(f98);
  vTEMP_Y = 166;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rfFELL_OFF_BEANSTALK);
}
FINALCHECKS:
  if (rfFELL_OFF_BEANSTALK || posn(oEGO, 0, 166, 159, 167)) {
    new.room(ROOM71);
  }
  if (vEGO_EDGE_CODE == EDGE_RIGHT) {
    new.room(ROOM56);
  }
  return;

LOGIC__FOOTER

