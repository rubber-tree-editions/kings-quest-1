#include "vars.txt"

LOGIC__HEADER

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.sound(SND_FALLING);
  set.horizon(40);
  if (vPREV_ROOM_NO == ROOM66_MOUNTAIN_STAIRWAY_BOTTOM) {
    position(oEGO, 9, 152);
    set.loop(oEGO, 0);
    move.obj(oEGO, 9, 152, 0, f148);
  }
  if (vPREV_ROOM_NO == ROOM68_MOUNTAIN_STAIRWAY_NEAR_TOP) {
    position(oEGO, 120, 41);
    set.loop(oEGO, 1);
    move.obj(oEGO, 120, 41, 0, f148);
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "stairway") || said("examine", "room")) {
  print("The stairs are moss-covered and slippery.");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !rf1) {
  set(rf1);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(f98);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rv0);
  set.priority.v(oEGO, rv0);
  print("Darn!  You slipped on the steep stairs.");
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rf0);
  sound(SND_FALLING, fSOUND_END_MARKER);
}
if (rf0) {
  reset(rf0);
  erase(oEGO);
  set(fGAME_OVER);
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM68_MOUNTAIN_STAIRWAY_NEAR_TOP);
}
if (posn(oEGO, 5, 150, 6, 167)) {
  new.room(ROOM66_MOUNTAIN_STAIRWAY_BOTTOM);
}
return;

LOGIC__FOOTER

