#include "vars.txt"

LOGIC__HEADER

#define rfTO_WITCHS_HOUSE rf0
#define rfTO_LEPRECHAUN_HOLE rf1

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  if (fCARRIED_BY_WITCH) {
    load.view(91);
    set.view(oEGO, 91);
    position(oEGO, 1, 60);
    move.obj(oEGO, 130, 60, 0, rfTO_WITCHS_HOUSE);
  }
  else {
    load.view(89);
    set.view(oEGO, 89);
    position(oEGO, 1, 60);
    move.obj(oEGO, 135, 60, 0, rfTO_LEPRECHAUN_HOLE);
  }
  draw(oEGO);
  show.pic();
}
if (said("examine", "view") || said("examine", "clouds") || said("examine", "room")) {
  print("The view is magnificent.");
}
if (said("anyword", "witch")) {
  print("Now is not the time to irritate the witch.");
}
if (said("anyword", "bird")) {
  print("Now is not a good time to bother the bird.");
}
if (rfTO_WITCHS_HOUSE) {
  new.room(ROOM65_IN_GINGERBREAD_HOUSE);
}
if (rfTO_LEPRECHAUN_HOLE) {
  new.room(ROOM48_LEPRECHAUN_HOLE);
}
return;

LOGIC__FOOTER

#ifndef LOGIC_AS_C
#message 1 "The view is magnificent."
#message 2 "Now is not the time to irritate the witch."
#message 3 "Now is not a good time to bother the bird."
#endif
