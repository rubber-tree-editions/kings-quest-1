#include "vars.txt"

LOGIC__HEADER

if (fROOM_SCRIPT_EXECUTED) {
  load.pic(vROOM_NO);
  draw.pic(vROOM_NO);
  discard.pic(vROOM_NO);
  load.view(105);
  load.sound(SND_FALLING);
  set.horizon(45);
  if (vPREV_ROOM_NO == ROOM19_BASE_MOUNTAIN_DOOR) {
    position(oEGO, 21, 162);
  }
  if (vPREV_ROOM_NO == ROOM67_MOUNTAIN_STAIRWAY_NEAR_BOTTOM) {
    position(oEGO, 132, 46);
    set.loop(oEGO, 1);
    move.obj(oEGO, 132, 46, 0, f148);
  }
  draw(oEGO);
  show.pic();
}
if (fGOAT_COMPANION) {
  reset(fGOAT_COMPANION);
  set(fGOAT_IS_GONE);
  print("The old goat is afraid of the dark and decides to stay outside.");
  vSCORE -= 5;
}
if (said("examine", "stairway") || said("examine", "room")) {
  print("The stairs go up into the heart of the mountain.");
}
if (fEGO_TOUCHED_TRIGGER_LINE && !rf1) {
  set(rf1);
  if (!fEGO_INVISIBLE) {
    set.view(oEGO, 105);
  }
  start.cycling(oEGO);
  set(f98);
  get.posn(oEGO, vTEMP_X, vTEMP_Y);
  get.priority(oEGO, rv0);
  set.priority.v(oEGO, rv0);
  vTEMP_Y = 165;
  vTEMP_STEP = 4;
  move.obj.v(oEGO, vTEMP_X, vTEMP_Y, vTEMP_STEP, rf0);
  sound(SND_FALLING, fSOUND_END_MARKER);
}
if (rf0) {
  reset(rf0);
  print("Oops!  Those stairs are pretty slippery, aren't they?");
  erase(oEGO);
  set(fGAME_OVER);
}
if (vEGO_EDGE_CODE == EDGE_HORIZON) {
  new.room(ROOM67_MOUNTAIN_STAIRWAY_NEAR_BOTTOM);
}
if (posn(oEGO, 16, 165, 31, 166)) {
  new.room(ROOM19_BASE_MOUNTAIN_DOOR);
}
return;

LOGIC__FOOTER

